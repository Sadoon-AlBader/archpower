# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Brad Fanella <cesura@archlinux.org>
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Graeme Gott <graeme@gottcode.org>

pkgname=xfce4-whiskermenu-plugin
pkgver=2.4.5
pkgrel=1
pkgdesc='Menu for Xfce4'
arch=(x86_64 powerpc64le)
url='https://gottcode.org/xfce4-whiskermenu-plugin/'
groups=('xfce4-goodies')
license=('GPL2')
depends=('garcon' 'xfce4-panel' 'gtk3>=3.22')
makedepends=('cmake' 'ninja')
source=("https://archive.be.xfce.org/src/panel-plugins/xfce4-whiskermenu-plugin/${pkgver%.*}/xfce4-whiskermenu-plugin-$pkgver.tar.bz2")
sha256sums=('f5241910ea6411840b8c9f9471f0d262ab0583150bb82f9b280eccbaadb0ebbe')

build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib \
		-GNinja
  	ninja
}

package() {
	DESTDIR="$pkgdir" ninja -C build install
}
