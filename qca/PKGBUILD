# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=qca
pkgver=2.2.1
pkgrel=4
pkgdesc="Qt Cryptographic Architecture"
arch=(x86_64 powerpc64le)
url="https://userbase.kde.org/QCA"
license=(LGPL)
depends=(qt5-base nss ca-certificates)
optdepends=('pkcs11-helper: PKCS-11 plugin' 'botan: botan plugin')
makedepends=(cmake doxygen pkcs11-helper botan)
conflicts=(qca-qt5)
provides=(qca-qt5)
replaces=(qca-qt5)
source=("https://download.kde.org/stable/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('d716d2d8e3ed8d95bbdb061f03081d7d032206f746a30a4d29d72196f50e7b02'
            'SKIP')
validpgpkeys=(CB9387521E1EE0127DA804843FDBB55084CC5D84) # Harald Sitter <sitter@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTS=OFF \
    -DQCA_INSTALL_IN_QT_PREFIX=ON \
    -DQCA_MAN_INSTALL_DIR=/usr/share/man
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
