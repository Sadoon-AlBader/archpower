# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer:  Jan de Groot <jgc@archlinux.org>
# Maintainer:  Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=guile
pkgver=2.0.14
pkgrel=1
pkgdesc='Portable, embeddable Scheme implementation written in C'
url='https://www.gnu.org/software/guile/'
arch=(x86_64 powerpc64le powerpc)
license=(GPL)
depends=(gmp libltdl ncurses texinfo libunistring gc libffi)

case ${CARCH} in
  powerpc) depends+=(libatomic_ops)
esac

#options=(!distcc !ccache !makeflags)
source=(https://ftp.gnu.org/pub/gnu/$pkgname/$pkgname-$pkgver.tar.gz{,.sig})
validpgpkeys=('3CE464558A84FDC69DB40CFB090B11993D9AEBB5' # Ludovic Courtès <ludo@gnu.org>
              'FF478FB264DE32EC296725A3DDC0F5358812F8F2' # Andy Wingo
              '4FD4D288D445934E0A14F9A5A8803732E4436885') # Andy Wingo <wingo@pobox.com>"
sha256sums=('8aeb2f353881282fe01694cce76bb72f7ffdd296a12c7a1a39255c27b0dfe5f1'
            'SKIP')

build() {
  cd $pkgname-$pkgver

  # fixes https://bugs.debian.org/cgi-bin/bugreport.cgi?bug=941218
  case ${CARCH} in
    powerpc)
      export CFLAGS="${CFLAGS/-O?/-Os}";
      export CXXFLAGS="${CXXFLAGS/-O?/-Os}";
    ;;
  esac

  ./configure --prefix=/usr \
    --disable-static  \
    --disable-error-on-warning \
    --build=${CHOST}
  make -j1
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install
  rm "$pkgdir"/usr/lib/libguile-2.?.so.*-gdb.scm
}
