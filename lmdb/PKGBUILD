# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Matt Monaco <cx monaco matt>

pkgname=lmdb
pkgver=0.9.25
pkgrel=1
pkgdesc="Symas Lightning Memory-Mapped Database"
arch=(x86_64 powerpc64le)
url="https://symas.com/mdb"
license=('custom:OpenLDAP')
depends=('glibc')
options=('!emptydirs')
source=("https://git.openldap.org/openldap/openldap/-/archive/LMDB_$pkgver/openldap-LMDB_$pkgver.tar.gz" lmdb.pc)
sha512sums=('557b78590f4ae4ee8ba1989bd84e11ffe7d179eafd1767c8e0ee7ce644a91514399b2f88eefe8eecbe3c53686d1e5f1acd28f3fd9b8854d26194ed650213d33c'
            '0349d4b08a1f93fe338d8f8e3e5a83e24211a46f999fe529bc1ac49c8c4975559d95a548c203d9427e3f82d62e934ba3cd1be6f734f9b9405b2a26477be4ed33')

prepare() {
  sed -e "s|@PKGVER@|$pkgver|" -i lmdb.pc
}

build() {
  cd "$srcdir"/openldap-LMDB_$pkgver/libraries/liblmdb
  make prefix=/usr
}

check() {
  cd "$srcdir"/openldap-LMDB_$pkgver/libraries/liblmdb
  make test
}

package() {
  cd "$srcdir"/openldap-LMDB_$pkgver/libraries/liblmdb
  install -dm755 "$pkgdir"/usr/{bin,lib,include,man/man1,share}
  make DESTDIR="$pkgdir" prefix=/usr install

  mv "$pkgdir"/usr/man "$pkgdir"/usr/share/man

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 "$srcdir"/lmdb.pc "$pkgdir"/usr/lib/pkgconfig/lmdb.pc
}
