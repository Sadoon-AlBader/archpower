# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=libvpx
pkgver=1.8.1
pkgrel=1
pkgdesc='VP8 and VP9 codec'
arch=(x86_64 powerpc64le)
url=http://www.webmproject.org/
license=(BSD)
makedepends=(git)
provides=(libvpx.so)
source=(git+https://chromium.googlesource.com/webm/libvpx#tag=v${pkgver})
sha256sums=(SKIP)

build() {
  cd libvpx

  ./configure \
    --prefix='/usr' \
    --disable-install-docs \
    --disable-install-srcs \
    --enable-pic \
    --enable-postproc \
    --enable-shared \
    --enable-vp8 \
    --enable-vp9 \
    --enable-vp9-highbitdepth \
    --enable-vp9-temporal-denoising \
    --target=generic-gnu
    #--enable-runtime-cpu-detect

  make
}

package() {
  cd libvpx

  make DIST_DIR="${pkgdir}"/usr install
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/libvpx/
}

# vim: ts=2 sw=2 et:
