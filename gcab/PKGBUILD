# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gcab
pkgver=1.3
pkgrel=1
pkgdesc="A GObject library to create cabinet files"
url="https://wiki.gnome.org/msitools"
arch=(x86_64 powerpc64le)
license=(LGPL)
depends=(glib2)
makedepends=(gobject-introspection vala zlib git gtk-doc meson)
_commit=8ed3cfb07293bab3e075b800b86bf5accedbc635  # tags/v1.3^0
source=("git+https://gitlab.gnome.org/GNOME/gcab.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
