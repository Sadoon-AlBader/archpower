# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-mako
pkgname=('python-mako' 'python2-mako')
pkgver=1.0.9
pkgrel=1
pkgdesc="A super-fast templating language that borrows the best ideas from the existing templating languages"
arch=('any')
url="http://www.makotemplates.org/"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-markupsafe' 'python2-markupsafe'
             'python-beaker' 'python2-beaker')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-mock' 'python2-mock'
              'python-babel' 'python2-babel' 'python-pygments' 'python2-pygments'
              'python-dogpile.cache' 'python2-dogpile.cache')
source=("https://pypi.io/packages/source/M/Mako/Mako-$pkgver.tar.gz"{,.asc})
sha512sums=('2b40b96edba9f1ac0c2090e5677742c1f7578d5a55ab1c09aa4198493144e30c25d6179a7bdee2d3fa12410da6c3b1105d2d70af77d4265d679f577a7e711c63'
            'SKIP')
validpgpkeys=('83AF7ACE251C13E6BB7DEFBD330239C1C4DAFEE1')  # Michael Bayer

prepare() {
  cp -a Mako-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/Mako-$pkgver
  python setup.py build

  cd "$srcdir"/Mako-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/Mako-$pkgver
  python setup.py ptr

  cd "$srcdir"/Mako-$pkgver-py2
  python2 setup.py ptr
}

package_python-mako() {
  depends=('python-markupsafe' 'python-beaker' 'python-setuptools')

  cd Mako-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-mako() {
  depends=('python2-markupsafe' 'python2-beaker' 'python2-setuptools')

  cd Mako-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/mako-render{,2}
}
