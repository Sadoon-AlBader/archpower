# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: Andreas Zwinkau
# Contributor: rabyte <rabyte__gmail>
# Contributor: Stefan Husmann < stefan-husmann@t-online.de>

pkgname=ttf-liberation
pkgver=2.1.3
pkgrel=1
pkgdesc='Font family which aims at metric compatibility with Arial, Times New Roman, and Courier New'
arch=('any')
url=https://github.com/liberationfonts/liberation-fonts
license=('custom:OFL')
makedepends=('fontforge' 'python-fonttools')
provides=('ttf-font')
source=("$url/archive/$pkgver/liberation-fonts-$pkgver.tar.gz")
b2sums=('534e1018ba9c22b9fd9c71c9adfd8702feea2b4f04d5377079d0972b363faee771b2125590e7ad80f88424b4d911fbc24b85db53e1c8eee87d285e6f661bccc9')

build() {
  cd liberation-fonts-$pkgver
  make build
}

package() {
  cd liberation-fonts-$pkgver
  install -Dm644 -t "$pkgdir/usr/share/fonts/${pkgname#ttf-}" \
    liberation-fonts-ttf-$pkgver/*.ttf
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

