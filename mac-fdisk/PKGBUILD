# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
pkgname=mac-fdisk
pkgver=0.1
pkgrel=18
pkgdesc="Partition table editor for Apple partition tables"
arch=(powerpc)
url="https://github.com/glaubitz/mac-fdisk-debian"
license=('GPL')
_commit='bda743065fa2c75a83fec60166bc2e317059ef7a'
source=("${pkgname}::git+https://github.com/glaubitz/mac-fdisk-debian#commit=${_commit}"
	mac-fdisk-dirty-glibc.patch)
sha256sums=('SKIP'
	    'edd663118c6d36f888766a2136d78bc5423eca9e6b77105f39e3c3da7c60b90a')

prepare() {
	cd "$pkgname"
	patch -Np1 -i ${srcdir}/mac-fdisk-dirty-glibc.patch
}

build() {
	cd "$pkgname"
	make CFLAGS+=' -fcommon'
}

package() {
	cd "$pkgname"
	make DESTDIR="$pkgdir/" install
}
