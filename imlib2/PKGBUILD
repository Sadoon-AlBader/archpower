# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Arjan Timmerman <arjan.archlinux.org>
# Contributor: Tom Newsom <Jeepster.gmx.co.uk>

pkgname=imlib2
pkgver=1.7.0
pkgrel=1
pkgdesc="Library that does image file loading and saving as well as rendering, manipulation, arbitrary polygon support"
url="https://sourceforge.net/projects/enlightenment/"
arch=(x86_64 powerpc64le powerpc)
license=('BSD')
depends=('libtiff' 'giflib' 'bzip2' 'freetype2' 'libxext' 'libpng' 'libid3tag' 'libjpeg-turbo' 'libwebp')
source=(https://downloads.sourceforge.net/enlightenment/$pkgname-$pkgver.tar.bz2
        be.patch)

prepare() {
  cd "${pkgname}-${pkgver}"

  patch -Np0 -i ${srcdir}/be.patch

  sed -i 's/@my_libs@//' imlib2-config.in
}


build() {
  cd "${pkgname}-${pkgver}"

  [ $CARCH = "i686" ] && EXTRAOPTS="--enable-mmx"
  [ $CARCH = "x86_64" ] && EXTRAOPTS="--enable-amd64"

  ./configure --prefix=/usr \
              --sysconfdir=/etc/imlib2 \
              --x-libraries=/usr/lib $EXTRAOPTS
  make
}


check() {
  cd "${pkgname}-${pkgver}"
  make check
}


package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # Install License
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
sha512sums=('078126269565b55f4b167608ee65d558711e37c34283fb6193752b27f7fc59c738cbd38839b17203273defee20c2ffc41a495e44364089deebffbd39c67e6d72'
            '54190fbff27c2e7881964285261d71cd03302289b2617d5b35d2beb9422e26602f4c96ba12e8f1fae33d946e2c40fcb4deccc3137b4066e7e5fc7572450bf903')
