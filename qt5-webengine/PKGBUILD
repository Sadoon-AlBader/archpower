# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt5-webengine
_qtver=5.13.2
pkgver=${_qtver/-/}
pkgrel=5
arch=(x86_64 powerpc64le)
url='https://www.qt.io'
license=('LGPL3' 'LGPL2.1' 'BSD')
pkgdesc='Provides support for web applications using the Chromium browser project'
depends=('qt5-webchannel' 'qt5-location' 'libxcomposite' 'libxrandr' 'pciutils' 'libxss' 
         'libevent' 'snappy' 'nss' 'libxslt' 'minizip' 'ffmpeg' 're2' 'libvpx')
makedepends=('python2' 'gperf' 'jsoncpp' 'ninja' 'qt5-tools' 'poppler')
groups=('qt' 'qt5')
_pkgfqn="${pkgname/5-/}-everywhere-src-${_qtver}"
source=("https://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz"
	0001-webengine-ppc64.patch
	0002-chromium-ppc64le.patch
	0003-chromium-v8-elfv2.patch
	0004-chromium-sockios.patch)
sha256sums=('adcf56b5de6f34744bba2307b696fc75611884f4270e87dfa760d6e99dd711bb'
            'b483224d9a5bd71284453d37aaee776c6a7cd7546063b031722d29ad1edfcf8f'
            'd482f3eb4f79d477363418bb79afddc23859d21a939566dfe8810a07d0aaee7d'
            'e6529d85bff7d772f624874845f022f5fa97dde4dcba063455935d0f3a75bbaf'
            '45ab2c43b8ae5f625e2a287dcea60b349669b9bb0ffa65248d569d3d2aabcecf')
prepare() {
  mkdir -p build

  cd $_pkgfqn
  patch -p1 -i ../0001-webengine-ppc64.patch
  patch -p1 -i ../0002-chromium-ppc64le.patch
  patch -p1 -i ../0003-chromium-v8-elfv2.patch
  patch -p1 -i ../0004-chromium-sockios.patch
}

build() {
  cd build

  qmake ../${_pkgfqn} -- \
    -proprietary-codecs \
    -system-ffmpeg \
    -webp \
    -spellchecker \
    -webengine-icu
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -Dm644 "$srcdir"/${_pkgfqn}/src/3rdparty/chromium/LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE.chromium
}
