# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt5-webengine
_qtver=5.15.1
pkgver=${_qtver/-/}
pkgrel=3
arch=(x86_64 powerpc64le)
url='https://www.qt.io'
license=('LGPL3' 'LGPL2.1' 'BSD')
pkgdesc='Provides support for web applications using the Chromium browser project'
depends=('qt5-webchannel' 'qt5-location' 'libxcomposite' 'libxrandr' 'pciutils' 'libxss' 
         'libevent' 'snappy' 'nss' 'libxslt' 'minizip' 'ffmpeg' 're2' 'libvpx' 'krb5' 'ttf-font')
makedepends=('python2' 'gperf' 'jsoncpp' 'ninja' 'qt5-tools' 'poppler' 'libpipewire02')
optdepends=('libpipewire02: WebRTC desktop sharing under Wayland')
groups=('qt' 'qt5')
_pkgfqn="${pkgname/5-/}-everywhere-src-${_qtver}"
source=("https://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz"
         qtbug-86752.patch::"https://code.qt.io/cgit/qt/qtwebengine-chromium.git/patch/?id=4e828b3b"
         0001-Use-_fpstate-instead-of-_libc_fpstate-on-linux.patch
         0018-sandbox-membarrier.patch
         0020-msgvec-push_back.patch
         0021-sandbox-sched_getparam.patch
         0089-enable-ppc64.patch
         0090-qtwebengine-ppc64.patch
         0100-sndio.patch
         0102-sndio.patch
         0999-size_t-fix.patch)
sha256sums=('f903e98fe3cd717161252710125fce011cf882ced96c24968b0c38811fbefdf2'
            'b9fde4d76a031c4bdba19b628ff9435647f54915546d977869568093f361d1b9'
            'db912bb3af0e75299a050bb6a00fa9f1483805f2642181f5a6c95f10e998c897'
            'da58d2214d1c50a7b784e18a97473c9086657d5f43e8640675e98451859307d1'
            'd13e6ad37d36834867d57e23c339b73053983dafa76c7377999ff2a5b17332c3'
            '8ec43b866f6a5e4ac0f3739a59f2e6c54ae7d2a2719a5b9102d7d2ec7053bc7f'
            '83dec2057fc6e8d2924be5d9dcf31f3f75c829a7fdc9251e126417236e674312'
            'e2644664040dfc6642d36aac0422e8b1428de22f6e779e26a7a5bafe86acdbcb'
            '8a44084968011a7f10dff4ec4df8fc46c0caeede8796f24c1b7913c3d67b8c40'
            '42e5c2b7af492188eb51365c4f25605b4856a90f5fce0b6f16d884005eef78c4'
            '581ee65a1e68532a78a32d1609b63cf7f8b5a89aa08fe2fe6063e0c58d17614a')

prepare() {
  mkdir -p build

  patch -d $_pkgfqn/src/3rdparty -p1 -i "$srcdir"/qtbug-86752.patch # https://bugreports.qt.io/browse/QTBUG-86752

  # thanks Voidlinux!
  cd ${_pkgfqn}
  patch -Np1 -i ${srcdir}/0001-Use-_fpstate-instead-of-_libc_fpstate-on-linux.patch
  patch -Np1 -i ${srcdir}/0018-sandbox-membarrier.patch
  patch -Np1 -i ${srcdir}/0020-msgvec-push_back.patch
  patch -Np1 -i ${srcdir}/0021-sandbox-sched_getparam.patch
  patch -Np1 -i ${srcdir}/0089-enable-ppc64.patch
  patch -Np1 -i ${srcdir}/0090-qtwebengine-ppc64.patch
  patch -Np1 -i ${srcdir}/0100-sndio.patch
  patch -Np1 -i ${srcdir}/0102-sndio.patch


  patch -Np2 -i ${srcdir}/0999-size_t-fix.patch
}

build() {
  cd build
  qmake ../${_pkgfqn} -- \
    -proprietary-codecs \
    -system-ffmpeg \
    -webp \
    -spellchecker \
    -webengine-icu \
    -webengine-kerberos \
    -webengine-webrtc-pipewire
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -Dm644 "$srcdir"/${_pkgfqn}/src/3rdparty/chromium/LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE.chromium
}
