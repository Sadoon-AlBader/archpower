# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=make
pkgver=4.3
pkgrel=1
pkgdesc="GNU make utility to maintain groups of programs"
arch=(x86_64 powerpc64le)
url="https://www.gnu.org/software/make"
license=('GPL3')
groups=('base-devel')
depends=('glibc' 'guile')
source=("https://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.lz"
        make-4.2.1-test-driver.patch)
md5sums=('d5c40e7bd1e97a7404f5d3be982f479a'
         'af63eff24206e1fa06af64b8ec7b07b9')

prepare() {
	cd "${pkgname}-${pkgver}"

	patch -p1 -i "${srcdir}"/make-4.2.1-test-driver.patch

	autoreconf -fi
}

build() {
	cd "${pkgname}-${pkgver}"
	./configure --prefix=/usr
	make
}

check() {
	cd "${pkgname}-${pkgver}"
	make -k check
}

package() {
	cd "${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
