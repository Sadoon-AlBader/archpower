# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: hexchain <i at hexchain.org>

pkgname=range-v3
pkgver=0.9.1
pkgrel=1
pkgdesc="Experimental range library for C++11/14/17"
arch=('any')
url='https://github.com/ericniebler/range-v3'
license=('custom')
makedepends=('cmake')
source=("https://github.com/ericniebler/range-v3/archive/${pkgver}.tar.gz")
sha512sums=('167db645527b769f3d375db63bb0a5c831e9c854bc729581a7d25c571cc1741eafb82812006c5bbe346222a73d5fbbd0b2f17119317038394ac15cf892088aa3')

prepare() {
    cd "${srcdir}/range-v3-${pkgver}"
    rm -rf build && mkdir build
}

build() {
    cd "${srcdir}/range-v3-${pkgver}"

    cd build
    cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DRANGE_V3_TESTS=OFF \
        -DRANGE_V3_HEADER_CHECKS=OFF \
        -DRANGE_V3_EXAMPLES=OFF \
        -DRANGE_V3_PERF=OFF
    make
}

package() {
    cd "${srcdir}/range-v3-${pkgver}"
    install -Dm644 "LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

    cd build
    make DESTDIR="${pkgdir}/" install
}
