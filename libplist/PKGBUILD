# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer : Tom Gundersen <teg@jklm.no>
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Gabriel Martinez < reitaka at gmail dot com >

pkgname=libplist
pkgver=2.0.0+55+g6a53de9
pkgrel=1
pkgdesc="A library to handle Apple Property List format whereas it's binary or XML"
url="https://libimobiledevice.org/"
arch=(x86_64 powerpc64le)
license=('GPL2' 'LGPL2.1')
depends=('glib2')
makedepends=('python2' 'cython2' 'python' 'cython' 'autoconf-archive' git python-setuptools python2-setuptools)
options=('!makeflags')
_commit=6a53de92e2b5029ee293c79d481ff5fd9528f8c3  # master
source=("git+https://github.com/libimobiledevice/libplist#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

check() {
  export CC=gcc
  export CXX=g++

  cd build-py2
  make check
}

prepare() {
  export CC=gcc
  export CXX=g++

  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  export CC=gcc
  export CXX=g++

  mkdir -p build-py2
  pushd build-py2
  PYTHON=/usr/bin/python2 CYTHON=/usr/bin/cython2 ../$pkgname/configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
  popd

  mkdir -p build-py3
  pushd build-py3
  PYTHON=/usr/bin/python CYTHON=/usr/bin/cython ../$pkgname/configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
  popd
}

package() {
  export CC=gcc
  export CXX=g++

  pushd build-py2
  make DESTDIR="$pkgdir" install
  popd
  pushd build-py3/cython
  make DESTDIR="$pkgdir" install
  popd
  install -D -m644 $pkgname/cython/plist.pxd "${pkgdir}"/usr/include/plist/cython/plist.pxd
}
