# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Gilles CHAUVIN <gcnweb@gmail.com>

pkgname=live-media
pkgver=2019.05.29
pkgrel=1
pkgdesc='Set of C++ libraries for multimedia streaming'
url='http://live555.com/liveMedia'
arch=(x86_64 powerpc64le)
license=('LGPL')
depends=('gcc-libs')
options=('staticlibs')
source=(http://live555.com/liveMedia/public/live.${pkgver}.tar.gz)
sha256sums=('be06c4f3da6c956b0a258568f07489d47c3f34f5c3965f70ecc1c288b21c2622')
sha512sums=('10f641d5748af49bf9a85b2b31f6a215ccef131f257cb5e44a0c41a9d56c68f3ce757ab59be7e127f2696f03f8a797de4177e9dd2b09b1b2b695d4fbc691fe4c')

build() {
  cd live
  sed -E 's|(\$\(INCLUDES\) -I. -O2 -DSOCKLEN_T=socklen_t)|\1 -fPIC -DRTSPCLIENT_SYNCHRONOUS_INTERFACE=1|g' -i config.linux
  ./genMakefiles linux
  make
}

package() {
  cd live
  for dir in BasicUsageEnvironment groupsock liveMedia UsageEnvironment; do
    install -Dm 644 ${dir}/*.a -t "${pkgdir}/usr/lib"
    install -Dm 644 ${dir}/include/*.h* -t "${pkgdir}/usr/include/${dir}"
  done
  find testProgs -type f -perm 755 -print0|xargs -0 -I '{}' install -Dm 755 '{}' -t "${pkgdir}/usr/bin"
}

# vim: ts=2 sw=2 et:
