# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Gilles CHAUVIN <gcnweb@gmail.com>

pkgname=live-media
pkgver=2019.11.22
pkgrel=1
pkgdesc='Set of C++ libraries for multimedia streaming'
url='http://live555.com/liveMedia'
arch=(x86_64 powerpc64le)
license=('LGPL')
depends=('gcc-libs')
options=('staticlibs')
source=(http://live555.com/liveMedia/public/live.${pkgver}.tar.gz)
sha256sums=('cd10fbd2d28d0bc39c12ec57b9f6b13fab3bfc7005f5ac8f9c94a9eb34179e90')
sha512sums=('67dcd709e646cfcca3617e1c1f2996db1e1987801b0fa334c3519026611958227805087e7a58356d8a9e2272a41d16ddc7979db99b2c2200da86cdcc5b54ca86')

build() {
  cd live
  sed -E 's|(\$\(INCLUDES\) -I. -O2 -DSOCKLEN_T=socklen_t)|\1 -fPIC -DRTSPCLIENT_SYNCHRONOUS_INTERFACE=1|g' -i config.linux
  ./genMakefiles linux
  make
}

package() {
  cd live
  for dir in BasicUsageEnvironment groupsock liveMedia UsageEnvironment; do
    install -Dm 644 ${dir}/*.a -t "${pkgdir}/usr/lib"
    install -Dm 644 ${dir}/include/*.h* -t "${pkgdir}/usr/include/${dir}"
  done
  find testProgs -type f -perm 755 -print0|xargs -0 -I '{}' install -Dm 755 '{}' -t "${pkgdir}/usr/bin"
}

# vim: ts=2 sw=2 et:

