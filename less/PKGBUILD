# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: SÃ©bastien "Seblu" Luttringer <seblu@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=less
pkgver=551
pkgrel=3
pkgdesc='A terminal based program for viewing text files'
license=('GPL3')
arch=(x86_64 powerpc64le)
url='http://www.greenwoodsoftware.com/less'
depends=('glibc' 'ncurses' 'pcre')
validpgpkeys=('AE27252BD6846E7D6EAE1DD6F153A7C833235259') # Mark Nudelman
source=("http://www.greenwoodsoftware.com/$pkgname/$pkgname-$pkgver.tar.gz"
        "$pkgname-$pkgver.tar.gz.sig::http://www.greenwoodsoftware.com/$pkgname/$pkgname-$pkgver.sig")
sha256sums=('ff165275859381a63f19135a8f1f6c5a194d53ec3187f94121ecd8ef0795fe3d'
            'SKIP')

build() {
  cd $pkgname-$pkgver
  sh configure --prefix=/usr --sysconfdir=/etc --with-regex=pcre
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
