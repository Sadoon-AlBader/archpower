# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer : Sven-Hendrik Haase <svenstaro@gmail.com>
pkgname=openvdb
pkgver=7.0.0
pkgrel=5
pkgdesc='A large suite of tools for the efficient storage and manipulation of sparse volumetric data discretized on three-dimensional grids'
url='https://github.com/dreamworksanimation/openvdb'
arch=(x86_64 powerpc64le)
license=('MPL')
depends=('openexr' 'boost-libs' 'intel-tbb' 'zlib' 'jemalloc' 'blosc' 'log4cplus' 'openexr')
makedepends=('doxygen' 'boost' 'cmake' 'mesa' 'cppunit' 'glfw-x11' 'glu' 'python' 'python-numpy' 'ninja')
optdepends=('glfw: for tools'
            'glu: for tools'
            'python-numpy: python module')
source=("https://github.com/dreamworksanimation/openvdb/archive/v${pkgver}.tar.gz")
sha512sums=('f5321b22b10e192012d544cc02921d3d52f621778ec5aa21ba6d450f3e29f2fdbd76486ad390a0c008394dbc6ee4aeecc04527ef30e73adf71c1a08a021746b7')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i "s/MINIMUM_PYTHON_VERSION 2.7/MINIMUM_PYTHON_VERSION 3.7/g" CMakeLists.txt

  mkdir build
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"/build

  cmake .. \
    -GNinja \
    -DUSE_NUMPY=ON \
    -DUSE_LOG4CPLUS=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DOPENVDB_BUILD_PYTHON_MODULE=ON \
    -DOPENVDB_BUILD_DOCS=ON \
    -DOPENVDB_BUILD_UNITTESTS=OFF

  sed -i "s/isystem/I/g" build.ninja
  ninja
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"

  DESTDIR="${pkgdir}" ninja install
  mkdir -p "${pkgdir}"/usr/share/doc/
  mv "${pkgdir}"/usr/doc "${pkgdir}"/usr/share/doc/$pkgname
}

# vim:set sw=2 sts=2 et:
