# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: James Campos <james.r.campos@gmail.com>
# Contributor: BlackEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Dongsheng Cai <dongsheng at moodle dot com>
# Contributor: Masutu Subric <masutu.arch at googlemail dot com>
# Contributor: TIanyi Cui <tianyicui@gmail.com>

pkgname=nodejs
pkgver=14.12.0
pkgrel=1
pkgdesc='Evented I/O for V8 javascript'
arch=( x86_64 powerpc64le powerpc )
url='https://nodejs.org/'
license=('MIT')
depends=('openssl' 'zlib' 'icu' 'libuv' 'c-ares' 'libnghttp2') # 'http-parser' 'v8')
[ "${CARCH}" == 'powerpc' ] && depends+=('libatomic_ops')
makedepends=('python' 'procps-ng')
provides=('npm')
conflicts=('npm')
source=("nodejs-$pkgver.tar.gz::https://github.com/nodejs/node/archive/v$pkgver.tar.gz")
sha512sums=('2d65d46881893005ed12bc5334baf0536f32a7ecd313adc2633d53592be2af753f12d5400216ab6e87bb5a3d16ad2f41719f041033df2ec28e2f7ffdf50388e5')

build() {
  cd node-$pkgver

  case "${CARCH}" in
    powerpc) CPU=ppc ;;
    powerpc64le) CPU=ppc64 ;;
    x86_64) CPU=x64 ;;
  esac

  ./configure \
    --dest-cpu=${CPU} \
    --prefix=/usr \
    --with-intl=system-icu \
    --shared-openssl \
    --shared-zlib \
    --shared-libuv \
    --experimental-http-parser \
    --shared-cares \
    --shared-nghttp2
    # --shared-v8
    # --shared-http-parser

  make
}

check() {
  cd node-$pkgver
  # Expected failure: https://github.com/nodejs/node/issues/11627
  make test || warning "Tests failed"
}

package() {
  cd node-$pkgver

  make DESTDIR="$pkgdir" install

  install -D -m644 LICENSE \
    "$pkgdir"/usr/share/licenses/nodejs/LICENSE
}

# vim:set ts=2 sw=2 et:
