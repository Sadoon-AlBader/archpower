# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: James Campos <james.r.campos@gmail.com>
# Contributor: BlackEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Dongsheng Cai <dongsheng at moodle dot com>
# Contributor: Masutu Subric <masutu.arch at googlemail dot com>
# Contributor: TIanyi Cui <tianyicui@gmail.com>

pkgname=nodejs
pkgver=15.5.1
pkgrel=1
pkgdesc='Evented I/O for V8 javascript'
arch=( x86_64 powerpc64le powerpc )
url='https://nodejs.org/'
license=('MIT')
# c-ares: wait until a newer release than 1.16.1
depends=('openssl' 'zlib' 'icu' 'libuv' 'libnghttp2') # 'c-ares' 'http-parser' 'v8')
makedepends=('python' 'procps-ng')
optdepends=('npm: nodejs package manager')
source=("nodejs-$pkgver.tar.gz::https://github.com/nodejs/node/archive/v$pkgver.tar.gz")
sha512sums=('e84cb41204039d59c8697a558a2fc2c808c4781bf856d6eecf4f1ea5713feef39f343586c15832d62114b306869c72ec7669d241d0e5239357be2ed2e0b400a9')

build() {
  cd node-$pkgver

  case "${CARCH}" in
    powerpc) CPU=ppc ;;
    powerpc64le) CPU=ppc64 ;;
    x86_64) CPU=x64 ;;
  esac

  ./configure \
    --dest-cpu=${CPU} \
    --prefix=/usr \
    --with-intl=system-icu \
    --without-npm \
    --shared-openssl \
    --shared-zlib \
    --shared-libuv \
    --experimental-http-parser \
    --shared-nghttp2
    # --shared-cares
    # --shared-v8
    # --shared-http-parser

  make
}

check() {
  cd node-$pkgver
  make test || :
}

package() {
  cd node-$pkgver

  make DESTDIR="$pkgdir" install

  install -D -m644 LICENSE \
    "$pkgdir"/usr/share/licenses/nodejs/LICENSE
}

# vim:set ts=2 sw=2 et:
