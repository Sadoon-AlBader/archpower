# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkgapi
pkgver=18.12.3
pkgrel=1
pkgdesc="A KDE-based library for accessing various Google services via their public API"
url='https://www.kde.org/'
arch=(x86_64 powerpc64le)
license=(GPL LGPL FDL)
depends=(kcalcore kcontacts kio qt5-webkit)
makedepends=(extra-cmake-modules qt5-tools)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
        libre.patch) # credits to Parabola for liberating patch
sha256sums=('de0314fd83d8fa8f88e6a355c4725047d2e507e0d40f1950c8ae083c2bc21924'
            'SKIP'
            '7ca4817130bc78822798c1fc54e6800db4283da858ba2a142fc5110c48221000')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/libre.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
