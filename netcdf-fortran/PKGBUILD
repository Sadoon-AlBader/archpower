# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=netcdf-fortran
pkgver=4.5.0
pkgrel=1
pkgdesc="NetCDF fortran bindings"
arch=(x86_64 powerpc64le)
url="https://github.com/Unidata/netcdf-fortran"
license=(custom)
depends=(gcc-libs netcdf curl)
makedepends=(gcc-fortran doxygen)
checkdepends=(valgrind)
options=(!makeflags staticlibs)
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('23d60050e4bfded7c1f7ffc52b97949fa8f981dfaa138af88ba0d6e06586d8c0')

build() {
    cd ${pkgname}-${pkgver}
    ./configure \
        --prefix=/usr \
        --enable-extra-tests \
        --enable-extra-example-tests \
        --enable-large-file-tests \
        --enable-valgrind-tests
    make
}

check() {
    cd ${pkgname}-${pkgver}
    make check
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
    install -Dm644 COPYRIGHT -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
