# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=xdg-utils-cxx
pkgver=0.git20190307
pkgrel=2
_commit=6e6428a3248566ef0fd0c448fda8eb83e8835c31
pkgdesc="Implementation of the FreeDesktop specifications to be used in C++ projects"
arch=(x86_64 powerpc64le)
url="https://github.com/azubieta/xdg-utils-cxx"
license=(MIT)
depends=()
makedepends=(cmake git)
#source=($pkgname-$pkgver.tar.gz::"https://github.com/azubieta/xdg-utils-cxx/v$pkgver.tar.gz")
source=(git://github.com/azubieta/xdg-utils-cxx.git#commit=$_commit)
sha256sums=('SKIP')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS} -fPIC"
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/$pkgname/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
