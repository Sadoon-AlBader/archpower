# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=xdg-utils-cxx
pkgver=1.0.0
pkgrel=1
_commit=6e6428a3248566ef0fd0c448fda8eb83e8835c31
pkgdesc="Implementation of the FreeDesktop specifications to be used in C++ projects"
arch=(x86_64 powerpc64le)
url="https://github.com/azubieta/xdg-utils-cxx"
license=(MIT)
depends=()
makedepends=(cmake)
source=($pkgname-$pkgver.tar.gz::"https://github.com/azubieta/xdg-utils-cxx/archive/v$pkgver.tar.gz")
sha256sums=('f17cf9830850f93f4b3d86d458242be0fe37554c73d0e968944ac9730b649801')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS} -fPIC"
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/$pkgname-$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
