# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Baechler <thomas@archlinux.org>

pkgbase=nvidia
pkgname=(nvidia nvidia-dkms)
pkgver=418.67
_extramodules=extramodules-ARCH
pkgrel=9
pkgdesc="NVIDIA drivers for linux"
arch=(powerpc64le)
url="http://www.nvidia.com/"
makedepends=("nvidia-utils=${pkgver}" 'libglvnd' 'linux' 'linux-headers')
license=('custom')
options=('!strip')
_pkg="NVIDIA-Linux-ppc64le-${pkgver}"
source=("http://us.download.nvidia.com/tesla/${pkgver}/${_pkg}.run"
        fs62142.patch
	list_is_first.diff
        linux-5.1.x.patch
        linux-version.diff)
sha512sums=('e104a0ae99a1db06bf40e56f0860aa0b4355701447c4385a7f38e1f47e7d381e13645632abce88fd10774571f388997ffa241f906f6681dd27bb8dd25fae0532'
            'df949debf9fed92b3c58322c02685fb344bbfff2920557e7d55ed3f70559f48cd6199bc85e2af170b7e56797f3e9881a53eab8c411f21e75f5abec26eaa47752'
            '8d6933cfd6b7a33b6ae29b354a411ae5a8d74c2ac329e291abf01d93489ee67bd8c798188fcd33cd6b71b1cd0643b5229606ea72eb5fb6a3fb7bcb26ff2d02b5'
            '231dfcae150ffb9a18672244661530d09a307b4ccc908ad4c2b253c92f9063467a3552c6a884e0f0d212053065f28e6f44fd5bb9c35d31f80a2139bb1f410c75'
            '77195ffae25edc7f80ecea37e365e43837ba56073d2612947b296c6036589299c9ce6bd36b0211e40f4bf3cab0f55a6aac6d04d91d2250c1d898a74bfd19b5c8')
prepare() {
    sh "${_pkg}.run" --extract-only
    cd "${_pkg}"

    # Fix https://bugs.archlinux.org/task/62142
    patch -Np1 -i ${srcdir}/fs62142.patch

    # Temporary place til we get a driver that supports 5.1 OOTB
    sed -i "s/static int nv_drm_vma_fault(struct vm_fault \*vmf)/#if LINUX_VERSION_CODE < KERNEL_VERSION(5, 1, 0)\nstatic int nv_drm_vma_fault(struct vm_fault \*vmf)\n#else\nstatic vm_fault_t nv_drm_vma_fault(struct vm_fault \*vmf)\n#endif/g" "$srcdir/$_pkg/kernel/nvidia-drm/nvidia-drm-gem-nvkms-memory.c"
    patch -Np1 -i ${srcdir}/linux-5.1.x.patch


    patch -Np1 -i ${srcdir}/linux-version.diff

#    sed -i "s/static inline int list_is_first(const struct list_head \*list,/#if LINUX_VERSION_CODE < KERNEL_VERSION(5, 1, 0)\nstatic inline int list_is_first(const struct list_head \*list,/g" "$srcdir/$_pkg/kernel/common/inc/nv-list-helpers.h"
#    sed -i "s/                                const struct list_head \*head)/                                const struct list_head \*head)\n#else\nstatic inline int nv_list_is_first(const struct list_head \*list,\n                                   const struct list_head \*head)\n#endif/g" "$srcdir/$_pkg/kernel/common/inc/nv-list-helpers.h"
#    patch -Np1 -i ${srcdir}/list_is_first.diff

    cp -a kernel kernel-dkms
    cd kernel-dkms

    sed -i "s/__VERSION_STRING/${pkgver}/" dkms.conf
    sed -i 's/__JOBS/`nproc`/' dkms.conf
    sed -i 's/__DKMS_MODULES//' dkms.conf
    sed -i '$iBUILT_MODULE_NAME[0]="nvidia"\
DEST_MODULE_LOCATION[0]="/kernel/drivers/video"\
BUILT_MODULE_NAME[1]="nvidia-uvm"\
DEST_MODULE_LOCATION[1]="/kernel/drivers/video"\
BUILT_MODULE_NAME[2]="nvidia-modeset"\
DEST_MODULE_LOCATION[2]="/kernel/drivers/video"\
BUILT_MODULE_NAME[3]="nvidia-drm"\
DEST_MODULE_LOCATION[3]="/kernel/drivers/video"' dkms.conf

    # Gift for linux-rt guys
    sed -i 's/NV_EXCLUDE_BUILD_MODULES/IGNORE_PREEMPT_RT_PRESENCE=1 NV_EXCLUDE_BUILD_MODULES/' dkms.conf
}

build() {
    _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
    cd "${_pkg}"/kernel
    make SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package_nvidia() {
    pkgdesc="NVIDIA drivers for linux"
    depends=('linux' "nvidia-utils=${pkgver}" 'libglvnd')

    install -Dt "${pkgdir}/usr/lib/modules/${_extramodules}" -m644 \
      "${srcdir}/${_pkg}/kernel"/nvidia{,-modeset,-drm,-uvm}.ko

    find "${pkgdir}" -name '*.ko' -exec gzip -n {} +

    echo "blacklist nouveau" |
        install -Dm644 /dev/stdin "${pkgdir}/usr/lib/modprobe.d/${pkgname}.conf"

    install -Dt "${pkgdir}/usr/share/licenses/${pkgname}" -m644 "${srcdir}/${_pkg}/LICENSE"
}

package_nvidia-dkms() {
    pkgdesc="NVIDIA driver sources for linux"
    depends=('dkms' "nvidia-utils=$pkgver" 'libglvnd')
    optdepends=('linux-headers: Build the module for Arch kernel')
    provides=("nvidia=$pkgver")
    conflicts+=('nvidia')

    cd ${_pkg}

    install -dm 755 "${pkgdir}"/usr/src
    cp -dr --no-preserve='ownership' kernel-dkms "${pkgdir}/usr/src/nvidia-${pkgver}"

    echo "blacklist nouveau" |
        install -Dm644 /dev/stdin "${pkgdir}/usr/lib/modprobe.d/${pkgname}.conf"

    install -Dt "${pkgdir}/usr/share/licenses/${pkgname}" -m644 "${srcdir}/${_pkg}/LICENSE"
}
