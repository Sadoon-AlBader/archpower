# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jonathan Wiersma <archaur at jonw dot org>

pkgname=virt-viewer
pkgver=9.0
pkgrel=1
pkgdesc="A lightweight interface for interacting with the graphical display of virtualized guest OS."
depends=('gtk-vnc' 'libvirt' 'spice-gtk3' 'desktop-file-utils' 'libvirt-glib' 'libgovirt')
makedepends=('intltool' 'spice-protocol' 'gobject-introspection')
replaces=('virtviewer')
url="https://virt-manager.org/"
arch=(x86_64 powerpc64le)
license=('GPL')
validpgpkeys=('DAF3A6FDB26B62912D0E8E3FBE86EBB415104FDF')
source=("https://virt-manager.org/download/sources/virt-viewer/virt-viewer-$pkgver.tar.gz"{,.asc})
sha512sums=('a5901d76c3e144fa3d6e640de93ed7f1e7cbeb075c04cdbe4a8c9c09bce96104ea552484df5e84c7a69af8dddd1177123ddb81b2dac775a98ac312f3102daae2'
            'SKIP')

build() {
  cd "$srcdir/virt-viewer-$pkgver"
  ./configure --prefix=/usr --with-spice --disable-update-mimedb
  make
}

package() {
  cd "$srcdir/virt-viewer-$pkgver"
  make DESTDIR="$pkgdir" install
}
