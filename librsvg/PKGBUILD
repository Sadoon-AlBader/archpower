# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=librsvg
pkgver=2.44.13
pkgrel=1
epoch=2
pkgdesc="SVG rendering library"
url="https://wiki.gnome.org/Projects/LibRsvg"
arch=(x86_64 powerpc64le)
license=(LGPL)
depends=(gdk-pixbuf2 pango libcroco)
makedepends=(gobject-introspection vala gtk-doc git rust mesa)
_commit=031f747fbf0366d5b007f22fb27b63560e29d0cd  # tags/2.44.13^0
source=("git+https://gitlab.gnome.org/GNOME/librsvg.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  export CARGO_INCREMENTAL=1
  ./configure --prefix=/usr --disable-static --enable-vala --enable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' libtool
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  export CARGO_INCREMENTAL=1
  make DESTDIR="$pkgdir" install
}

# vim: ts=2 sw=2 et:
