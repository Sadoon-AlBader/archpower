# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Simon Lipp <sloonz+aur@gmail.com>

pkgname=fop
pkgver=2.4
pkgrel=1
pkgdesc='XSL-FO implementation in Java.'
arch=('any')
url='https://xmlgraphics.apache.org/fop/'
license=('APACHE')
depends=('java-xmlgraphics-commons' 'java-commons-logging' 'java-commons-io' 'java-batik'
         'java-avalon-framework')
makedepends=('apache-ant' 'java-environment')
source=("https://ftp-stud.hs-esslingen.de/pub/Mirrors/ftp.apache.org/dist/xmlgraphics/$pkgname/source/$pkgname-$pkgver-src.tar.gz"
        'fop.conf')
sha256sums=('1d6b323b1c4968a492a234ba1b726e78c96b91cfd871f77cb579d68729443086'
            'd2900605dc271f5938d0820ca30bdec8060724976b56e43d827157bd6975da96')
backup=('etc/fop.conf')

build() {
  cd $pkgname-$pkgver
  ant -f "$pkgname"/build.xml package
}

package() {
  cd $pkgname-$pkgver

  install -Dm0644 "$pkgname/build/fop.jar" "$pkgdir/usr/share/java/fop/fop.jar"
  install -Dm0644 "../fop.conf" "$pkgdir/etc/fop.conf"
  install -Dm0755 "$pkgname/$pkgname" "$pkgdir/usr/bin/$pkgname"
}
