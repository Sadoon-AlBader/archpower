# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Vladimir Tsanev <tsachev@gmail.com>

pkgname=hiredis
pkgver=0.14.0
pkgrel=1
pkgdesc='Minimalistic C client library for Redis'
arch=(x86_64 powerpc64le)
url="https://github.com/redis/hiredis/"
license=('BSD')
depends=('glibc')
checkdepends=('redis')
source=(https://github.com/redis/$pkgname/archive/v$pkgver/hiredis-$pkgver.tar.gz)
sha256sums=('042f965e182b80693015839a9d0278ae73fae5d5d09d8bf6d0e6a39a8c4393bd')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  make PREFIX=/usr
}

check() {
  cd "$srcdir"/$pkgname-$pkgver
  make check
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" PREFIX=/usr install

  install -Dm 644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
