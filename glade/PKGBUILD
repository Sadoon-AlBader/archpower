# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=glade
pkgver=3.36.0+1+g3907987d
pkgrel=1
pkgdesc="User Interface Builder for GTK+ applications"
url="https://glade.gnome.org/"
arch=(x86_64 powerpc64le)
license=(GPL LGPL)
depends=(gtk3 libxml2)
makedepends=(gtk-doc gobject-introspection python-gobject itstool docbook-xsl
             git webkit2gtk meson)
checkdepends=(xorg-server-xvfb)
optdepends=('devhelp: development help')
groups=(gnome-extra)
provides=(libgladeui-2.so)
_commit=3907987d318875fae551ca9a2bce97f33749d230  # glade-3-36
source=("git+https://gitlab.gnome.org/GNOME/glade.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GLADE_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build -D gtk_doc=true
  ninja -C build ${MAKEFLAGS}
}

check() {
  dbus-run-session xvfb-run \
    -s '-screen 0 1920x1080x24 -nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
