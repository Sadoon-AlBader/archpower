# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Gerardo Exequiel Pozzi <djgera@archlinux.org>

pkgbase=archiso
pkgname=('archiso' 'mkinitcpio-archiso')
pkgver=50.1
pkgrel=2
pkgdesc='Tools for creating Arch Linux live and install iso images'
arch=('any')
license=('GPL3')
url="https://gitlab.archlinux.org/archlinux/archiso"
makedepends=('git')
source=("git+https://gitlab.archlinux.org/archlinux/archiso.git#tag=v${pkgver}?signed"i
        'archiso-50-powerpc.patch')
sha256sums=('SKIP'
            'e59371f137481542a6672d00d190c870b51bdd26361b96fa4f74f14e9d9b3878')
validpgpkeys=('C7E7849466FE2358343588377258734B41C31549') # David Runge <dvzrv@archlinux.org>

prepare() {
  cd "${pkgbase}"
  patch -Np1 -i ${srcdir}/archiso-50-powerpc.patch
}

package_archiso() {
  depends=('arch-install-scripts' 'bash' 'dosfstools' 'e2fsprogs' 'libisoburn'
  'mtools' 'squashfs-tools')
  optdepends=('edk2-ovmf: for emulating UEFI with run_archiso'
              'qemu: for run_archiso')
  cd "${pkgbase}"
  make DESTDIR="${pkgdir}" install
}

package_mkinitcpio-archiso() {
  pkgdesc='Mkinitcpio hooks and scripts for archiso'
  depends=('mkinitcpio')
  cd "${pkgbase}"
  make DESTDIR="${pkgdir}" install-initcpio
}
