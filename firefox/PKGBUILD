# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Jakub Schmidtke <sjakub@gmail.com>

pkgname=firefox
pkgver=69.0.1
pkgrel=1
pkgdesc="Standalone web browser from mozilla.org"
arch=(x86_64 powerpc64le)
license=(MPL GPL LGPL)
url="https://www.mozilla.org/firefox/"
depends=(gtk3 mozilla-common libxt startup-notification mime-types dbus-glib
         ffmpeg nss ttf-font libpulse jemalloc sqlite)
makedepends=(unzip zip diffutils python2-setuptools mesa imake inetutils
             xorg-server-xvfb autoconf2.13 rust mercurial jack gtk2
             python nodejs python2-psutil llvm cbindgen clang)
optdepends=('networkmanager: Location detection via available WiFi networks'
            'libnotify: Notification integration'
            'pulseaudio: Audio support'
            'speech-dispatcher: Text-to-Speech'
            'hunspell-en_US: Spell checking, American English')
options=(!emptydirs !makeflags)
_repo=https://hg.mozilla.org/mozilla-unified
source=(https://archive.mozilla.org/pub/firefox/releases/$pkgver/source/firefox-$pkgver.source.tar.xz{,.asc}
        0001-Use-remoting-name-for-GDK-application-names.patch        
        $pkgname.desktop firefox-symbolic.svg
        firefox-powerpc-clang.patch
        reduce-rust-debuginfo-on-selected-architectures.patch
        rust-drop-dll-checksums.patch
        ppc-no-static-sizes.patch)
sha256sums=('f5f2f592b8296812d43244d6a50c0c57ad11a5324db8e4e79749545482b79033'
            'SKIP'
            'ab07ab26617ff76fce68e07c66b8aa9b96c2d3e5b5517e51a3c3eac2edd88894'
            '4a783dca1f88e003c72f32d22719a0915f3fa576adbc492240e7cc250246ce10'
            '9a1a572dc88014882d54ba2d3079a1cf5b28fa03c5976ed2cb763c93dabbd797'
            '889a5a4ebe53223955bc32ae9182e20afd514934e145062d0507e0e87e46d9fd'
            'f61b37f682ac10b524bc6d5ef81a590f6e242b7302987d1cdeab62d110571fd0'
            '0d896944d110b3b71f62c8fa5941e7d6e856015cc7ad8c98efde458ee0bedbf6'
            '644931554cbcbc612f467e24ca6eabf98ab5aa89713c27762c159155a69e6f7a')
validpgpkeys=('14F26682D0916CDD81E37B6D61B7B526D98F0353') # Mozilla Software Releases <release@mozilla.com>

# Google API keys (see http://www.chromium.org/developers/how-tos/api-keys)
# Note: These are for Arch Linux use ONLY. For your own distribution, please
# get your own set of keys. Feel free to contact foutrelis@archlinux.org for
# more information.
#_google_api_key=AIzaSyDwr302FpOSkGRpLlUpPThNTDPbXcIn_FM

# Mozilla API keys (see https://location.services.mozilla.com/api)
# Note: These are for Arch Linux use ONLY. For your own distribution, please
# get your own set of keys. Feel free to contact heftig@archlinux.org for
# more information.
#_mozilla_api_key=16674381-f021-49de-8622-3021c5942aff

prepare() {
  mkdir -p mozbuild
  cd ${pkgname}-${pkgver} 

  patch -Np1 -i ${srcdir}/0001-Use-remoting-name-for-GDK-application-names.patch
  patch -Np1 -i ${srcdir}/firefox-powerpc-clang.patch
  patch -Np1 -i ${srcdir}/ppc-no-static-sizes.patch
  patch -Np1 -i ${srcdir}/reduce-rust-debuginfo-on-selected-architectures.patch
  patch -Np1 -i ${srcdir}/rust-drop-dll-checksums.patch

  echo -n "$_google_api_key" >google-api-key
  echo -n "$_mozilla_api_key" >mozilla-api-key

  cat >.mozconfig <<END
ac_add_options --enable-application=browser
ac_add_options --prefix=/usr
ac_add_options --enable-release
ac_add_options --enable-hardening
ac_add_options --enable-optimize
ac_add_options --enable-rust-simd
ac_add_options --disable-lto
ac_add_options --enable-jemalloc
export MOZ_PGO=1
export RUSTC_OPT_LEVEL=2

# Branding
ac_add_options --enable-official-branding
ac_add_options --enable-update-channel=release
ac_add_options --with-distribution-id=org.archlinuxpower
ac_add_options --with-unsigned-addon-scopes=app,system
export MOZILLA_OFFICIAL=1
export MOZ_APP_REMOTINGNAME=${pkgname//-/}
#export MOZ_TELEMETRY_REPORTING=0
#export MOZ_REQUIRE_SIGNING=0

# Keys
#ac_add_options --with-google-location-service-api-keyfile=${PWD@Q}/google-api-key
#ac_add_options --with-google-safebrowsing-api-keyfile=${PWD@Q}/google-api-key
#ac_add_options --with-mozilla-api-keyfile=${PWD@Q}/mozilla-api-key

# System libraries
ac_add_options --with-system-nspr
ac_add_options --with-system-nss
ac_add_options --enable-system-sqlite

# Features
ac_add_options --enable-alsa
ac_add_options --enable-jack
ac_add_options --enable-startup-notification
ac_add_options --disable-crashreporter
ac_add_options --disable-gconf
ac_add_options --disable-updater
ac_add_options --disable-tests
END
}

build() {
  cd ${pkgname}-${pkgver} 
  export MOZ_NOSPAM=1
  export MOZBUILD_STATE_PATH="$srcdir/mozbuild"
  export SHELL=/bin/bash

  # LTO needs more open files
  ulimit -n 4096
  xvfb-run -a -n 97 -s "-screen 0 1600x1200x24" ./mach build
  ./mach buildsymbols
}

package() {
  export SHELL=/bin/bash

  cd ${pkgname}-${pkgver} 
  DESTDIR="$pkgdir" ./mach install
  find . -name '*crashreporter-symbols-full.zip' -exec cp -fvt "$startdir" {} +

  _vendorjs="$pkgdir/usr/lib/$pkgname/browser/defaults/preferences/vendor.js"
  install -Dm644 /dev/stdin "$_vendorjs" <<END
// Use LANG environment variable to choose locale
pref("intl.locale.requested", "");

// Use system-provided dictionaries
pref("spellchecker.dictionary_path", "/usr/share/hunspell");

// Disable default browser checking.
pref("browser.shell.checkDefaultBrowser", false);

// Don't disable our bundled extensions in the application directory
pref("extensions.autoDisableScopes", 11);
pref("extensions.shownSelectionUI", true);
END

  _distini="$pkgdir/usr/lib/$pkgname/distribution/distribution.ini"
  install -Dm644 /dev/stdin "$_distini" <<END
[Global]
id=archlinuxpower
version=1.0
about=Mozilla Firefox for Archlinux POWER 

[Preferences]
app.distributor=archlinuxpower
app.distributor.channel=$pkgname
app.partner.archlinuxpower=archlinuxpower
END

  for i in 16 22 24 32 48 64 128 256; do
    install -Dm644 browser/branding/official/default$i.png \
      "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/$pkgname.png"
  done
  install -Dm644 browser/branding/official/content/about-logo.png \
    "$pkgdir/usr/share/icons/hicolor/192x192/apps/$pkgname.png"
  install -Dm644 browser/branding/official/content/about-logo@2x.png \
    "$pkgdir/usr/share/icons/hicolor/384x384/apps/$pkgname.png"
  install -Dm644 ../firefox-symbolic.svg \
    "$pkgdir/usr/share/icons/hicolor/symbolic/apps/$pkgname-symbolic.svg"

  install -Dm644 ../$pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # Install a wrapper to avoid confusion about binary path
  install -Dm755 /dev/stdin "$pkgdir/usr/bin/$pkgname" <<END
#!/bin/sh
exec /usr/lib/$pkgname/firefox "\$@"
END

  # Replace duplicate binary with wrapper
  # https://bugzilla.mozilla.org/show_bug.cgi?id=658850
  ln -srf "$pkgdir/usr/bin/$pkgname" \
    "$pkgdir/usr/lib/$pkgname/firefox-bin"
}

# vim:set sw=2 et:
