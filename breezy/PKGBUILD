# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Adam Fontenot <adam.m.fontenot@gmail.com>

pkgname=breezy
pkgver=3.0.2.3
pkgrel=3
pkgdesc='A decentralized revision control system with support for Bazaar and Git file formats'
arch=(x86_64 powerpc64le)
url=https://www.breezy-vcs.org/
license=(GPL2)
depends=(
  python-configobj
  python-dulwich
  python-fastimport
  python-patiencediff
  python-six
)
makedepends=(
  git
  python-setuptools
)
provides=(bzr)
conflicts=(bzr)
replaces=(bzr)
source=(git+https://github.com/breezy-team/breezy.git#tag=aa1875e6a279e8c3eb9f86e143193d4967358207)
sha256sums=(SKIP)

pkgver() {
  cd breezy

  git describe --tags | sed 's/-/./g'
}

build() {
  cd breezy

  python setup.py build
}

package() {
  cd breezy

  export PYTHONHASHSEED=0
  python setup.py install --root="${pkgdir}" --install-data=usr/share --optimize=1 --skip-build
  ln -s brz "${pkgdir}"/usr/bin/bzr # backwards compatibility
}

# vim: ts=2 sw=2 et:
