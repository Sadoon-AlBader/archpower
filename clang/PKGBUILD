# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=clang
pkgver=8.0.0
pkgrel=1
pkgdesc="C language family frontend for LLVM"
arch=('x86_64' 'powerpc64le')
url="https://clang.llvm.org/"
license=('custom:University of Illinois/NCSA Open Source License')
depends=('llvm-libs' 'gcc' 'compiler-rt')
makedepends=('llvm' 'cmake' 'ninja' 'python-sphinx' 'python2' 'extra-cmake-modules')
optdepends=('openmp: OpenMP support in clang with -fopenmp'
            'python: for git-clang-format'
            'python2: for scan-view')
provides=("clang-analyzer=$pkgver" "clang-tools-extra=$pkgver")
conflicts=('clang-analyzer' 'clang-tools-extra')
replaces=('clang-analyzer' 'clang-tools-extra')
source=(https://releases.llvm.org/$pkgver/cfe-$pkgver.src.tar.xz{,.sig}
        https://releases.llvm.org/$pkgver/clang-tools-extra-$pkgver.src.tar.xz{,.sig}
        https://releases.llvm.org/$pkgver/llvm-$pkgver.src.tar.xz{,.sig}
        enable-SSP-and-PIE-by-default.patch)
sha256sums=('a45b62dde5d7d5fdcdfa876b0af92f164d434b06e9e89b5d0b1cbc65dfe3f418'
            'SKIP'
            '4c93c7d2bb07923a8b272da3ef7914438080aeb693725f4fc5c19cd0e2613bed'
            'SKIP'
            'a38dfc4db47102ec79dcc2aa61e93722c5f6f06f0a961073bd84b78fb949419b'
            'SKIP'
            '8dd3938afb93a051acf69373f7a4e1eb7cf9dd28d8e334c98d30a531d4af0181')
validpgpkeys+=('B6C8F98282B944E3B0D5C2530FC3042E345AD05D') # Hans Wennborg <hans@chromium.org>
validpgpkeys+=('474E22316ABF4785A88C6E8EA2C794A986419D8A') # Tom Stellard <tstellar@redhat.com>

prepare() {
  cd "$srcdir/cfe-$pkgver.src"
  mkdir -p build
  mv "$srcdir/clang-tools-extra-$pkgver.src" tools/extra
  #patch -Np1 -i ../enable-SSP-and-PIE-by-default.patch
}

build() {
  cd "$srcdir/cfe-$pkgver.src/build"

  cmake .. -G Ninja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPYTHON_EXECUTABLE=/usr/bin/python \
    -DBUILD_SHARED_LIBS=ON \
    -DLLVM_LINK_LLVM_DYLIB=ON \
    -DLLVM_ENABLE_RTTI=ON \
    -DLLVM_BUILD_TESTS=ON \
    -DLLVM_INCLUDE_DOCS=ON \
    -DLLVM_BUILD_DOCS=ON \
    -DLLVM_ENABLE_SPHINX=ON \
    -DSPHINX_WARNINGS_AS_ERRORS=OFF \
    -DLLVM_EXTERNAL_LIT=/usr/bin/lit \
    -DLLVM_MAIN_SRC_DIR="$srcdir/llvm-$pkgver.src" \
    -DLLVM_CMAKE_PATH="$srcdir/llvm-$pkgver.src/cmake/modules"

  RAM_TOTAL=$(free --giga | grep Mem | awk '{print $2}')
  if [ ${RAM_TOTAL} -lt 16 ]; then
    warning "Limiting to 2 parallel job due to RAM constrains"
    NINJA_ARGS="-j2"
  fi

  ninja $NINJA_ARGS
}

check() {
  cd "$srcdir/cfe-$pkgver.src/build"
  ninja check-clang{,-tools}
}

_python2_optimize() {
  python2 -m compileall "$@"
  python2 -O -m compileall "$@"
}

_python3_optimize() {
  python3 -m compileall "$@"
  python3 -O -m compileall "$@"
  python3 -OO -m compileall "$@"
}

package() {
  cd "$srcdir/cfe-$pkgver.src/build"

  DESTDIR="$pkgdir" ninja install
  install -Dm644 ../LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # Remove documentation sources
  rm -r "$pkgdir"/usr/share/doc/clang{,-tools}/html/{_sources,.buildinfo}

  # Move analyzer scripts out of /usr/libexec
  mv "$pkgdir"/usr/libexec/{ccc,c++}-analyzer "$pkgdir/usr/lib/clang/"
  rmdir "$pkgdir/usr/libexec"
  sed -i 's|libexec|lib/clang|' "$pkgdir/usr/bin/scan-build"

  # Install Python bindings
  for _py in 2.7 3.7; do
    install -d "$pkgdir/usr/lib/python$_py/site-packages"
    cp -a ../bindings/python/clang "$pkgdir/usr/lib/python$_py/site-packages/"
    _python${_py%%.*}_optimize "$pkgdir/usr/lib/python$_py"
  done

  # Fix shebang in Python 2 scripts
  sed -i '1s|/usr/bin/env python$|&2|' \
    "$pkgdir/usr/bin/scan-view" \
    "$pkgdir"/usr/share/$pkgname/*.py

  # Compile Python scripts
  _python2_optimize "$pkgdir/usr/share"
}

# vim:set ts=2 sw=2 et:
sha256sums=('084c115aab0084e63b23eee8c233abb6739c399e29966eaeccfc6e088e0b736b'
            'SKIP'
            '4f00122be408a7482f2004bcf215720d2b88cf8dc78b824abb225da8ad359d4b'
            'SKIP'
            '8872be1b12c61450cacc82b3d153eab02be2546ef34fa3580ed14137bb26224c'
            'SKIP'
            '8dd3938afb93a051acf69373f7a4e1eb7cf9dd28d8e334c98d30a531d4af0181')
