# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=fontforge
pkgver=20190801
pkgrel=2
pkgdesc='Outline and bitmap font editor'
url='https://fontforge.github.io/'
arch=(x86_64 powerpc64le)
license=('BSD')
makedepends=('git')
depends=('libtool' 'pango' 'giflib' 'libtiff' 'libxml2' 'libspiro' 'python'
         'potrace' 'woff2' 'gtk3' 'libuninameslist')
source=("https://github.com/${pkgname}/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz"
	fontforge-python-3.8.patch)
sha256sums=('d92075ca783c97dc68433b1ed629b9054a4b4c74ac64c54ced7f691540f70852'
	    '09f18742cc40321110f7fafb38fe2d5179f743c6b5952217255d04b22634a616')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -Np1 -i ${srcdir}/fontforge-python-3.8.patch
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure \
		--prefix=/usr \
		--mandir=/usr/share/man \
		--enable-python-extension \
		--enable-woff2 \
		--enable-gdk \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
