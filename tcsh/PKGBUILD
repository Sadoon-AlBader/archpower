# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=tcsh
pkgver=6.21.00
pkgrel=1
pkgdesc='Enhanced version of the Berkeley C shell.'
arch=(x86_64 powerpc64le)
url='https://www.tcsh.org/'
license=('BSD')
depends=('ncurses')
backup=('etc/csh.cshrc'
        'etc/csh.login')
install='tcsh.install'
source=("ftp://ftp.astron.com/pub/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'csh.cshrc'
        'csh.login')
md5sums=('5bd5f11515cc5cca927777fa92f9d4b9'
         '7ca0fe6d1a1b9a0093f632499d4fb112'
         '4869b9da87c79854e2cc97241f125853')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --bindir=/usr/bin
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" mandir=/usr/share/man install install.man

  install -Dm0644 "${srcdir}/csh.cshrc" "${pkgdir}/etc/csh.cshrc"
  install -Dm0644 "${srcdir}/csh.login" "${pkgdir}/etc/csh.login"
  install -Dm0644 Copyright "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  ln -s tcsh "${pkgdir}/usr/bin/csh"
  ln -s tcsh.1 "${pkgdir}/usr/share/man/man1/csh.1"
}
