# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=genxrdpattern
pkgver=1.1
pkgrel=1
pkgdesc="Uses ObjCryst++ to generate a theoretical XRD pattern"
arch=(x86_64 powerpc64le)
url="https://github.com/psavery/genXrdPattern"
license=(GPL)
depends=()
makedepends=(cmake)
source=($pkgname-$pkgver.tar.gz::"https://github.com/psavery/genXrdPattern/archive/$pkgver-linux.tar.gz"
        https://github.com/psavery/genXrdPattern/releases/download/$pkgver-linux/objcryst.tgz)
sha256sums=('9bc01dbf637d6520d8e2f5743bb1b46f8319432af5ce2d2a484a36615c1586d3'
            '5523eae9937388260b3e491a1a11a443ab00a0961318ec92eee08f9366a0953f')

prepare() {
  mkdir -p build

  sed -e 's|/user/psavery/src/objcryst/Fox/src|.|g' -i objcryst/Fox/src/Fox.dep

  sed -e 's@-march=native@@g' -i objcryst/ObjCryst/rules.mak

  if [ "${CARCH}" != 'x86_64' ]; then
    sed -e 's@-DHAVE_SSE_MATHFUN -DUSE_SSE2@@g' -i objcryst/ObjCryst/rules.mak
  fi
}

build() {
  cd objcryst/Fox
  make Fox-nogui shared=0 -j1

  cd "$srcdir"/build
  cmake ../genXrdPattern-$pkgver-linux \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DObjCryst_ROOT="$srcdir"/objcryst
  make
}

package() {
  cd build
  install -Dm755 genXrdPattern -t "$pkgdir"/usr/bin
}
