# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cbindgen
pkgver=0.14.0
pkgrel=2
pkgdesc="A tool for generating C bindings to Rust code"
url="https://github.com/eqrion/cbindgen"
arch=(x86_64 powerpc64le)
license=(MPL2)
depends=(gcc-libs)
makedepends=(cargo git)
_commit=4a38a48937264d6ec162b2fd47ffd0e7a091ca72  # tags/v0.14.0^0
source=("git+https://github.com/eqrion/cbindgen#commit=$_commit")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname

  # Unbreak Firefox
  git revert -n ec1631f3e47686e6e0c1b7641bb08b0e39c59250

  cargo fetch --locked --target ${CHOST} 
}

build() {
  cd $pkgname
  cargo build --release --frozen --all-targets
}

check() {
  cd $pkgname
  # test_expand* fails; needs nightly rust
  cargo test --release --frozen || :
}

package() {
  cd $pkgname
  install -Dt "$pkgdir/usr/bin" target/release/cbindgen
  install -Dt "$pkgdir/usr/share/doc/$pkgname" -m644 README.md
}
