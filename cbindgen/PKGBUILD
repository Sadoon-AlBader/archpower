# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cbindgen
pkgver=0.9.0
pkgrel=1
pkgdesc="A tool for generating C bindings to Rust code"
url="https://github.com/eqrion/cbindgen"
arch=(x86_64 powerpc64le)
license=(MPL2)
depends=(gcc-libs)
makedepends=(cargo git)
_commit=e19526e00b3fe6921b881682147a1fe5d6b64124  # tags/v0.9.0^0
source=("git+https://github.com/eqrion/cbindgen#commit=$_commit")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  cd $pkgname
  cargo build --release
}

check() {
  cd $pkgname
  # test_expand* fails; needs nightly rust
  cargo test --release || :
}

package() {
  cd $pkgname
  install -Dt "$pkgdir/usr/bin" target/release/cbindgen
  install -Dt "$pkgdir/usr/share/doc/$pkgname" -m644 README.md
}
