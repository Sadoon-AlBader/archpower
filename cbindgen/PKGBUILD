# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cbindgen
pkgver=0.8.3
pkgrel=1
pkgdesc="A tool for generating C bindings to Rust code"
url="https://github.com/eqrion/cbindgen"
arch=(x86_64 powerpc64le)
license=(MPL2)
depends=(gcc-libs)
makedepends=(cargo git)
_commit=ba02367253bcffb6f4dcfe4bc27005e9cae78edd  # master
source=("git+https://github.com/eqrion/cbindgen#commit=$_commit")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
  git tag -f v0.8.3 ba02367253bcffb6f4dcfe4bc27005e9cae78edd
}

build() {
  cd $pkgname
  cargo build --release
}

check() {
  cd $pkgname
  cargo test --release
}

package() {
  cd $pkgname
  install -Dt "$pkgdir/usr/bin" target/release/cbindgen
  install -Dt "$pkgdir/usr/share/doc/$pkgname" -m644 README.md
}
