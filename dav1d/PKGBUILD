# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=dav1d
pkgver=0.3.1
pkgrel=1
pkgdesc='AV1 cross-platform decoder focused on speed and correctness'
url='https://code.videolan.org/videolan/dav1d/'
arch=(x86_64 powerpc64le)
license=('BSD')
depends=('glibc')
makedepends=('meson' 'ninja' 'doxygen')
provides=('libdav1d.so')
source=(${pkgname}-${pkgver}.tar.bz2::https://code.videolan.org/videolan/dav1d/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.bz2)
sha512sums=('b810c993a2a262f75ea0d4404428bc5660ac0d4c4d41aa311bc2180abecc40b36e73858f436b27372f4a1a0b7c700df4f8cc535130c663d6bfba2206d549e0f2')

build() {
  cd ${pkgbase}-${pkgver}
  arch-meson build
  ninja -C build
}

check() {
  cd ${pkgbase}-${pkgver}/build
  meson test
}

package() {
  cd ${pkgbase}-${pkgver}
  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
