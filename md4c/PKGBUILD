# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=md4c
pkgver=0.4.4
pkgrel=1
pkgdesc="C Markdown parser"
arch=(x86_64 powerpc64le)
url="https://github.com/mity/md4c"
license=(MIT)
depends=(glibc)
makedepends=(cmake)
source=($pkgname-$pkgver.tar.gz::"https://github.com/mity/md4c/archive/release-$pkgver.tar.gz")
sha256sums=('d9545e4380c36fef198205b1efaec002a4dd352e75f104955956ecc560dcebbd')

build() {
  cmake -B build -S $pkgname-release-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 $pkgname-release-$pkgver/LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}
