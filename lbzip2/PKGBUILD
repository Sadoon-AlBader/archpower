# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Dan Ziemba <zman0900@gmail.com>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

pkgname=lbzip2
pkgver=2.5
pkgrel=4
pkgdesc="A parallel, SMP-based, bzip2-compatible compression utility"
arch=(x86_64 powerpc64le)
url="http://lbzip2.org"
license=('GPL3')
depends=('glibc')
source=(http://archive.lbzip2.org/$pkgname-$pkgver.tar.bz2
        lbzip2-gnulib-recent-libc.patch)
sha512sums=('63468178604df926bd2071c23b068bddcb6fcab54bac92a149224c5ed99a38cc7e8366ae21eecf12ad9c1a193827ef627d2517ac9a7a65791a959dfcacd96127'
	    '593462292f530c655abe07f84ec79012d3473719bd6445bb228fb3f44b7d03666a02b437fea8df9b5fc6e7f20765ebd3a2db50c29ca2440d70f4bfd7e91a3cda')

prepare() {
  cd "${srcdir}"/$pkgname-$pkgver
  patch -Np1 -i ${srcdir}/lbzip2-gnulib-recent-libc.patch
}

build() {
  cd "${srcdir}"/$pkgname-$pkgver
 
  ./configure --prefix=/usr
  make
}

check() {
  cd "${srcdir}"/$pkgname-$pkgver
  
  make check
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver
 
  make DESTDIR="${pkgdir}" install
}
