# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gjs
pkgver=1.58.5
pkgrel=1
epoch=2
pkgdesc="Javascript Bindings for GNOME"
url="https://wiki.gnome.org/Projects/Gjs"
arch=(x86_64 powerpc64le)
license=(GPL)
depends=(cairo gobject-introspection-runtime js60 dconf readline)
makedepends=(gobject-introspection git autoconf-archive sysprof)
checkdepends=(valgrind xorg-server-xvfb)
_commit=680fc377fd4f8a3b87836d6c7a1fa1b5074c4137  # tags/1.58.5^0
source=("git+https://gitlab.gnome.org/GNOME/gjs.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd build
  ../$pkgname/configure \
    --prefix=/usr \
    --libexecdir=/usr/lib \
    --disable-static \
    --enable-compile-warnings=yes
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  dbus-run-session xvfb-run \
    -s '-screen 0 1920x1080x24 -nolisten local' \
    make check -C build
}

package() {
  depends+=(libreadline.so)
  DESTDIR="$pkgdir" make install -C build
}
