# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=mkinitcpio
pkgver=27
pkgrel=5
pkgdesc="Modular initramfs image creation utility"
arch=('any')
url="https://projects.archlinux.org/mkinitcpio.git/"
license=('GPL')
depends=('awk' 'mkinitcpio-busybox>=1.19.4-2' 'kmod' 'util-linux>=2.23' 'libarchive' 'coreutils'
         'bash' 'diffutils' 'findutils' 'grep' 'filesystem>=2011.10-1' 'gzip' 'systemd' 'binutils')
optdepends=('xz: Use lzma or xz compression for the initramfs image'
            'bzip2: Use bzip2 compression for the initramfs image'
            'lzop: Use lzo compression for the initramfs image'
            'lz4: Use lz4 compression for the initramfs image'
            'mkinitcpio-nfs-utils: Support for root filesystem on NFS')
makedepends=('asciidoc')
provides=('initramfs')
backup=('etc/mkinitcpio.conf')
source=("https://sources.archlinux.org/other/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig}
        "0001-mkinitcpio-remove-preset-pacsave.patch"
        "0002-mkinitcpio-fix-builtin.patch"
        "mkinitcpio-powerpc64le-support.patch")
install=mkinitcpio.install
sha256sums=('e6bff1cb78b677538eb9aace900b715fd59de8fc210b74fb9d899dfaa32bc354'
            'SKIP'
            '845569fa760f70c868ecb3dc8ae9667287970526dddaf403fdafcb716e8b3d51'
            'd097f3df15ba9fefd3771f9f784e1c31c8d5ff1d9d6885a46fc6b5188e419796'
            'c1bb1256f08245ac01e02eea6be22a9336aa55aafcbe1060ebb4a0ea24569395')
validpgpkeys=('487EACC08557AD082088DABA1EB2638FF56C0C53'   # Dave Reisner
              '86CFFCA918CF3AF47147588051E8B148A9999C34'   # Evangelos Foutras
              'ECCAC84C1BA08A6CC8E63FBBF22FB1D78A77AEAB')  # Giancarlo Razzolini

prepare() {
  cd $pkgname-$pkgver
  # Apply 0001-mkinitcpio-remove-preset-pacsave.patch
  patch -Np1 < $srcdir/0001-mkinitcpio-remove-preset-pacsave.patch

  # Apply 0002-mkinitcpio-fix-builtin.patch
  patch -Np1 < $srcdir/0002-mkinitcpio-fix-builtin.patch

  # powerpc64le support
  patch -Np1 -i ${srcdir}/mkinitcpio-powerpc64le-support.patch
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}
