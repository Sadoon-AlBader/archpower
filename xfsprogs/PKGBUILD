# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
pkgname=xfsprogs
pkgver=5.5.0
pkgrel=1
pkgdesc="XFS filesystem utilities"
arch=(powerpc64le)
license=('LGPL')
url="https://xfs.org"
depends=('sh' 'libutil-linux' 'readline' 'icu' 'device-mapper')
makedepends=('git')
options=('!makeflags')
source=("git+https://git.kernel.org/pub/scm/fs/xfs/xfsprogs-dev.git#tag=v${pkgver}")
md5sums=('SKIP')

build() {
  cd ${pkgname}-dev
  make configure
  export OPTIMIZER="-mcpu=power8 -O1"
  export DEBUG=-DNDEBUG
  ./configure --prefix=/usr --sbindir=/usr/bin --enable-readline
  make
}

package() {
  cd ${pkgname}-dev
  make DIST_ROOT="${pkgdir}" PKG_ROOT_SBIN_DIR="/usr/bin" install install-dev
  chown -R root "$pkgdir"
  chgrp -R root "$pkgdir"
  # add hack as we cannot set rootlibdir
  mv "${pkgdir}"/lib/libhandle.so* "${pkgdir}/usr/lib/"
  rm -rf "${pkgdir}/lib"
}
