# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=pixman
pkgver=0.38.4
pkgrel=1
pkgdesc="The pixel-manipulation library for X and cairo"
arch=(x86_64 powerpc64le)
url="https://cgit.freedesktop.org/pixman/"
license=('custom')
depends=('glibc')
makedepends=('meson' 'libpng')
source=(https://xorg.freedesktop.org/releases/individual/lib/${pkgname}-${pkgver}.tar.bz2)
sha1sums=('87e1abc91ac4e5dfcc275f744f1d0ec3277ee7cd')
#validpgpkeys=('') # Maarten Lankhorst <maarten.lankhorst@linux.intel.com>

build() {
  [ "${CARCH}" = 'powerpc64le' ] && EXTRA_FLAGS='-D mmx=disabled -D sse2=disabled -D ssse3=disabled -D vmx=enabled'
  [ "${CARCH}" = 'x86_64' ] && EXTRA_FLAGS='-D vmx=disabled'

  arch-meson $pkgbase-$pkgver build \
    -D loongson-mmi=disabled \
    -D mmx=disabled \
    -D arm-simd=disabled \
    -D neon=disabled \
    -D iwmmxt=disabled \
    -D mips-dspr2=disabled \
    -D gtk=disabled ${EXTRA_FLAGS}
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgname-$pkgver/COPYING
}

# vim:set et sw=2:
