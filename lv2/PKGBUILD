# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>

pkgname=lv2
pkgver=1.16.0
pkgrel=2
pkgdesc="Plugin standard for audio systems"
url="http://lv2plug.in/"
license=('LGPL' 'custom')
arch=(x86_64 powerpc64le)
makedepends=('asciidoc' 'doxygen' 'gtk2' 'libsndfile' 'pygmentize'
'python-pygments' 'python-rdflib')
optdepends=('libsndfile: Example sampler'
            'gtk2: Example scope and sampler'
            'python-pygments: Documentation generator script'
            'python-rdflib: Documentation generator script')
source=("http://lv2plug.in/spec/${pkgname}-${pkgver}.tar.bz2"{,.sig}
        "lv2-1.16.0-fix_lv2_validate.patch::https://gitlab.com/lv2/lv2/commit/2dd45ffd8d19491407bc9f9e0b395fbdefaaacf0.patch")
sha512sums=('ead6d590cded5dd7a548d6ffe0f2f9f8efadfa7bb9e8b4fa0aea6664ccdfbb3ca697514bddebe695a9442fba5b62714b5cd45c1bf7d0aaef12ffe50972c2d88c'
            'SKIP'
            '850518c8cc0c12d0aaf6693e7c0fda0aba84d6e660bd04c389238a9889ecfcbdb990ac7db708e2cfd16c96a92b56312bf9c3d3616514e5d5ea5e92bfa88249a5')
validpgpkeys=('907D226E7E13FA337F014A083672782A9BF368F3') # David Robillard <d@drobilla.net>

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i "../lv2-1.16.0-fix_lv2_validate.patch"
}

build() {
  cd "${pkgname}-${pkgver}"
  python waf configure --prefix=/usr \
    --libdir=/usr/lib \
    --docs \
    --test
  python waf build $MAKEFLAGS
}

check() {
  cd "${pkgname}-${pkgver}"
  python waf test
}

package() {
  cd "${pkgname}-${pkgver}"
  python waf install --destdir="${pkgdir}"
  # license
  install -vDm 644 COPYING \
    -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  # docs
  install -vDm 644 {NEWS,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim:set ts=2 sw=2 et:
