# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kldap
pkgver=20.12.1
pkgrel=1
pkgdesc="LDAP access API for KDE"
arch=(x86_64 powerpc64le)
url="https://kontact.kde.org"
license=(LGPL)
depends=(kcompletion kwallet)
optdepends=('kio: LDAP kioslave')
makedepends=(extra-cmake-modules kio kdoctools)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('63923db7e4a8985a3f2c6020e232ef62f20e89ccbf210966402db820d384840b'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
