# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: BluePeril <blueperil (at) blueperil _dot_ de>

pkgname=kbackup
pkgver=20.04.1
pkgrel=1
pkgdesc="A program that lets you back up any directories or files"
arch=(x86_64 powerpc64le)
url="https://www.kde.org/applications/utilities/kbackup/"
license=(GPL)
groups=(kde-applications kde-utilities)
depends=(kio hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('8c8e67c17efda683d67416b82e5dbcdb6706f261cfc10c8d8b6c13547781e477'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>


prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
