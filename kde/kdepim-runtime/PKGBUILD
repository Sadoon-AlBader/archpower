# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdepim-runtime
pkgver=19.04.3
pkgrel=2
pkgdesc='Extends the functionality of kdepim'
arch=(x86_64 powerpc64le)
url='https://kontact.kde.org'
license=(GPL LGPL FDL)
depends=(libkolabxml kdav akonadi-calendar knotifyconfig kalarmcal kmbox pimcommon akonadi-notes kdelibs4support akonadi)
makedepends=(extra-cmake-modules kdoctools boost kdesignerplugin qca qt5-networkauth)
optdepends=('qca: EWS resource' 'qt5-networkauth: EWS resource')
conflicts=(kio-pim)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
        kdebug-409887.patch::"https://cgit.kde.org/kdepim-runtime.git/patch/?id=d148d621"
        kdepim-runtime-no-webengine.patch)
sha256sums=('720945f803dc9027211eb46f281ef52b35da3fe4d990c1aeed6d57a89a45215a'
            'SKIP'
            '368fbeb3acacbb69b08ff0a93a1d431346700c65cedea93233000f091ce254e8'
            'd5ed7237861eb52f77662da113827eefb3edffdaffd0d38a1d381c003e32dde5')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kdepim-runtime-no-webengine.patch # inspired by parabola
  patch -p1 -i ../kdebug-409887.patch # Fix new mail notifier configuration dialog
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
