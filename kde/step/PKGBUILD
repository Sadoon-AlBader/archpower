# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=step
pkgver=19.12.2
pkgrel=1
pkgdesc="Interactive Physical Simulator"
url="https://kde.org/applications/education/step/"
arch=(x86_64 powerpc64le)
license=(GPL LGPL FDL)
groups=(kde-applications kdeedu)
depends=(libqalculate gsl knewstuff kplotting hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools eigen qt5-tools)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('6236521ac06f1589888ede136539b11aa56c6732c9b73792898a8adfae5a25b4'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
