# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=akonadi
pkgname=(akonadi libakonadi)
pkgver=19.04.0
pkgrel=2
pkgdesc="PIM layer, which provides an asynchronous API to access all kind of PIM data"
arch=(x86_64 powerpc64le)
url='https://kontact.kde.org'
license=(LGPL)
makedepends=(extra-cmake-modules postgresql qt5-tools boost kdesignerplugin kio kitemmodels)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('c0a2c4259b51293d21487cfe2295df6e8dcfb7ab50eb1a698f531dc7d3253e9a'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package_libakonadi() {
  pkgdesc='Libraries used by applications based on Akonadi'
  depends=(kio kitemmodels hicolor-icon-theme)

  cd build
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/usr/bin # Provided by akonadi
}

package_akonadi() {
  depends=(libakonadi mariadb)
  optdepends=('postgresql: PostgreSQL backend')

  cd build
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/{etc,usr/{include,lib,share}} # Provided by libakonadi
}
