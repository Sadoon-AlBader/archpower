# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=breeze-gtk
pkgver=5.16.4
pkgrel=1
pkgdesc="Breeze widget theme for GTK 2 and 3"
arch=(x86_64 powerpc64le)
url='https://quickgit.kde.org/?p=breeze-gtk.git'
license=(LGPL)
depends=(qt5-base)
makedepends=(extra-cmake-modules sassc python-cairo breeze)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('8c954a8754bc75b2d8edff70a7b322c559da6e23dc75e1ed616ac926fbe186eb'
            'SKIP')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare(){
  mkdir -p build
}

build(){
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
