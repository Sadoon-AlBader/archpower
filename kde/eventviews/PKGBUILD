# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=eventviews
pkgver=19.04.3
pkgrel=1
pkgdesc="Library for creating events"
arch=(x86_64 powerpc64le)
url="https://kontact.kde.org"
license=(LGPL)
depends=(calendarsupport kdiagram)
makedepends=(extra-cmake-modules boost qt5-tools)
conflicts=('libkdepim<16')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('3f479f695756cea5d10ea2ab028dd6a954e677223a5c1ff9f7be208742952f22'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
