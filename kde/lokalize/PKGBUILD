# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=lokalize
pkgver=19.04.3
pkgrel=1
pkgdesc='Computer-Aided Translation System'
url='https://kde.org/applications/development/lokalize/'
arch=(x86_64 powerpc64le)
license=(GPL LGPL FDL)
groups=(kde-applications kdesdk)
depends=(kross hunspell hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('kross-interpreters: for msgmerge')
conflicts=(kdesdk-lokalize)
replaces=(kdesdk-lokalize)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('ec8c552a176d131140273f975958e29f3a476c840cab0c470db072cc850e85c8'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
