# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwordquiz
pkgver=20.08.3
pkgrel=1
pkgdesc="Flash Card Trainer"
url="https://kde.org/applications/education/kwordquiz/"
arch=(x86_64 powerpc64le)
license=(GPL LGPL FDL)
groups=(kde-applications kde-education)
depends=(knotifyconfig libkeduvocdocument knewstuff phonon-qt5)
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('0bdb69236bf694add82bb1f714735b8de54678451a4bdc1b9e8782e6fb9838ce'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
