# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=akonadi-calendar-tools
pkgver=19.04.3
pkgrel=1
pkgdesc="CLI tools to manage akonadi calendars"
arch=(x86_64 powerpc64le)
url="https://kontact.kde.org"
license=(GPL2)
depends=(calendarsupport)
makedepends=(extra-cmake-modules kdoctools boost)
conflicts=(konsolekalendar)
replaces=(konsolekalendar)
groups=(kde-applications kdepim)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('b7bf7c6a8df76f66d13209b3a2d1c47d703f0a3fff7936d2c880c11c2eb33d39'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
