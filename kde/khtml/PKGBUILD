# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=khtml
pkgver=5.79.0
pkgrel=1
pkgdesc='KHTML APIs'
arch=(x86_64 powerpc64le)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(giflib kparts kjs phonon-qt5)
makedepends=(extra-cmake-modules gperf)
groups=(kf5-aids)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/portingAids/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('1ece8dbd1bae528e07a335ef53278b8fda3e79a6c52ce0163d0de439d69d26a4'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
