# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=audiocd-kio
pkgver=20.04.1
pkgrel=1
pkgdesc="Kioslave for accessing audio CDs"
url='https://www.kde.org/applications/multimedia/'
arch=(x86_64 powerpc64le)
license=(GPL LGPL FDL)
depends=(kcmutils libkcddb libkcompactdisc lame)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('opus-tools: encoding to opus format')
groups=(kde-applications kde-multimedia)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('d3d7392c76ff285ef6d9195da6ea0d7c7dc1e5113afebd64111f62af56ef9662'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
