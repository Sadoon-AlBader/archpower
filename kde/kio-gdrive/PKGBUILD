# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kio-gdrive
pkgver=20.08.3
pkgrel=1
arch=(x86_64 powerpc64le)
pkgdesc="KIO Slave to access Google Drive"
url="https://community.kde.org/KIO_GDrive"
license=(GPL)
depends=(libkgapi kaccounts-providers)
makedepends=(extra-cmake-modules kdoctools intltool)
groups=(kde-applications kde-network)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('dea97644341debb20838811352a5a6f629c2949398a4c0a7dea83667f5b6e05e'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package(){
  DESTDIR="$pkgdir" cmake --install build
}
