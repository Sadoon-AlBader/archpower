# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kfloppy
pkgver=19.04.3
pkgrel=1
pkgdesc='Floppy Formatter'
url='https://kde.org/applications/utilities/kfloppy/'
arch=(x86_64 powerpc64le)
license=(GPL LGPL FDL)
depends=(kxmlgui hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
conflicts=(kdeutils-kfloppy)
replaces=(kdeutils-kfloppy)
groups=(kde-applications kdeutils)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('7f3a206b6f4cd03771d8fba94423bf749a2b8e6bb4dfd01f30502d1ab08f1d8e'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
