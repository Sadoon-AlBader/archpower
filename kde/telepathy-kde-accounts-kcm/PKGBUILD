# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>

pkgname=telepathy-kde-accounts-kcm
_pkgname=ktp-accounts-kcm
pkgver=19.08.0
pkgrel=1
pkgdesc='KCM Module for configuring Telepathy Instant Messaging Accounts'
arch=(x86_64 powerpc64le)
url='https://community.kde.org/Real-Time_Communication_and_Collaboration'
license=(GPL)
groups=(kde-applications kdenetwork telepathy-kde)
depends=(telepathy-qt kaccounts-providers)
makedepends=(extra-cmake-modules intltool)
optdepends=('telepathy-gabble: XMPP/Jabber accounts support' 'telepathy-haze: account types supported by Pidgin/libpurple'
            'telepathy-morse: Telegram accounts support' 'telepathy-salut: link-local XMPP account support')
source=("https://download.kde.org/stable/applications/$pkgver/src/$_pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('f849b7a9b2cee5b328e726dd254b1e606b740e5b75cdc59a1ea18601e131c0b5'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$_pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
