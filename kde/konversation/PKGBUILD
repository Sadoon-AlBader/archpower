# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: James Rayner <iphitus@gmail.com>
# Contributor: Sergio Jovani Guzman <moret@paretux.org>

pkgname=konversation
pkgver=1.7.7
pkgrel=1
pkgdesc="A user-friendly and fully-featured IRC client"
arch=(x86_64 powerpc64le)
url="https://konversation.kde.org"
depends=(knotifyconfig kemoticons kparts kidletime phonon-qt5 qca-qt5 hicolor-icon-theme perl)
optdepends=('python: python scripting support' 'keditbookmarks: for managing bookmarks')
makedepends=(extra-cmake-modules kdoctools)
license=(GPL2 FDL)
source=("https://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('47ec161c1385981a8b99d28c419e88b24823dc01b68fb1623894818974c218a7'
            'SKIP')
validpgpkeys=(DA6B31C4B98A877DBB2EF9A292220328C632316E  # Eike Hein <hein@kde.org>
              0A48BC961075B4BA8523E3790A345FB086E797D9) # Friedrich W. H. Kossebau <kossebau@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
