# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=konqueror
pkgver=19.04.0
pkgrel=1
pkgdesc="KDE File Manager & Web Browser"
arch=(x86_64 powerpc64le)
url="https://konqueror.org/"
license=(LGPL)
depends=(dolphin keditbookmarks kdelibs4support khtml) # qt5-webengines)
makedepends=(extra-cmake-modules kdoctools tidy kdesignerplugin kdesu)
optdepends=('tidy: tidy HTML plugin' 'kdesu: shell command plugin')
groups=(kde-applications kdebase)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
        "konqueror-optional-qtwebengine.patch")
sha256sums=('8da6fdd7de037bed79b6341e670a7491d371d084c59fdbefb4ffae0d2da82e4c'
            'SKIP'
            '0f9672efdd54abd3c2c3516a9bae103b2ade3a2cbdac2b566d179f411ff17b5e')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd ${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/konqueror-optional-qtwebengine.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
