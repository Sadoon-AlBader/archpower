# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=parley
pkgver=19.04.0
pkgrel=1
pkgdesc="Vocabulary Trainer"
url="https://kde.org/applications/education/parley/"
arch=(x86_64 powerpc64le)
license=(GPL LGPL FDL)
groups=(kde-applications kdeedu)
depends=(kross knewstuff libkeduvocdocument qt5-webkit kcmutils)
makedepends=(extra-cmake-modules kdoctools)
replaces=(kdeedu-parley)
conflicts=(kdeedu-parley)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('243f7f1a89bb603c059b0610a9bb7f51627540d439b4026d736062b693879ed2'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
