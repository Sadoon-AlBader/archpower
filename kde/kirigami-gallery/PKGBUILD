# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kirigami-gallery
pkgver=19.08.0
pkgrel=1
pkgdesc="Gallery application built using Kirigami"
url="https://www.kde.org/products/kirigami/"
arch=(x86_64 powerpc64le)
license=(GPL LGPL FDL)
groups=(kde-applications kdesdk)
depends=(kirigami2 qt5-svg)
makedepends=(extra-cmake-modules qt5-tools)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('e32f596c188911336060838c071f7856aba02438b335f107bf057644948cf9d6'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
