# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kleopatra
pkgver=19.12.1
pkgrel=1
pkgdesc='Certificate Manager and Unified Crypto GUI'
arch=(x86_64 powerpc64le)
url='https://www.kde.org/applications/utilities/kleopatra/'
license=(GPL LGPL FDL)
depends=(hicolor-icon-theme libkleo kcmutils kmime)
makedepends=(extra-cmake-modules kdoctools boost)
groups=(kde-applications kdepim)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('94ee94031696dd5d79d7a0ca00a2e51b4569466689e8a76c129deae645af08f4'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
