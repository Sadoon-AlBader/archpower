# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kcompletion
pkgver=5.60.0
pkgrel=1
pkgdesc='Text completion helpers and widgets'
arch=(x86_64 powerpc64le)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kwidgetsaddons kconfig)
makedepends=(extra-cmake-modules qt5-tools clang python-pyqt5 doxygen sip)
optdepends=('python-pyqt5: for the Python bindings')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('d5afb075661dfe2da5e300bd73dee89f92736f39859be711f186310bed245f7c'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
