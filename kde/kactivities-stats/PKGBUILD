# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kactivities-stats
pkgver=5.67.0
pkgrel=1
arch=(x86_64 powerpc64le)
pkgdesc="A library for accessing the usage data collected by the activities system"
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kactivities)
makedepends=(extra-cmake-modules boost doxygen qt5-tools qt5-doc)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('a5cf10e8731cff0d136a261e43aff5f3331cd9d399061058d72b60fce94db005'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
