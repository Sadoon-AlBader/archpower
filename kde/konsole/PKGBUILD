# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=konsole
pkgver=20.04.1
pkgrel=3
arch=(x86_64 powerpc64le)
url='https://kde.org/applications/system/konsole/'
pkgdesc="KDE's terminal emulator"
license=(GPL LGPL FDL)
groups=(kde-applications kde-utilities)
depends=(knotifyconfig kpty kparts kinit knewstuff)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('keditbookmarks: to manage bookmarks')
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
         kdebug-419526.patch::"https://invent.kde.org/utilities/konsole/commit/e7e8e9d1.patch"
         kdebug-415762.patch::"https://invent.kde.org/utilities/konsole/commit/d40e3c72.patch")
sha256sums=('b5ada61759b4731cccb3814c4cc9a495c309ad93c9943e4b7e802b3fe53a7932'
            'SKIP'
            '92f5fd30536017a0711869a04b5be298326e45e7b19afb2012f319761d0c322f'
            '10b341a351f1638c0d62ebeff87d09c7d1d10aba167ac4be8971b83f4d933e1c')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../kdebug-419526.patch # Fix crash on right-click with Qt 5.15
  patch -d $pkgname-$pkgver -p1 -i ../kdebug-415762.patch # Fix konsolepart crash after showing context menu
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
