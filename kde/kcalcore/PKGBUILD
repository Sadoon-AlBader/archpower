# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kcalcore
pkgver=19.08.0
pkgrel=2
pkgdesc="The KDE calendar access library"
arch=(x86_64 powerpc64le)
url="https://kontact.kde.org"
license=(LGPL)
depends=(libical qt5-base)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
        kcalcore-version-header.patch::"https://cgit.kde.org/kcalcore.git/patch/?id=28eab632")
sha256sums=('7c7bbca70ada8c8317d6d3d91e0357b2b5886328189423e0b7fac1d326f8ed85'
            'SKIP'
            'd02a2f883c81427c15611f528dd18b13fe5090ddfbb5445b6c3d1730bf8c2557')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kcalcore-version-header.patch # Restore old version header for backwards compatibility
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
