# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Florian Pritz <flo@xinu.at>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>
# Contributor: Dan McGee <dan@archlinux.org>
# Contributor: Jaroslaw Rosiek <philosoph@interia.pl>
# Contributor: Darwin Bautista <djclue917@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=asciidoc
pkgver=9.1.0
pkgrel=1
pkgdesc='Text document format for short documents, articles, books and UNIX man pages'
arch=('any')
url='https://asciidoc.org/'
license=('GPL')
depends=('python' 'libxslt' 'docbook-xsl')
checkdepends=('dblatex' 'graphviz' 'lilypond' 'source-highlight')
optdepends=('graphviz: graphviz-filter'
            'lilypond: music-filter'
            'imagemagick: music-filter'
            'source-highlight: source-highlight-filter'
            'dblatex: pdf generation'
            'fop: alternative pdf generation'
            'w3m: text generation'
            'lynx: alternative text generation')
source=("https://github.com/asciidoc/asciidoc-py3/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('5056c20157349f8dc74f005b6e88ccbf1078c4e26068876f13ca3d1d7d045fe7')
b2sums=('379c4d4ee1b10e09d39192ab570be10f8b51f31c355866c494c3ab3d79961b81d0fb1c5c0a63e89f671644a55ff25627250597b0a05dfe78e55760480fe60765')

prepare() {
  cd ${pkgname}-py-${pkgver}

  autoconf
}

build() {
  cd ${pkgname}-py-${pkgver}

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc
  make manpages
}

check() {
  cd ${pkgname}-py-${pkgver}

  make test
}

package() {
  cd ${pkgname}-py-${pkgver}

  make install DESTDIR="${pkgdir}"
  make docs DESTDIR="${pkgdir}"

  install -Dm644 asciidocapi.py \
    "${pkgdir}/$(python -c 'import sysconfig; print(sysconfig.get_paths()["platlib"])')"/asciidocapi.py
}
