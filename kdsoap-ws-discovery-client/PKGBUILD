# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kdsoap-ws-discovery-client
pkgver=git20200316
pkgrel=2
_commit=14287e92e80a77aa4c0adee2871e6b87c9c3055e
pkgdesc='WS-Discovery client library based on KDSoap'
license=(GPL3 custom)
arch=(x86_64 powerpc64le)
url='https://github.com/KDAB/KDSoap'
depends=(kdsoap)
makedepends=(extra-cmake-modules doxygen qt5-tools qt5-doc)
source=("https://gitlab.com/caspermeijn/kdsoap-ws-discovery-client/-/archive/$_commit/$pkgname-$_commit.tar.gz"
         kdsoap-dependency.patch::"https://gitlab.com/caspermeijn/kdsoap-ws-discovery-client/-/merge_requests/11.patch")
sha256sums=('17e092729597aecb96bcf46459b4eb14d28cf335f4b4c225971b5be55b9572a7'
            '550e045bc32e077c4aa9e348aba64ff0fde5767ce1ddfdd8d840fbae1e904137')

prepare() {
  mkdir -p build

  cd $pkgname-$_commit
  patch -p1 -i ../kdsoap-dependency.patch # Add missing kdsoap dependency to cmake
}

build() {
  cd build
  cmake ../$pkgname-$_commit \
    -DBUILD_QCH=ON
  make 
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir"/$pkgname-$_commit/LICENSES/* -t "$pkgdir"/usr/share/licenses/$pkgname
}
