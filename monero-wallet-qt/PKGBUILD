# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: redfish <redfish at galactica dot pw>

pkgname='monero-wallet-qt'
_monerover=0.15.0.1
pkgver=0.15.0.2
pkgrel=1
arch=(x86_64 powerpc64le)
url="https://getmonero.org/"
license=('custom:Cryptonote')

provides=('monero-wallet-qt')

depends=("libmonero-wallet>=$_monerover"
	'openssl' 'boost-libs>=1.45'  'libunwind' 'readline' 'unbound' 'hidapi'
	'qt5-base' 'qt5-declarative' 'qt5-graphicaleffects'
	'qt5-location' 'qt5-quickcontrols' 'qt5-quickcontrols2' 'qt5-tools' 'qt5-webchannel'
	'qt5-webengine' 'qt5-x11extras' 'qt5-xmlpatterns' 'qt5-svg' 'randomx')

makedepends=('git' 'cmake' 'boost' 'xorgproto')

pkgdesc="Official QT GUI wallet for Monero, a private, secure, untraceable peer-to-peer currency"

source=("https://github.com/monero-project/monero-gui/archive/v${pkgver}.tar.gz")


prepare() {
    cd "$srcdir/monero-gui-$pkgver"

    echo "var GUI_VERSION = \"$pkgver\"" > version.js
    echo "var GUI_MONERO_VERSION = \"$_monerover\"" >> version.js
}

build() {
    cd "$srcdir/monero-gui-$pkgver"

    make -C src/zxcvbn-c

    qmake
    make
}

package() {
    install -D -m755 "$srcdir/monero-gui-$pkgver/release/bin/monero-wallet-gui" "$pkgdir/usr/bin/monero-wallet-qt"
    install -D -m644 "$srcdir/monero-gui-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

sha256sums=('03571b7a41a7ae26439063e7da134a3ad99fbf6d37a54c39a9ca7db5b893547e')
