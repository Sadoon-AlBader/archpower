# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer:
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>
# Contributor: roberto <roberto@archlinux.org>

pkgname=transcode
_sripver=0.3-4
pkgver=1.1.7
pkgrel=33
pkgdesc="A video/DVD ripper and encoder for the terminal/console"
arch=(powerpc64le)
url="http://www.transcoding.org/"
license=(GPL)
depends=(gawk imagemagick lzo libdvdread ffmpeg mjpegtools libmpeg2 libxaw a52dec)
makedepends=(x264 libquicktime)
options=('!makeflags')
optdepends=('libquicktime: Quicktime format support')
#source=(https://bitbucket.org/france/transcode-tcforge/downloads/$pkgname-$pkgver.tar.bz2
source=(https://sources.archlinux.org/other/community/$pkgname/$pkgname-$pkgver.tar.bz2
        transcode-imagemagick7.patch
        transcode-ffmpeg3.patch
        transcode-ffmpeg4.patch
        transcode-swresample.patch)
sha256sums=('1e4e72d8e0dd62a80b8dd90699f5ca64c9b0cb37a5c9325c184166a9654f0a92'
            '4ede15540ea6932954ac332c12dde130bf48e7e4773d1e04d3c3f23038c6ac51'
            '728ec3101039d98116275e36ea5cf0434a23a0bb82aca830ea9612fb9d16b062'
            '2cc680666f705685a9fdc54b5a377e7728765bde6ed83c43b835634e27767721'
            '6b4e91085742535174015be5da103fe3337b4cb14537634ae378c9050088280e')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../transcode-ffmpeg3.patch # LFS patch
  patch -p1 -i ../transcode-ffmpeg4.patch # Gentoo patch
  patch -p1 -i ../transcode-imagemagick7.patch # Gentoo patch
  sed -e 's|freetype/ftglyph.h|freetype2/freetype/ftglyph.h|' -i filter/subtitler/load_font.c
  patch -p1 -i ../transcode-swresample.patch # port away from avresample
  autoreconf -vi
}


build() {
  cd $pkgname-$pkgver
  CFLAGS+=" -fcommon" \
  ./configure --prefix=/usr --build=${CHOST} \
    --disable-sse --disable-sse2 --enable-altivec --disable-mmx \
    --enable-lame --enable-ogg --enable-vorbis --enable-theora \
    --enable-libdv --enable-libxml2 --enable-v4l \
    --enable-imagemagick --enable-libjpeg --enable-lzo --enable-mjpegtools \
    --enable-sdl --enable-freetype2 --enable-a52 --enable-libpostproc \
    --enable-xvid --enable-x264 --enable-alsa --enable-libmpeg2 \
    --enable-libmpeg2convert --enable-libquicktime

  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
