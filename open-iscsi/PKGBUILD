# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Stefan Kirrmann <stefan.kirrmann at gmail dot com>

pkgname=open-iscsi
pkgver=2.1.1
pkgrel=1
pkgdesc='iSCSI userland tools'
arch=(x86_64 powerpc64le)
url='https://www.open-iscsi.com/'
license=('GPL')
depends=('systemd-libs' 'libutil-linux' 'kmod' 'openssl' 'open-isns')
install=$pkgname.install
backup=('etc/iscsi/iscsid.conf'
	'etc/iscsi/initiatorname.iscsi')
options=('docs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/open-iscsi/open-iscsi/archive/$pkgver.tar.gz")
sha256sums=('dfc1ea37f230f9d116f5b39c795b35be43002d65c81330ccd3878786532b811b')

build() {
  cd "$srcdir"/${pkgname}-${pkgver}

  make user LIB_DIR=/usr/lib
}

package() {
  cd "$srcdir"/${pkgname}-${pkgver}

  make DESTDIR="$pkgdir" LIB_DIR=/usr/lib install install_systemd

  install -D -m0644 "$srcdir"/${pkgname}-${pkgver}/etc/iscsid.conf "$pkgdir"/etc/iscsi
  echo -n > "$pkgdir"/etc/iscsi/initiatorname.iscsi

  # copy docs
  mkdir -p "$pkgdir"/usr/share/doc/${pkgname}
  install -m0644 Changelog "$pkgdir"/usr/share/doc/${pkgname}/
  install -m0644 README "$pkgdir"/usr/share/doc/${pkgname}/

  mv "$pkgdir"/sbin "$pkgdir"/usr/bin
}
