# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=faad2
pkgver=2.9.1
pkgrel=1
pkgdesc="ISO AAC audio decoder"
arch=(x86_64 powerpc64le)
url="http://faac.sourceforge.net/"
license=('GPL')
depends=('glibc')
options=('!makeflags')
source=($pkgname-$pkgver.tar.gz::https://github.com/knik0/faad2/archive/${pkgver//./_}.tar.gz)
sha256sums=('7fa33cff76abdda5a220ca5de0b2e05a77354f3b97f735193c2940224898aa9a')

build() {
  cd ${pkgname}-${pkgver//./_}
  ./bootstrap
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver//./_}
  make DESTDIR="${pkgdir}" install
}
