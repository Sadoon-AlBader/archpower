# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Anatol Pomozov
# Contributor: Frederic Bezies <fredbezies at gmail dot com>

pkgname=dtc
pkgver=1.5.1
pkgrel=1
pkgdesc='Device Tree Compiler'
url='http://www.devicetree.org/'
arch=(x86_64 powerpc64le)
license=(GPL2)
makedepends=(swig python)
checkdepends=(valgrind)
source=(https://www.kernel.org/pub/software/utils/dtc/dtc-$pkgver.tar.xz)
sha256sums=('660b74039690fc37013660544d09191834efb58503c73c555c5513ba75ab031f')

prepare() {
  cd dtc-$pkgver
  sed -i 's/-Werror//' Makefile
}

build() {
  cd dtc-$pkgver
  make
}

check() {
  cd dtc-$pkgver
  make check
}

package() {
  cd dtc-$pkgver
  DESTDIR="$pkgdir" make SETUP_PREFIX="$pkgdir/usr" PREFIX="$pkgdir/usr" install
}

