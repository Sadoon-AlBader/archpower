# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Mirco Tischler <mt-ml at gmx dot de>

pkgname=ostree
pkgver=2019.3
pkgrel=2
pkgdesc="Operating system and container binary deployment and upgrades"
url="https://ostree.readthedocs.org/en/latest/"
arch=(x86_64 powerpc64le)
license=(GPL)
depends=(glib2 xz zlib libsoup gpgme libarchive fuse2 util-linux mkinitcpio openssl avahi)
makedepends=(e2fsprogs gobject-introspection gtk-doc libxslt systemd git python)
checkdepends=(parallel gjs cpio elfutils)
_commit=b15ed42111c25bd3fcb0d9bdc425f79f771fcece  # tags/v2019.3^0
source=("git+https://github.com/ostreedev/ostree#commit=$_commit"
        "git+https://github.com/mendsley/bsdiff"
        "git+https://gitlab.gnome.org/GNOME/libglnx.git"
        1908.patch)
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'f2fadec4f29418a61164b5b50ee9829124e2b6427d820dc064a3597aace55ad9')

prepare() {
  cd $pkgname

  git submodule init
  git config --local submodule.bsdiff.url "$srcdir/bsdiff"
  git config --local submodule.libglnx.url "$srcdir/libglnx"
  git submodule update

  patch -p1 -i "$srcdir/1908.patch"

  NOCONFIGURE=1 ./autogen.sh
}

pkgver() {
  cd $pkgname
  git describe | sed 's/^v//;s/-/+/g'
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --with-mkinitcpio \
    --with-openssl \
    --with-builtin-grub2-mkconfig \
    --enable-experimental-api \
    --disable-static \
    --enable-gtk-doc

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd $pkgname
  make check || :
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
