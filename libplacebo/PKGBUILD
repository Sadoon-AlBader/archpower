# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=libplacebo
pkgver=1.29.1
pkgrel=3
pkgdesc='Reusable library for GPU-accelerated video/image rendering primitives'
url='https://github.com/haasn/libplacebo'
arch=( x86_64 powerpc64le powerpc )
license=('LGPL2.1')
depends=('vulkan-icd-loader' 'glslang' 'lcms2' 'shaderc' 'libshaderc_shared.so')
makedepends=('meson' 'ninja' 'vulkan-headers')
provides=('libplacebo.so')
source=(https://code.videolan.org/videolan/libplacebo/-/archive/v${pkgver}/libplacebo-v${pkgver}.tar.gz
        ${pkgname}-${pkgver}-vulkan.patch::https://github.com/haasn/libplacebo/commit/45e19e7bbbbfceb197d8826c775e16ef536a4565.patch)
sha512sums=('9f09560832a88e670d6c31e9f5b9cbc03de437bfb19ceb0985140307b5cdf20e2050a1dab5d07f18258dfa3e5429a51f1d3d54532099be4bfc6a0dafe3ffd516'
            'ae7ce0e9681f78cccfc3c93bb06a109897c3df3f1d28fe363d7dac2d0e17884121100e2615d8d7a2a400a9f4408b0f3b56c3803cb4ab98c952f8739a8c3fd680')

prepare() {
  cd ${pkgbase}-v${pkgver}
  patch -Np1 -i ${srcdir}/${pkgname}-${pkgver}-vulkan.patch
}

build() {
  cd ${pkgbase}-v${pkgver}
  CXXFLAGS+=" -I/usr/include/glslang"
  arch-meson build \
    -D tests=true \
    -D vulkan=enabled \
    -D glslang=enabled \
    -D shaderc=enabled \
    -D lcms=enabled
  ninja -C build
}

check() {
  cd ${pkgbase}-v${pkgver}
  ninja -C build test
}

package() {
  cd ${pkgbase}-v${pkgver}
  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
