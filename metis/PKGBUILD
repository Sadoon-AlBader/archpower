# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer:
# Contributor: jedbrown
# Contributor: eleftg
# Contributor: mickele
# Contributor: gborzi
# Contributor: abenson

pkgname=metis
pkgver=5.1.0.p8
_pkgver=5.1.0-p8
pkgrel=1
arch=(x86_64 powerpc64le)
pkgdesc="A set of serial programs for partitioning graphs, partitioning finite element meshes, and producing fill reducing orderings for sparse matrices"
url="http://glaros.dtc.umn.edu/gkhome/metis/metis/overview"
license=(custom)
depends=(glibc)
makedepends=(cmake)
options=(docs)
source=($pkgname-$pkgver.tar.bz2::"https://bitbucket.org/petsc/pkg-metis/get/v$_pkgver.tar.bz2")
sha256sums=('d098b9909477aeefcc06604faa2953d6248833dcec6ede8971ea991e443fed01')

prepare() {
  mv petsc-pkg-metis-* $pkgname-$_pkgver
}

build() {
  cd $pkgname-$_pkgver
  make config shared=1 prefix=/usr
  make
}

package() {
  cd $pkgname-$_pkgver

  make install DESTDIR="$pkgdir"
  install -m644 -D LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
