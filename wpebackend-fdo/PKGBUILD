# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=wpebackend-fdo
pkgver=1.4.0
pkgrel=2
pkgdesc="freedesktop.org backend for WPE WebKit"
url="https://wpewebkit.org/"
arch=(x86_64 powerpc64le)
license=(custom)
depends=(libwpe glib2 libegl wayland)
makedepends=(cmake mesa)
source=($url/releases/$pkgname-$pkgver.tar.xz{,.asc}
        glvnd.diff)
sha256sums=('a919ca4a5bc445f9419a5b7f3781cfc98e5abcf5d7259eb1869f5ab20fd18baf'
            'SKIP'
            'ac67e680efb5040d10c6f1cf475259dd3efaf5bc1f97c316930e28bab07f44ce')
validpgpkeys=('5AA3BC334FD7E3369E7C77B291C559DBE4C9123B') # Adrián Pérez de Castro <aperez@igalia.com>

prepare() {
  cd $pkgname-$pkgver

  # Fix build with headers from libglvnd
  patch -Np1 -i ../glvnd.diff
}

build() {
  cmake -H$pkgname-$pkgver -Bbuild \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --build build --target install
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" $pkgname-$pkgver/COPYING
}

# vim:set sw=2 et:
