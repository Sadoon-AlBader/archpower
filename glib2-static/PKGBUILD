# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: nightuser <nightuser.android at gmail dot com>

pkgname=glib2-static
pkgver=2.64.5
pkgrel=1
pkgdesc="Low level core library: Static library"
url="https://wiki.gnome.org/Projects/GLib"
license=(LGPL2.1)
arch=(x86_64 powerpc64le)
depends=(pcre libffi)
makedepends=(zlib libffi shared-mime-info python libelf git util-linux meson dbus)
checkdepends=(desktop-file-utils dbus)
options=('!docs' '!libtool' '!emptydirs' '!strip' 'staticlibs')
source=(https://gitlab.gnome.org/GNOME/glib/-/archive/$pkgver/glib-$pkgver.tar.gz)
sha256sums=('752cef4b5b1ef7366122e4801f904d08ca9f47ecf3212f50f957970f8c777519')

build() {
  arch-meson "glib-$pkgver" _build \
    --default-library static \
    --buildtype release \
    -Dselinux=disabled \
    -Dman=false \
    -Dgtk_doc=false \
    -Dinternal_pcre=false
  ninja -C _build ${MAKEFLAGS}
}

check() {
  meson test -C _build --no-suite flaky --timeout-multiplier 2 --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C _build

  # Only install static library
  rm -rf "$pkgdir"/usr/{bin,include,share,lib/glib-2.0,lib/pkgconfig}
}
