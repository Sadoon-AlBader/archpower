# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgbase=python-text-unidecode
pkgname=(python-text-unidecode python2-text-unidecode)
pkgver=1.3
pkgrel=1
pkgdesc="The most basic Text::Unidecode port"
url="https://github.com/kmike/text-unidecode"
license=('PerlArtistic')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("https://pypi.io/packages/source/t/text-unidecode/text-unidecode-$pkgver.tar.gz")
sha512sums=('aeaf0e79bd0545ca7a4fe700d5a7dac1d5900841fcfe8a5a568c84ba7d3bee5f75eaac0531f4a01d689a550ac5bc7e1acaeb8132293f220db8387fae2eb6a150')

prepare() {
  cp -a text-unidecode-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/text-unidecode-$pkgver
  python setup.py build

  cd "$srcdir"/text-unidecode-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/text-unidecode-$pkgver
  python setup.py pytest

  cd "$srcdir"/text-unidecode-$pkgver-py2
  python2 setup.py pytest
}

package_python-text-unidecode() {
  depends=('python')

  cd text-unidecode-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
}

package_python2-text-unidecode() {
  depends=('python2')

  cd text-unidecode-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
