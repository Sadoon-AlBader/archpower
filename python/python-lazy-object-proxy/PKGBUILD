# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-lazy-object-proxy
pkgname=(python-lazy-object-proxy python2-lazy-object-proxy)
pkgver=1.4.3
pkgrel=2
pkgdesc='A fast and thorough lazy object proxy'
arch=(x86_64 powerpc64le)
url="https://github.com/ionelmc/python-lazy-object-proxy"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-benchmark' 'python-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/ionelmc/python-lazy-object-proxy/archive/v$pkgver.tar.gz")
sha256sums=('ce4c9872869331d509ff2b7be19fdddb1dc4b922d14aa62675770e3caebd92e2')

prepare() {
  cp -a python-lazy-object-proxy-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/python-lazy-object-proxy-$pkgver
  python setup.py build

  cd "$srcdir"/python-lazy-object-proxy-$pkgver-py2
  python2 setup.py build
}

check() {
  cd python-lazy-object-proxy-$pkgver
  python setup.py pytest --addopts --ignore=src
}

package_python-lazy-object-proxy() {
  depends=("python")

  cd python-lazy-object-proxy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-lazy-object-proxy() {
  depends=("python2")

  cd python-lazy-object-proxy-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
