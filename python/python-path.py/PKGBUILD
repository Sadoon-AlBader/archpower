# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-path.py
pkgname=('python-path.py' 'python2-path.py')
pkgver=12.0.1
pkgrel=1
pkgdesc='A module wrapper for os.path'
arch=('any')
license=('MIT')
url='https://github.com/jaraco/path.py'
makedepends=('python-setuptools-scm' 'python2-setuptools-scm' 'python-importlib-metadata'
             'python2-importlib-metadata' 'python2-backports.os')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-pytest-flake8' 'python2-pytest-flake8')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/jaraco/path.py/archive/$pkgver.tar.gz")
sha512sums=('5906b31db753374a75e8b1a8515ed3d6ab7b394906ce8d07aea13be77d5e6db164d79c4b1dc517c8c6692b4eef57505a5c739ba54e282dcbda01f37d21e04044')

prepare() {
  cp -a path.py-$pkgver{,-py2}

  # https://github.com/jaraco/path.py/issues/153
  sed -i 's/milliseconds=100/milliseconds=200/' path.py-$pkgver-py2/test_path.py

  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  cd "$srcdir"/path.py-$pkgver
  python setup.py build

  cd "$srcdir"/path.py-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/path.py-$pkgver
  python setup.py pytest

  cd "$srcdir"/path.py-$pkgver-py2
  python2 setup.py pytest
}

package_python-path.py() {
  depends=('python-importlib-metadata')

  cd path.py-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-path.py() {
  depends=('python2-importlib-metadata' 'python2-backports.os')

  cd path.py-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
