# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Anatol Pomozov
# Contributor: Tim Hütz <tim@huetz.biz>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: Sebastien Binet <binet@cern.ch>

_pkg=mpi4py
pkgbase=python-${_pkg}
pkgname=(python-${_pkg} python2-${_pkg})
pkgver=3.0.2
pkgrel=2
pkgdesc="Python bindings for the Message Passing Interface (MPI) standard"
arch=(x86_64 powerpc64le)
url="https://bitbucket.org/mpi4py/mpi4py"
license=(BSD)
makedepends=(cython cython2 python-setuptools python2-setuptools openmpi)
checkdepends=(inetutils)
source=("https://files.pythonhosted.org/packages/source/m/${_pkg}/${_pkg}-${pkgver}.tar.gz"
	"${pkgname}-${pkgver}-python-3.8-fix.patch::https://bitbucket.org/mpi4py/mpi4py/commits/36e77661b7a879f0ec1eb165c3b5cd0259eb8496/raw")
sha256sums=('f8d629d1e3e3b7b89cb99d0e3bc5505e76cc42089829807950d5c56606ed48e0'
	    '8f53f154b2fbce81b20991c7b77d9f3894ed2e4cd268613d251e6d9260c18817')

prepare() {
    cp -a ${_pkg}-${pkgver}{,-py2}

    cd ${_pkg}-${pkgver}
    patch -Np1 -i ${srcdir}/${pkgname}-${pkgver}-python-3.8-fix.patch

    # need to recythonize for python 3.8, so clean pregenerated files
    rm $(grep -rl '/\* Generated by Cython')
}

build() {
    cd ${_pkg}-${pkgver}
    python setup.py build

    cd ../${_pkg}-${pkgver}-py2
    python2 setup.py build
}

check() {
    # This is required starting with OpenMPI 3.0 when trying to run more
    # processes than the number of available cores
    export OMPI_MCA_rmaps_base_oversubscribe=yes

    cd ${_pkg}-${pkgver}
    python setup.py test

    cd ../${_pkg}-${pkgver}-py2
    python2 setup.py test
}

package_python-mpi4py() {
    depends=(python openmpi)

    cd ${_pkg}-${pkgver}
    python setup.py install --root="${pkgdir}" --skip-build --optimize=1
    install -Dm644 LICENSE.rst -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}

package_python2-mpi4py() {
    depends=(python2 openmpi)

    cd ${_pkg}-${pkgver}-py2
    python2 setup.py install --root="$pkgdir" --skip-build --optimize=1
    install -Dm644 LICENSE.rst -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
