# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com

pkgname=python-stevedore
pkgver=3.2.0
pkgrel=1
pkgdesc="Manage dynamic plugins for Python applications"
arch=('any')
url="https://github.com/openstack/stevedore"
license=('Apache')
depends=('python')
makedepends=('python-pbr')
options=('!emptydirs')
source=(https://pypi.org/packages/source/s/stevedore/stevedore-$pkgver.tar.gz)
sha256sums=('38791aa5bed922b0a844513c5f9ed37774b68edc609e5ab8ab8d8fe0ce4315e5')

prepare() {
  cd stevedore-$pkgver
  # Remove dependency on pbr; it's not a runtime dependency
  sed -i '/pbr/d' requirements.txt
}

package() {
  cd stevedore-$pkgver
  python3 setup.py install --root="$pkgdir" -O1
}

# vim:set ts=2 sw=2 et:
