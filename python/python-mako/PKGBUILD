# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-mako
pkgname=('python-mako' 'python2-mako')
pkgver=1.1.2
pkgrel=1
pkgdesc="A super-fast templating language that borrows the best ideas from the existing templating languages"
arch=('any')
url="https://www.makotemplates.org/"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-markupsafe' 'python2-markupsafe'
             'python-beaker' 'python2-beaker')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-mock' 'python2-mock'
              'python-babel' 'python2-babel' 'python-pygments' 'python2-pygments'
              'python-dogpile.cache' 'python2-dogpile.cache')
source=("https://pypi.io/packages/source/M/Mako/Mako-$pkgver.tar.gz"{,.asc})
sha512sums=('73b7a32ceb6e39dc2491734546c83a752bf0c8bc7546bb236801c02a5bccc5b59f2440333a328ff747eee936612dd12279f08d27d76f065fd589081d232692a8'
            'SKIP')
validpgpkeys=('83AF7ACE251C13E6BB7DEFBD330239C1C4DAFEE1')  # Michael Bayer

prepare() {
  cp -a Mako-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/Mako-$pkgver
  python setup.py build

  cd "$srcdir"/Mako-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/Mako-$pkgver
  python setup.py ptr

  cd "$srcdir"/Mako-$pkgver-py2
  python2 setup.py ptr
}

package_python-mako() {
  depends=('python-markupsafe' 'python-beaker' 'python-setuptools')

  cd Mako-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-mako() {
  depends=('python2-markupsafe' 'python2-beaker' 'python2-setuptools')

  cd Mako-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/mako-render{,2}
}
