# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-recommonmark
pkgname=('python-recommonmark' 'python2-recommonmark')
pkgver=0.5.0.167.f79c2bf
pkgrel=2
_gitcommit=f79c2bf6161c0011c10c4dd18bea2fde832f2da5
pkgdesc='Markdown parser for docutils'
url='https://github.com/rtfd/recommonmark'
arch=('any')
license=('MIT')
makedepends=('git'
             'python-setuptools' 'python-commonmark' 'python-docutils' 'python-sphinx'
             'python2-setuptools' 'python2-commonmark' 'python2-docutils' 'python2-sphinx')
checkdepends=('python-pytest' 'python2-pytest')
source=(${pkgbase}::"git+https://github.com/rtfd/recommonmark#commit=$_gitcommit")
sha512sums=('SKIP')

pkgver() {
  cd ${pkgbase}
  printf "%s.%s.%s" \
    "$(PYTHONPATH=. python -c 'print(__import__('"'recommonmark'"').__version__)')" \
    "$(git rev-list --count HEAD)" \
    "$(git rev-parse --short HEAD)"
}

prepare() {
  cp -a ${pkgbase}{,-py2}
  for tool in cm2{html,latex,man,pseudoxml,xetex,xml}; do
    sed -r "s|(${tool}) |\12 |g" -i ${pkgbase}-py2/setup.py
  done
}

build() {
  msg2 "Building python..."
  (cd ${pkgbase}
    python setup.py build
    #make -j1 -C docs text man SPHINXBUILD=sphinx-build
  )
  msg2 "Building python2..."
  (cd ${pkgbase}-py2
    python2 setup.py build
    #make -j1 -C docs text man SPHINXBUILD=sphinx-build2
  )
}

check() {
  msg2 "Checking python..."
  (cd ${pkgbase}
    py.test
  )
  msg2 "Checking python2..."
  (cd ${pkgbase}-py2
    py.test2
  )
}

package_python-recommonmark() {
  depends=('python-docutils' 'python-commonmark' 'python-setuptools' 'python-sphinx')

  cd ${pkgbase}
  python setup.py install --root="${pkgdir}" --skip-build -O1
  install -Dm 644 license.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

package_python2-recommonmark() {
  depends=('python2-docutils' 'python2-commonmark' 'python2-setuptools' 'python2-sphinx')

  cd ${pkgbase}-py2
  python2 setup.py install --root="${pkgdir}" --skip-build -O1
  install -Dm 644 license.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
