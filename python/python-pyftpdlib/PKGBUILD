# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgbase=python-pyftpdlib
pkgname=('python-pyftpdlib' 'python2-pyftpdlib')
pkgver=1.5.5
pkgrel=1
pkgdesc='Very fast asynchronous FTP server library'
arch=('any')
url='https://github.com/giampaolo/pyftpdlib/'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pysendfile' 'python2-pysendfile')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-mock' 'python2-mock'
              'python-pyopenssl' 'python2-pyopenssl' 'python-psutil' 'python2-psutil')
source=("https://pypi.io/packages/source/p/pyftpdlib/pyftpdlib-$pkgver.tar.gz")
sha512sums=('27427154f893812f689adf3a9ceb4ec2d8f0ea7a4bf7b08671dc146c95f09ddd596a27c85e009d8d6647277def48c0bf0a8b76fb3fba5818eda3dfdc6c10795e')

prepare() {
  cp -a pyftpdlib-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pyftpdlib-$pkgver
  python setup.py build

  cd "$srcdir"/pyftpdlib-$pkgver-py2
  python2 setup.py build
}

check() {
  # https://github.com/giampaolo/pyftpdlib/issues/500

  cd "$srcdir"/pyftpdlib-$pkgver
  python setup.py pytest || warning "Tests failed"

  cd "$srcdir"/pyftpdlib-$pkgver-py2
  python2 setup.py pytest || warning "Tests failed"
}

package_python-pyftpdlib() {
  depends=('python')
  optdepends=('python-pyopenssl: FTPS support'
              'python-psutil: to keep track of FTP server memory usage')

  cd pyftpdlib-$pkgver
  python setup.py install --root="$pkgdir" -O1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pyftpdlib() {
  depends=('python2')
  optdepends=('python2-pyopenssl: FTPS support'
              'python2-pysendfile: Speed up file transfers'
              'python2-psutil: to keep track of FTP server memory usage')

  cd pyftpdlib-$pkgver-py2
  python2 setup.py install --root="$pkgdir" -O1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/ftpbench{,2}
}
