# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgbase=python-stestr
pkgname=(python-stestr python2-stestr)
pkgver=2.5.1
pkgrel=2
pkgdesc="A test runner runner similar to testrepository"
url="https://stestr.readthedocs.io/en/latest/"
license=('Apache')
arch=('any')
makedepends=('python-future' 'python2-future' 'python-pbr' 'python2-pbr' 'python-cliff'
             'python2-cliff' 'python-subunit' 'python2-subunit' 'python-fixtures' 'python2-fixtures'
             'python-testtools' 'python2-testtools' 'python-yaml' 'python2-yaml' 'python-voluptuous'
             'python2-voluptuous')
checkdepends=('python-hacking' 'python2-hacking' 'python-sphinx' 'python2-sphinx' 'python-mock'
              'python2-mock' 'python-subunit2sql' 'python2-subunit2sql' 'python-coverage'
              'python2-coverage' 'python-ddt' 'python2-ddt')
source=("https://pypi.io/packages/source/s/stestr/stestr-$pkgver.tar.gz")
sha512sums=('f3f64448bd6782ab125e7d1094992bf1f9416cab8656c9f0bc800d00839b898000a04ffcf6010002f7c247ec06ca236389688ea67be6f3c1c979916455a03e28')

prepare() {
  cp -a stestr-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/stestr-$pkgver
  python setup.py build

  cd "$srcdir"/stestr-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/stestr-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  (
    export PYTHONPATH="$PWD/tmp_install/usr/lib/python3.7/site-packages"
    export PATH="$PWD/tmp_install/usr/bin:$PATH"
    stestr run || warning "Tests failed"
  )

  cd "$srcdir"/stestr-$pkgver-py2
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  (
    export PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages"
    export PATH="$PWD/tmp_install/usr/bin:$PATH"
    stestr run || warning "Tests failed"
  )
}

package_python-stestr() {
  depends=('python-future' 'python-cliff' 'python-pbr' 'python-subunit' 'python-fixtures'
           'python-testtools' 'python-yaml' 'python-voluptuous')

  cd stestr-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-stestr() {
  depends=('python2-future' 'python2-cliff' 'python2-pbr' 'python2-subunit' 'python2-fixtures'
           'python2-testtools' 'python2-yaml' 'python2-voluptuous')

  cd stestr-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/stestr{,2}
}
