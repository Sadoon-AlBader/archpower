# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Nikola MilinkoviÄ‡ <nikmil@gmail.com>
# Submitter: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

_pkgbase=regex
pkgbase=python-regex
pkgname=('python-regex' 'python2-regex')
pkgver=2020.7.14
pkgrel=1
pkgdesc="Alternative python regular expression module."
arch=(x86_64 powerpc64le)
url="https://bitbucket.org/mrabarnett/mrab-regex"
license=('Python')
makedepends=('python-setuptools' 'python2-setuptools')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/r/${_pkgbase}/${_pkgbase}-${pkgver}.tar.gz")
sha256sums=('3a3af27a8d23143c49a3420efe5b3f8cf1a48c6fc8bc6856b03f638abc1833bb')
b2sums=('d77f40c5dc4ced1e66dc385e6f203cb2479ee016d8c5ec6987c839b5b1ae576eda04d1d53268188ffe91c9cdf9e60e5b789ad698a680311362ecc7f4cb09d498')

case "${CARCH}" in
  "powerpc64le")
    export MYCARCH=ppc64le
    ;;
  *)
    export MYCARCH=${CARCH}
esac

build() {
  cd "regex-${pkgver}"

  python setup.py build
  python2 setup.py build
}

check() {
  cd "regex-${pkgver}"

  pushd build/lib.linux-${MYCARCH}-3*/
  python -m unittest regex/test_regex.py
  popd

  pushd build/lib.linux-${MYCARCH}-2*/
  python2 -m unittest regex.test_regex
  popd
}

package_python2-regex() {
  depends=('python2')
  pkgdesc="Alternative python regular expression module. (python2 version)"

  cd "regex-${pkgver}"
  python2 setup.py install --root="${pkgdir}/" --optimize=1 --skip-build

  sed -n '1,/^$/p' regex_2/regex.py | install -Dm644 /dev/stdin "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_python-regex() {
  depends=('python')
  pkgdesc="Alternative python regular expression module. (python3 version)"

  cd "regex-${pkgver}"
  python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build

  sed -n '1,/^$/p' regex_3/regex.py | install -Dm644 /dev/stdin "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
