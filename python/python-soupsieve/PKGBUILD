# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-soupsieve
pkgname=(python-soupsieve python2-soupsieve)
pkgver=1.9.6
pkgrel=1
pkgdesc="A CSS4 selector implementation for Beautiful Soup"
url="https://github.com/facelessuser/soupsieve"
license=('MIT')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools' 'python2-backports.functools_lru_cache')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-beautifulsoup4'
              'python2-beautifulsoup4' 'python-html5lib' 'python2-html5lib' 'python-lxml'
              'python2-lxml')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/facelessuser/soupsieve/archive/$pkgver.tar.gz")
sha512sums=('44954cdedc16a51c794ebe6770caeb8f179167ad2f0a0478cb1efd5878d6cc49fffd6119125964b19184afa6d0d7dedadb81dee020a48c5128ccf56e36a78f61')

build() {
  cd soupsieve-$pkgver
  python setup.py build
  python2 setup.py build
}

check() {
  cd soupsieve-$pkgver
  python setup.py pytest
  python2 setup.py pytest
}

package_python-soupsieve() {
  depends=('python')

  cd soupsieve-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
}

package_python2-soupsieve() {
  depends=('python2-backports.functools_lru_cache')

  cd soupsieve-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
}
