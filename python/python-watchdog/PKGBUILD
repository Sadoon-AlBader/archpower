# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgbase=python-watchdog
pkgname=('python-watchdog' 'python2-watchdog')
pkgver=0.10.2
pkgrel=1
pkgdesc="Python API and shell utilities to monitor file system events."
arch=('any')
url="https://pypi.python.org/pypi/watchdog"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pathtools' 'python2-pathtools'
             'python-yaml' 'python2-yaml' 'python-argh' 'python2-argh')
checkdepends=('python-pytest-cov' 'python-pytest-timeout' 'python-pytest-runner')
source=($pkgbase-$pkgver.tar.gz::https://github.com/gorakhargosh/watchdog/archive/v$pkgver.tar.gz)
sha512sums=('3563a9686ce304350f33bbf4b16933cd0db5d6ab17676dfcc779b86cf9ddd06c42462ec40ad5c046cf4aa4b603aa477865de45daef05f9c6af3a441bcab16dbb')

build() {
  cd watchdog-$pkgver
  python setup.py build
  python2 setup.py build
}

check() {
  cd watchdog-$pkgver
  python setup.py pytest
}

package_python-watchdog() {
  depends=('python-pathtools' 'python-yaml' 'python-argh')

  cd watchdog-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-watchdog() {
  depends=('python2-pathtools' 'python2-yaml' 'python2-argh')

  cd watchdog-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/watchmedo{,2}
}

# vim:ts=2:sw=2:et:
