# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Roger Duran <rogerduran@gmail.com>

pkgbase=python-xcffib
pkgname=(python-xcffib python2-xcffib)
pkgver=0.8.0
pkgrel=1
pkgdesc="Python3 drop in replacement for xpyb, an XCB python binding"
arch=('any')
url="https://pypi.python.org/pypi/xcffib"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-cffi' 'python2-cffi' 'python-six'
             'python2-six' 'libxcb' 'haskell-xcffib')
checkdepends=('python-nose' 'python2-nose' 'xorg-server-xvfb' 'xorg-xeyes')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/tych0/xcffib/archive/v$pkgver.tar.gz")
sha512sums=('d7dbcb8cc2f3d9376df6466459596ceface6db79f71ee06ae354b84e26e660212befc4082ed84ed3fdd48b6373babe0989d5dbe76f5cc900aa2295ae385a2deb')

prepare() {
  cp -a xcffib-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/xcffib-$pkgver
  make xcffib GEN=xcffibgen CABAL=true
  python setup.py build

  cd "$srcdir"/xcffib-$pkgver-py2
  make xcffib GEN=xcffibgen CABAL=true
  python2 setup.py build
}

check() {
  cd "$srcdir"/xcffib-$pkgver
  nosetests3

  cd "$srcdir"/xcffib-$pkgver-py2
  nosetests2
}

package_python-xcffib() {
  depends=('python-six' 'python-cffi' 'libxcb')

  cd xcffib-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-xcffib() {
  depends=('python2-six' 'python2-cffi' 'libxcb')

  cd xcffib-$pkgver-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1
}
