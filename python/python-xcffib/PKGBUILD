# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Roger Duran <rogerduran@gmail.com>

pkgbase=python-xcffib
pkgname=(python-xcffib python2-xcffib)
pkgver=0.8.1
pkgrel=4
pkgdesc="Python3 drop in replacement for xpyb, an XCB python binding"
arch=('any')
url="https://pypi.python.org/pypi/xcffib"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-cffi' 'python2-cffi' 'python-six'
             'python2-six' 'libxcb' 'haskell-xcffib' 'xorgproto')
checkdepends=('python-nose' 'python2-nose' 'xorg-server-xvfb' 'xorg-xeyes')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/tych0/xcffib/archive/v$pkgver.tar.gz")
sha512sums=('ad1f643191e9f6ae4bd50e4b05c5d5ca144553414950877bf9fa371c0a5a607db3a3efd0009e64a4a392a824075af3f5bf0cbca91861b3b29c98bf58e8a5e7ab')

prepare() {
  cp -a xcffib-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/xcffib-$pkgver
  make xcffib GEN=xcffibgen CABAL=true
  python setup.py build

  cd "$srcdir"/xcffib-$pkgver-py2
  make xcffib GEN=xcffibgen CABAL=true
  python2 setup.py build
}

check() {
  cd "$srcdir"/xcffib-$pkgver
  xvfb-run nosetests3

  cd "$srcdir"/xcffib-$pkgver-py2
  xvfb-run nosetests2
}

package_python-xcffib() {
  depends=('python-six' 'python-cffi' 'libxcb')

  cd xcffib-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-xcffib() {
  depends=('python2-six' 'python2-cffi' 'libxcb')

  cd xcffib-$pkgver-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1
}
