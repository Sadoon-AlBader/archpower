# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-wsgiproxy2
pkgname=('python-wsgiproxy2' 'python2-wsgiproxy2')
pkgver=0.4.6
pkgrel=2
pkgdesc="A WSGI Proxy with various http client backends"
arch=('any')
license=('MIT')
url="https://github.com/gawel/WSGIProxy2/"
makedepends=('python-setuptools' 'python2-setuptools' 'python-webob' 'python2-webob'
             'python-six' 'python2-six')
checkdepends=('python-nose' 'python2-nose' 'python-requests' 'python2-requests'
              'python-webtest' 'python2-webtest' 'python-waitress' 'python2-waitress')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/gawel/WSGIProxy2/archive/$pkgver.tar.gz")
sha512sums=('9a3c8e96cafe4451943f63bbf47cc0f6ffc7a6916cd8e42f7d1aa53ccab59a74f772c55c1a1f456e97e72c5d15f1601c6501e2c0650bb54c81acd52696580023')

build() {
  cd WSGIProxy2-$pkgver
  python setup.py build
  python2 setup.py build
}

check() {
  cd WSGIProxy2-$pkgver
  nosetests3
  nosetests2
}

package_python-wsgiproxy2() {
  depends=('python-webob' 'python-six')

  cd WSGIProxy2-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-wsgiproxy2() {
  depends=('python2-webob' 'python2-six')

  cd WSGIProxy2-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
}
