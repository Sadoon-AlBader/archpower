# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgname=python-pytest
pkgver=6.1.0
pkgrel=1
pkgdesc="Simple powerful testing with Python"
arch=('any')
license=('MIT')
url="https://pytest.org/"
depends=('python-attrs' 'python-iniconfig' 'python-more-itertools' 'python-packaging' 'python-pluggy'
         'python-py' 'python-toml')
makedepends=('python-setuptools-scm')
checkdepends=('python-argcomplete' 'python-hypothesis' 'python-mock' 'python-nose' 'python-requests'
              'python-xmlschema'
              # Unlisted, but actually used. Check again after a while:
              'python-decorator' 'python-docutils' 'python-jinja' 'python-numpy' 'python-pexpect'
              'python-pygments' 'python-twisted' 'python-pytest-xdist')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pytest-dev/pytest/archive/$pkgver.tar.gz")
sha512sums=('fdf60a3a1c93a254f51652f4a56204d08faa763bd313a96fca4b439c877e05ac3c78d93d2ca9af9639a884415709a30736ef1b890ee10dee3d55f6accd3ad57b')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd pytest-$pkgver
  python setup.py build
}

check() {
  cd pytest-$pkgver
  python setup.py egg_info
  PYTHONPATH="$PWD"/build/lib python src/pytest/__main__.py
}

package() {
  cd pytest-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
