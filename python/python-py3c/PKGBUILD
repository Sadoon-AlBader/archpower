# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-py3c
pkgver=1.1
pkgrel=2
pkgdesc="A Python 2/3 compatibility layer for C extensions"
url="https://github.com/encukou/py3c"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/encukou/py3c/archive/v$pkgver.tar.gz")
sha512sums=('aa95bbe1a80eec9e32d3dab4b0771a35fc467052757077fa17b42ceba78a5fe7fb1fa99079240aeeea5538abff778518b706f3bf16dbce2cd4f7dc1900c61b24')

prepare() {
  cd py3c-$pkgver
  # Don't regenerate with broken path in "install" target
  sed -i 's/install: py3c.pc/install:/' Makefile
}

build() {
  cd py3c-$pkgver
  python setup.py build
  make prefix=/usr py3c.pc
}

check() {
  cd py3c-$pkgver
  make test-python
}

package() {
  cd py3c-$pkgver
  make prefix="$pkgdir"/usr install
  python setup.py install --root="$pkgdir" --optimize=1

  # Remove headers in broken location
  rm -r "$pkgdir"/usr/include/python*

  install -Dm644 LICENSE.MIT -t "$pkgdir"/usr/share/licenses/$pkgname/
}
