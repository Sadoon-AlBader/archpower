# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-oslo-config
pkgver=8.1.0
pkgrel=1
pkgdesc="parsing command line arguments and .ini style configuration files"
arch=('any')
url="https://pypi.python.org/pypi/oslo.config/$pkgver"
license=('Apache')
depends=('python-six' 'python-netaddr' 'python-stevedore' 'python-debtcollector'
         'python-oslo-i18n' 'python-rfc3986' 'python-yaml')
makedepends=('python-setuptools')
checkdepends=('python-oslotest' 'python-sphinx' 'python-requests-mock' 'python-oslo-log')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/oslo.config/archive/$pkgver.tar.gz")
sha512sums=('7ec73f269278c883b9f7be2832830943213804a2cbc6bd1b9931b40edce6712ab8e040ecacca7ff6dc377aa672d39177590eaae56fc3e8e9c8060d975411bfd5')

export PBR_VERSION=$pkgver

build() {
  cd oslo.config-$pkgver
  python setup.py build
}

check() {
  cd oslo.config-$pkgver
  stestr run
}

package() {
  cd oslo.config-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
