# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-oslo-config
pkgname=(python-oslo-config python2-oslo-config)
pkgver=6.11.1
pkgrel=3
pkgdesc="parsing command line arguments and .ini style configuration files"
arch=('any')
url="https://pypi.python.org/pypi/oslo.config/$pkgver"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-netaddr' 'python2-netaddr'
             'python-stevedore' 'python2-stevedore' 'python-debtcollector' 'python2-debtcollector'
             'python-oslo-i18n' 'python2-oslo-i18n' 'python-rfc3986' 'python2-rfc3986'
             'python-yaml' 'python2-yaml' 'python2-enum34')
checkdepends=('python-oslotest' 'python2-oslotest' 'python-sphinx' 'python2-sphinx'
              'python-requests-mock' 'python2-requests-mock' 'python-oslo-log' 'python2-oslo-log')
options=('!emptydirs')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/oslo.config/archive/$pkgver.tar.gz")
sha512sums=('ea01bcd83900f368f7df29c841ed17bfd8baad1d011da83287111d36ca45d7e00647ecd68c40e34d61f5d0bb07eeb4313c6fcb41efae102110593f81a7ea7242')

prepare() {
  sed -i '/argparse/d' oslo.config-$pkgver/requirements.txt

  cp -a oslo.config-$pkgver{,-py2}
  find oslo.config-$pkgver-py2 -name \*.py -exec sed -i '1s/python$/&2/' {} +

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/oslo.config-$pkgver
  python setup.py build

  cd "$srcdir"/oslo.config-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/oslo.config-$pkgver
  stestr run

  cd "$srcdir"/oslo.config-$pkgver-py2
  PYTHON=python2 stestr2 run
}

package_python-oslo-config() {
  depends=('python-six' 'python-netaddr' 'python-stevedore' 'python-debtcollector'
           'python-oslo-i18n' 'python-rfc3986' 'python-yaml')

  cd "$srcdir"/oslo.config-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-oslo-config() {
  depends=('python2-six' 'python2-netaddr' 'python2-stevedore' 'python2-debtcollector'
           'python2-oslo-i18n' 'python2-rfc3986' 'python2-yaml' 'python2-enum34')

  cd "$srcdir"/oslo.config-$pkgver-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1
  mv "$pkgdir"/usr/bin/oslo-config-generator{,2}
  mv "$pkgdir"/usr/bin/oslo-config-validator{,2}
}

# vim:set ts=2 sw=2 et:
