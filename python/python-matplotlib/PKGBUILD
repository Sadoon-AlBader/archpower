# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>

pkgname=python-matplotlib
pkgver=3.3.0
pkgrel=3
pkgdesc="A python plotting library, making publication quality plots"
arch=(x86_64 powerpc64le)
url="https://matplotlib.org"
license=(custom)
depends=(freetype2 python-cycler python-dateutil python-kiwisolver python-numpy python-pillow python-pyparsing qhull)
optdepends=('tk: Tk{Agg,Cairo} backends'
            'python-pyqt5: Qt5{Agg,Cairo} backends'
            'python-gobject: for GTK3{Agg,Cairo} backend'
            'python-wxpython: WX{,Agg,Cairo} backend'
            'python-cairo: {GTK3,Qt5,Tk,WX}Cairo backends'
            'python-cairocffi: alternative for Cairo backends'
            'python-tornado: WebAgg backend'
            'ffmpeg: for saving movies'
            'imagemagick: for saving animated gifs'
            'ghostscript: usetex dependencies'
            'texlive-bin: usetex dependencies'
            'texlive-latexextra: usetex usage with pdflatex')
makedepends=(git rsync python-setuptools
             tk python-pyqt5 python-gobject
             python-wxpython python-cairocffi python-tornado
             ghostscript texlive-bin)
             # agg missing some non-upstreamed patches?
checkdepends=(python-pytest-xdist python-pytest-runner xorg-server-xvfb
              texlive-core texlive-latexextra inkscape mencoder
              ffmpeg imagemagick python-pandas)
source=("https://github.com/matplotlib/matplotlib/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
         matplotlib-fix-path-of-degenerate-polygons.patch::"https://github.com/matplotlib/matplotlib/commit/07847dd2.patch")
sha512sums=('79555a175ef1e63da4f3d2457dc44dd825dbe4b5833696fa9ba57bfc654dc4df286ede4357ee276f19d674be1cb622e8718000a74c1f122cc13641f4d3ad0ada'
            'b16c4af737a08004ad83f5eb058ffd1470f6cb522114ef8ef74e211887a6c00809b7f475cdb83cceb46aaaff6acfd0c25c8ba1fbcbf2fd536e09efde35e80e2a')

prepare() {
  cd matplotlib-${pkgver}
  patch -p1 -i ../matplotlib-fix-path-of-degenerate-polygons.patch # Fix path of degenerate polygons

# Use system freetype and qhull
  sed -e 's|#system_freetype = False|system_freetype = True|' -e 's|#system_qhull = False|system_qhull = True|' setup.cfg.template > setup.cfg
}

build() {
  cd matplotlib-${pkgver}
  python setup.py build
}

check() {
  cd matplotlib-${pkgver}
  xvfb-run -a -s "+extension GLX +extension RANDR +render -screen 0 1280x1024x24" \
    python setup.py pytest --addopts="-n auto" || warning "Tests failed" # Different font rendering details
}

package_python-matplotlib() {
  cd matplotlib-${pkgver}
  python setup.py install --root "${pkgdir}" --prefix=/usr --optimize=1 --skip-build
  install -Dm644 doc/users/license.rst -t "${pkgdir}"/usr/share/licenses/${pkgname}/
  # Needed since https://github.com/matplotlib/matplotlib/pull/14170
  rm -r "${pkgdir}"/usr/lib/python3.8/site-packages/{matplotlib,mpl_toolkits}/tests/
}
