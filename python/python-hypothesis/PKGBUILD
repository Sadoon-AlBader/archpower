# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-hypothesis
pkgver=5.41.3
pkgrel=1
pkgdesc="Advanced Quickcheck style testing library for Python"
arch=('any')
license=('MPL')
url="https://hypothesis.readthedocs.org"
depends=('python-attrs' 'python-sortedcontainers')
optdepends=('python-pytz: for datetime and django module'
            'python-faker: for fakefactory and django module'
            'python-django: for django module'
            'python-numpy: for numpy module'
            'python-pytest: for pytest module'
            'python-lark-parser: for lark module')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'flake8' 'python-pytz' 'python-numpy' 'python-faker'
              'python-flaky' 'python-django' 'python-mock' 'python-pandas' 'python-dpcontracts'
              'python-pytest-xdist' 'python-lark-parser' 'python-pexpect' 'python-coverage'
              'python-typing_extensions' 'python-black' 'python-fakeredis')
source=("$pkgname-$pkgver.tar.gz::https://github.com/HypothesisWorks/hypothesis/archive/hypothesis-python-$pkgver.tar.gz")
sha512sums=('974dee6040222fa55729257cea4e3c4c049625a2a5dfddbc12667dbd58dfe31718474ce8e9de81519e998c7a0c8ff01920e0aa2bca9fb7100754e985177f4b69')

prepare() {
  mv hypothesis-hypothesis-python-$pkgver hypothesis-$pkgver
}

build() {
  cd hypothesis-$pkgver/hypothesis-python
  python setup.py build
}

check() {
  # tests/numpy/test_lazy_import.py: https://github.com/HypothesisWorks/hypothesis/issues/2411

  cd hypothesis-$pkgver/hypothesis-python
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  python examples/example_hypothesis_entrypoint/setup.py install --root="$PWD/tmp_install" --optimize=1

  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.9/site-packages:$PWD/examples/example_hypothesis_entrypoint" \
  PATH="$PWD/tmp_install/usr/bin:$PATH" \
    pytest --ignore tests/numpy/test_lazy_import.py --ignore tmp_install
}

package() {
  cd hypothesis-$pkgver/hypothesis-python
  python setup.py install --root="$pkgdir" --optimize=1

  # It's only present when tests are enabled, so adding -f
  rm -rf "$pkgdir"/usr/lib/python3.9/site-packages/tests
}
