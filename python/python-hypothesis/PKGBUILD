# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-hypothesis
pkgver=5.20.3
pkgrel=1
pkgdesc="Advanced Quickcheck style testing library for Python"
arch=('any')
license=('MPL')
url="https://hypothesis.readthedocs.org"
depends=('python-attrs' 'python-sortedcontainers')
optdepends=('python-pytz: for datetime and django module'
            'python-faker: for fakefactory and django module'
            'python-django: for django module'
            'python-numpy: for numpy module'
            'python-pytest: for pytest module'
            'python-lark-parser: for lark module')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'flake8' 'python-pytz' 'python-numpy' 'python-faker'
              'python-flaky' 'python-django' 'python-mock' 'python-pandas' 'python-dpcontracts'
              'python-pytest-xdist' 'python-lark-parser' 'python-pexpect' 'python-coverage'
              'python-typing_extensions')
source=("$pkgname-$pkgver.tar.gz::https://github.com/HypothesisWorks/hypothesis/archive/hypothesis-python-$pkgver.tar.gz")
sha512sums=('2b8819c425f607e300ad569115cc72b3ab45026a4d82b7f52a66fc60fe92d392a4ca0dac3260969409006958a81e69fbaac1b4612bfa6236e27f4d8f996acb89')

prepare() {
  mv hypothesis-hypothesis-python-$pkgver hypothesis-$pkgver
}

build() {
  cd hypothesis-$pkgver/hypothesis-python
  python setup.py build
}

check() {
  cd hypothesis-$pkgver/hypothesis-python
  python setup.py pytest
}

package() {
  cd hypothesis-$pkgver/hypothesis-python
  python setup.py install --root="$pkgdir" --optimize=1
}
