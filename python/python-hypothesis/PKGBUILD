# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-hypothesis
pkgname=('python-hypothesis' 'python2-hypothesis')
pkgver=4.54.2
pkgrel=2
pkgdesc="Advanced Quickcheck style testing library for Python"
arch=('any')
license=('MPL')
url="https://hypothesis.readthedocs.org"
makedepends=('python-setuptools' 'python2-setuptools' 'python-attrs' 'python2-attrs'
             'python-coverage' 'python2-coverage' 'python2-enum34')
checkdepends=('python-pytest-runner' 'flake8' 'python-pytz' 'python-numpy' 'python-faker'
              'python-flaky' 'python-django' 'python-mock' 'python-pandas' 'python-dpcontracts'
              'python-pytest-xdist' 'python-lark-parser' 'python-pexpect')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/HypothesisWorks/hypothesis/archive/hypothesis-python-$pkgver.tar.gz")
sha512sums=('41076ecc04a2ecadba814988b42d3d66e0f07ef80a0dbe928d582c39caac5cb7bfa0b0cdc1937c91a700181b6de4a8ef94310bb6ecb9a3fac57616f613825f9c')

prepare() {
  mv hypothesis-hypothesis-python-$pkgver hypothesis-$pkgver

  cp -a hypothesis-$pkgver{,-py2}

  rm -r hypothesis-$pkgver/hypothesis-python/tests/py2
  rm -r hypothesis-$pkgver-py2/hypothesis-python/tests/py3

  export LC_CTYPE=en_US.UTF-8
}

build() {
  cd "$srcdir"/hypothesis-$pkgver/hypothesis-python
  python setup.py build

  cd "$srcdir"/hypothesis-$pkgver-py2/hypothesis-python
  python2 setup.py build
}

check() {
  cd hypothesis-$pkgver/hypothesis-python
  mv tests/django ../
  python setup.py pytest
  mv ../django tests/
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python -m tests.django.manage test tests.django
}

package_python-hypothesis() {
  depends=('python-attrs' 'python-coverage')
  optdepends=('python-pytz: for datetime and django module'
              'python-faker: for fakefactory and django module'
              'python-django: for django module'
              'python-numpy: for numpy module'
              'python-pytest: for pytest module'
              'python-lark-parser: for lark module')

  cd hypothesis-$pkgver/hypothesis-python
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-hypothesis() {
  depends=('python2-attrs' 'python2-coverage' 'python2-enum34')
  optdepends=('python2-pytz: for datetime and django module'
              'python2-numpy: for numpy module'
              'python2-pytest: for pytest module')

  cd hypothesis-$pkgver-py2/hypothesis-python
  python2 setup.py install --root="$pkgdir" --optimize=1
}
