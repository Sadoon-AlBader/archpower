# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-oslo-utils
pkgname=(python-oslo-utils python2-oslo-utils)
pkgver=3.42.1
pkgrel=1
pkgdesc="Oslo Utility library"
arch=('any')
url="https://pypi.python.org/pypi/oslo.utils/$pkgver"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python2-funcsigs'
             'python-iso8601' 'python2-iso8601' 'python-oslo-i18n' 'python2-oslo-i18n'
             'python2-monotonic' 'python-pytz' 'python2-pytz' 'python-netaddr' 'python2-netaddr'
             'python-netifaces' 'python2-netifaces' 'python-debtcollector' 'python2-debtcollector')
checkdepends=('python-oslotest' 'python2-oslotest' 'python-ddt' 'python2-ddt' 'python-eventlet'
              'python2-eventlet')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/oslo.utils/archive/$pkgver.tar.gz")
sha512sums=('7e7161e2c65c5b722335d45fd73e6debb7d9ae0f7ff792d2d8600ee356a3a97028daf5da980e8cdfd3fbef47312f1c7c47411fea431711eebadb92c90e723dd9')

prepare() {
  cp -a oslo.utils-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/oslo.utils-$pkgver
  python setup.py build

  cd "$srcdir"/oslo.utils-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/oslo.utils-$pkgver
  stestr run || warning "Tests failed"

  cd "$srcdir"/oslo.utils-$pkgver-py2
  PYTHON=python2 stestr2 run
}

package_python-oslo-utils() {
  depends=('python-six' 'python-pbr' 'python-iso8601' 'python-oslo-i18n' 'python-pytz'
           'python-netaddr' 'python-netifaces' 'python-debtcollector')

  cd "$srcdir"/oslo.utils-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-oslo-utils() {
  depends=('python2-six' 'python2-pbr' 'python2-iso8601' 'python2-oslo-i18n' 'python2-monotonic'
           'python2-pytz' 'python2-netaddr' 'python2-netifaces' 'python2-debtcollector'
           'python2-funcsigs')

  cd "$srcdir"/oslo.utils-$pkgver-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
