# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pylons-sphinx-themes
pkgname=(python-pylons-sphinx-themes python2-pylons-sphinx-themes)
pkgver=1.0.10
pkgrel=3
pkgdesc="Sphinx themes for Pylons Project documentation"
url="https://pylonsproject.org"
license=('BSD')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/Pylons/pylons-sphinx-themes/archive/$pkgver.tar.gz")
sha512sums=('a86809c1e961c293311c91199e2e3ac49521983386434bb03b14fa56820a087a927d282ef7220777104014a581fcc77169de7e7f3d951970de5a8166270796a3')

prepare() {
  cp -a pylons-sphinx-themes-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pylons-sphinx-themes-$pkgver
  python setup.py build

  cd "$srcdir"/pylons-sphinx-themes-$pkgver-py2
  python2 setup.py build
}

package_python-pylons-sphinx-themes() {
  depends=('python')

  cd pylons-sphinx-themes-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

package_python2-pylons-sphinx-themes() {
  depends=('python2')

  cd pylons-sphinx-themes-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
