# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-http-parser
pkgname=('python-http-parser' 'python2-http-parser')
pkgver=0.8.3
pkgrel=5
pkgdesc="HTTP request/response parser for Python"
arch=(x86_64 powerpc64le)
license=('MIT')
url="https://github.com/benoitc/http-parser"
makedepends=('python-setuptools' 'python2-setuptools' 'cython' 'git')
checkdepends=('python-pytest' 'python2-pytest')
source=("${pkgname}::git+https://github.com/benoitc/http-parser.git#tag=$pkgver")
sha512sums=('SKIP')

prepare() {
  rm -rf ${pkgbase}/http_parser/parser.c ${pkgbase}-py2
  cp -a ${pkgbase}{,-py2}
}

build() {
  cd "$srcdir/$pkgbase"
  python setup.py build

  cd "$srcdir/$pkgbase-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/$pkgbase"
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.8:$PYTHONPATH" \
  py.test testing/

  cd "$srcdir/$pkgbase-py2"
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-2.7:$PYTHONPATH" \
  py.test2 testing/
}

package_python-http-parser() {
  depends=('python')

  cd ${pkgbase}
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-http-parser() {
  depends=('python2')

  cd ${pkgbase}-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
