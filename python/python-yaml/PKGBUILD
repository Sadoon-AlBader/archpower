# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Michal Bozon <michal.bozon__at__gmail.com>

pkgbase=python-yaml
pkgname=(python-yaml python2-yaml)
pkgver=5.1
pkgrel=1
pkgdesc='Python bindings for YAML, using fast libYAML library'
url='http://pyyaml.org/wiki/PyYAML'
arch=(x86_64 powerpc64le)
license=('MIT')
makedepends=('python' 'python2' 'libyaml' 'cython' 'cython2')
source=(http://pyyaml.org/download/pyyaml/PyYAML-${pkgver}.tar.gz)
sha512sums=('8f27f92bdfa310a99dd6d83947332cc033fa18f0011998bb585ad5c4340a2da20d8c20bfdb53beaae15651198d1240c986818379b0a05b230f74d1f30f53e7fd')

prepare() {
  # Force cython rebuild
  rm PyYAML-$pkgver/ext/_yaml.c

  cp -a PyYAML-$pkgver{,-py2}
}

build() {
  msg2 "Building python..."
  (cd PyYAML-$pkgver
    python setup.py --with-libyaml build
  )
  msg2 "Building python2..."
  (cd PyYAML-$pkgver-py2
    python2 setup.py --with-libyaml build
  )
}

check() {
  msg2 "Checking python..."
  (cd PyYAML-$pkgver
    python setup.py test || warning "https://github.com/yaml/pyyaml/issues/184"
  )
  msg2 "Checking python2..."
  (cd PyYAML-$pkgver-py2
    python2 setup.py test || warning "https://github.com/yaml/pyyaml/issues/184"
  )
}

package_python-yaml() {
  depends=('python' 'libyaml')

  cd PyYAML-$pkgver
  python setup.py  --with-libyaml install --prefix=/usr --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
  install -Dm 644 CHANGES README -t "${pkgdir}"/usr/share/doc/${pkgname}
}

package_python2-yaml() {
  depends=('python2' 'libyaml')

  cd PyYAML-$pkgver-py2
  python2 setup.py --with-libyaml install --prefix=/usr --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
  install -Dm 644 CHANGES README -t "${pkgdir}"/usr/share/doc/${pkgname}
}

# vim: ts=2 sw=2 et:
