# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-commonmark
pkgname=('python-commonmark' 'python2-commonmark')
pkgver=0.9.1
pkgrel=1
pkgdesc='Python parser for the commonmark Markdown spec'
url='https://pypi.python.org/pypi/commonmark'
arch=('any')
license=('BSD')
makedepends=('python-setuptools' 'python-docutils' 'python-future' 'python-sphinx'
             'python2-setuptools' 'python2-docutils' 'python2-future' 'python2-sphinx'
             'python-sphinx_rtd_theme' 'python2-sphinx_rtd_theme')
checkdepends=('python-hypothesis' 'python2-hypothesis')
options=('!makeflags')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/rtfd/commonmark-py/archive/${pkgver}.tar.gz)
sha256sums=('b5d5be6af35a183c3f915d714b31b61e6918894051114419f9ac0bbb715bf4c6')
sha512sums=('a6de0b384247ff79fe326012bd1fc71135e6ae5bc7b6a15d5402969a94f81fdd802f1a9d557c504eb74bfb3fea68f55355a70a6e0112a620cc285cf9039892e4')

prepare() {
  cp -r commonmark.py-${pkgver}{,-py2}
}

build() {
  (cd commonmark.py-${pkgver}
    python setup.py build
    make -C docs man text SPHINXBUILD=sphinx-build
  )
  (cd commonmark.py-${pkgver}-py2
    python2 setup.py build
    make -C docs man text SPHINXBUILD=sphinx-build2
  )
}

check() {
  (cd commonmark.py-${pkgver}
    export PYTHONPATH=.
    python commonmark/tests/unit_tests.py
    python setup.py test
  )
  (cd commonmark.py-${pkgver}-py2
    export PYTHONPATH=.
    export PYTHONIOENCODING=UTF-8
    python2 commonmark/tests/unit_tests.py
    python2 setup.py test
  )
}

package_python-commonmark() {
  depends=('python-docutils' 'python-future')

  cd commonmark.py-${pkgver}
  python setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 README.rst CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 docs/_build/text/* -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/man/commonmark-py.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
  mv "${pkgdir}/usr/bin/cmark"{,-py3}
}

package_python2-commonmark() {
  depends=('python2-docutils' 'python2-future')

  cd commonmark.py-${pkgver}-py2
  python2 setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 README.rst CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 docs/_build/text/* -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/man/commonmark-py.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
  mv "${pkgdir}/usr/bin/cmark"{,-py2}
}

# vim: ts=2 sw=2 et:
