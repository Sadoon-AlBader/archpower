# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgbase=python-anytree
pkgname=(python-anytree python2-anytree)
pkgver=2.5.0
pkgrel=1
pkgdesc="Powerful and Lightweight Python Tree Data Structure"
url="https://anytree.readthedocs.io/"
arch=(any)
license=(Apache)
makedepends=(python{,2}-{six,setuptools} git)
_commit=caeb5886f8e582c92e3000acf00c1066fae669d0  # tags/2.5.0
source=("$pkgbase::git+https://github.com/c0fec0de/anytree#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgbase
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cp -a $pkgbase python2
  cp -a $pkgbase python3
}

build() {
  cd python3
  python3 setup.py build

  cd ../python2
  python2 setup.py build
}

package_python-anytree() {
  depends=(python-six)

  cd python3
  python3 setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

package_python2-anytree() {
  depends=(python2-six)

  cd python2
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
