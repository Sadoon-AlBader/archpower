# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=python-anytree
pkgver=2.7.1
pkgrel=1
pkgdesc="Powerful and Lightweight Python Tree Data Structure"
url="https://anytree.readthedocs.io/"
arch=(any)
license=(Apache)
depends=(python-six)
makedepends=(python-setuptools git)
_commit=d8a45625398847267e02775ce0323099fd7d3ec2  # tags/2.7.1
source=("$pkgname::git+https://github.com/c0fec0de/anytree#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
  cd $pkgname
  python setup.py build
}

package() {
  cd $pkgname
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
