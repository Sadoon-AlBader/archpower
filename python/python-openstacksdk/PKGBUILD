# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-openstacksdk
pkgname=(python-openstacksdk python2-openstacksdk)
pkgver=0.35.0
pkgrel=3
pkgdesc="An SDK for building applications to work with OpenStack"
arch=('any')
url="https://developer.openstack.org/sdks/python/openstacksdk"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-jsonpatch'
             'python2-jsonpatch' 'python-appdirs' 'python2-appdirs' 'python-keystoneauth1'
             'python2-keystoneauth1' 'python-os-service-types' 'python2-os-service-types'
             'python-deprecation' 'python2-deprecation' 'python-requestsexceptions'
             'python2-requestsexceptions' 'python-yaml' 'python2-yaml' 'python-decorator'
             'python2-decorator' 'python-jmespath' 'python2-jmespath' 'python2-ipaddress'
             'python2-futures' 'python-iso8601' 'python2-iso8601' 'python-netifaces'
             'python2-netifaces' 'python-dogpile.cache' 'python2-dogpile.cache' 'python-munch'
             'python2-munch')
checkdepends=('python-stestr' 'python2-stestr' 'python-mock' 'python2-mock' 'python-requests-mock'
              'python2-requests-mock' 'python-oslotest' 'python2-oslotest' 'python-jsonschema'
              'python2-jsonschema')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/openstacksdk/archive/$pkgver.tar.gz")
sha512sums=('fb8c4a4fab27d49f47784440392499c733512cae618fd5fef3aa86469ec7e53763f7bed2b4cb317192824f522abc47b69afc8307001b4b134b534cfd45fd13e4')

export PBR_VERSION=$pkgver

prepare() {
  echo -e "\nglobal-include *.json" >> openstacksdk-$pkgver/MANIFEST.in
  cp -a openstacksdk-$pkgver{,-py2}
  sed -i 's/assertItemsEqual/assertCountEqual/' openstacksdk-$pkgver/openstack/tests/unit/*.py openstacksdk-$pkgver/openstack/tests/unit/*/*.py
}

build() {
  cd "$srcdir"/openstacksdk-$pkgver
  python setup.py build

  cd "$srcdir"/openstacksdk-$pkgver-py2
  python2 setup.py build
}

check() {
  # From https://github.com/openstack/openstacksdk/blob/master/tox.ini
  export OS_LOG_CAPTURE=true OS_STDOUT_CAPTURE=true OS_STDERR_CAPTURE=true

  cd "$srcdir"/openstacksdk-$pkgver
  stestr run || warning "Tests failed"

  cd "$srcdir"/openstacksdk-$pkgver-py2
  PYTHON=python2 stestr2 run || warning "Tests failed"
}

package_python-openstacksdk() {
  depends=('python-six' 'python-pbr' 'python-jsonpatch' 'python-appdirs' 'python-keystoneauth1'
           'python-os-service-types' 'python-deprecation' 'python-requestsexceptions'
           'python-decorator' 'python-jmespath' 'python-iso8601' 'python-netifaces'
           'python-dogpile.cache' 'python-munch')

  cd "$srcdir"/openstacksdk-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-openstacksdk() {
  depends=('python2-six' 'python2-pbr' 'python2-jsonpatch' 'python2-appdirs'
           'python2-keystoneauth1' 'python2-os-service-types' 'python2-deprecation'
           'python2-requestsexceptions' 'python2-decorator' 'python2-jmespath' 'python2-ipaddress'
           'python2-futures' 'python2-iso8601' 'python2-netifaces' 'python2-dogpile.cache'
           'python2-munch')

  cd "$srcdir"/openstacksdk-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/openstack-inventory{,2}
}

# vim:set ts=2 sw=2 et:
