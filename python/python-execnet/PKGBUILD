# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-execnet
pkgname=('python-execnet' 'python2-execnet')
pkgver=1.7.0
pkgrel=1
pkgdesc="Rapid multi-Python deployment"
arch=('any')
license=('MIT')
url="https://codespeak.net/execnet"
makedepends=('python-setuptools-scm' 'python2-setuptools-scm' 'python-apipkg' 'python2-apipkg')
checkdepends=('python-pytest-timeout' 'python2-pytest-timeout' 'procps-ng' 'openssh' 'pypy')
source=("https://pypi.io/packages/source/e/execnet/execnet-$pkgver.tar.gz")
sha512sums=('5cc77f534d6a4966250a8494c53231dea938044268d3f81efd77ed20f5548ac167aa20e1e54fc3307828ba2937f5b1af44d2e8412110998b6016125fcf984c56')

prepare() {
  cp -a execnet-${pkgver}{,-py2}
}

build() {
  cd "$srcdir/execnet-${pkgver}"
  python setup.py build

  cd "$srcdir/execnet-${pkgver}-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/execnet-${pkgver}"
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" py.test testing

  cd "$srcdir/execnet-${pkgver}-py2"
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" py.test2 testing
}

package_python-execnet() {
  depends=('python-apipkg')

  cd execnet-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-execnet() {
  depends=('python2-apipkg')

  cd execnet-${pkgver}-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
