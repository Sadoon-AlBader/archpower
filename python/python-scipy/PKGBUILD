# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: Bodor Dávid Gábor <david.gabor.bodor@gmail.com>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>

_name=scipy
pkgname=python-scipy
pkgver=1.3.1
pkgrel=2
pkgdesc="SciPy is open-source software for mathematics, science, and engineering."
arch=(x86_64 powerpc64le)
url="https://www.scipy.org/"
license=('BSD')
depends=('python-numpy')
provides=('python3-scipy' 'scipy')
makedepends=('gcc-fortran' 'python-numpy' 'python-setuptools')
checkdepends=('python-pytest')
optdepends=('python-pillow: for image saving module')
#source=("https://github.com/scipy/scipy/releases/download/v${pkgver}/scipy-${pkgver}.tar.xz")
source=("https://pypi.python.org/packages/source/${_name:0:1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('672915da782f5a439a3920ad1a200937b8415aeb4f1db2451cf0b5a90ba6e18c69e17e9ff8433f6a7760bbef2434d7b0f6447319faec9bb96fa6841794bfa538')

build() {
  # required for gfortran
  export LDFLAGS="-Wall -shared"

  # 2 builds
  cp -r scipy-${pkgver} scipy-${pkgver}-py2

  # build for python3
  cd scipy-${pkgver}
  python3 setup.py config_fc --fcompiler=gnu95 build

}

check() {
  # we need to do a temp install so we can import scipy
  # also, the tests must not be run from the scipy source directory
  export LDFLAGS="-Wall -shared"

  cd scipy-${pkgver}
  python3 setup.py config_fc --fcompiler=gnu95 install \
    --prefix=/usr --root=${srcdir}/test --optimize=1
  export PYTHONPATH=${srcdir}/test/usr/lib/python3.7/site-packages
  cd ${srcdir}
  python -c "from scipy import test; test('full')"

}

package() {
  export LDFLAGS="-Wall -shared"

  cd scipy-${pkgver}
  python3 setup.py config_fc --fcompiler=gnu95 install \
    --prefix=/usr --root=${pkgdir} --optimize=1

  install -Dm644 LICENSE.txt \
    "${pkgdir}/usr/share/licenses/python-scipy/LICENSE"
}

# vim:set ts=2 sw=2 et:
