# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-invoke
pkgname=(python-invoke python2-invoke)
pkgver=1.2.0
pkgrel=2
pkgdesc="Pythonic task execution"
url="https://github.com/pyinvoke/invoke"
license=('BSD')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-mock' 'python2-mock'
              'python-pytest-relaxed' 'python2-pytest-relaxed')
source=("https://pypi.io/packages/source/i/invoke/invoke-$pkgver.tar.gz")
sha512sums=('354353d6fc359167ff36f999a6158c7f8dae1cd0d91ff5f344248a9b60e2ab3cdaf84d69b58e839d70b727e9417ab13cf84953b6f17c66d0f7aab839f13512dd')

prepare() {
  find invoke-$pkgver -type f -name '*.pyc' -delete
  cp -a invoke-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/invoke-$pkgver
  python setup.py build

  cd "$srcdir"/invoke-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/invoke-$pkgver
  python setup.py pytest || warning "Tests failed"

  cd "$srcdir"/invoke-$pkgver-py2
  python2 setup.py pytest || warning "Tests failed"
}

package_python-invoke() {
  depends=('python')

  cd invoke-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-invoke() {
  depends=('python2')

  cd invoke-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  for _exe in "$pkgdir"/usr/bin/*; do
    mv "$_exe"{,2}
  done
}
