# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-js2py
pkgname=(python-js2py python2-js2py)
pkgver=0.66
_commit=7858d1d0898070b8a9bce22c0c740a25b292f426
pkgrel=2
pkgdesc="JavaScript to Python Translator & JavaScript interpreter written in 100% pure Python"
url="https://github.com/PiotrDabkowski/Js2Py"
license=('MIT')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools' 'python-tzlocal' 'python2-tzlocal'
             'python-six' 'python2-six' 'python-pyjsparser' 'python2-pyjsparser')
checkdepends=('npm' 'python-numpy' 'python2-numpy')
source=("$pkgbase-$_commit.tar.gz::https://github.com/PiotrDabkowski/Js2Py/archive/$_commit.tar.gz")
sha512sums=('a30cfb3d9f48ceb36d881bcbf5f51f643d845168479a27157a2aa13d0834087e35acaaa95a8c05b27bec08ee305dc9d8fc98e88ef50ba9238f520f6f4c127219')

prepare() {
  mv Js2Py-{$_commit,$pkgver}
}

build() {
  cd Js2Py-$pkgver
  python setup.py build
  python2 setup.py build
}

check() {
  cd Js2Py-$pkgver
  python simple_test.py
  python2 simple_test.py || warning "https://github.com/PiotrDabkowski/Js2Py/issues/149"
}

package_python-js2py() {
  depends=('python-tzlocal' 'python-six' 'python-pyjsparser')

  cd Js2Py-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
}

package_python2-js2py() {
  depends=('python2-tzlocal' 'python2-six' 'python2-pyjsparser')

  cd Js2Py-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
}
