# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-mongomock
pkgver=3.21.0
pkgrel=2
pkgdesc="Fake pymongo stub for testing simple MongoDB-dependent code"
url="https://github.com/mongomock/mongomock"
license=('BSD')
arch=('any')
depends=('python-sentinels' 'python-six')
makedepends=('python-pbr')
checkdepends=('python-nose')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mongomock/mongomock/archive/$pkgver.tar.gz")
sha512sums=('8f289599948db3c949c90977e1e29af00334316727367412dfa183a613c0f988b875f8647b3b8052ae5d71d63e83e9e3b4b09d2fa8f4656d5283d62e0b084a33')

export PBR_VERSION=$pkgver

build() {
  cd mongomock-$pkgver
  python setup.py build
}

check() {
  cd mongomock-$pkgver
  python setup.py nosetests
}

package_python-mongomock() {
  cd mongomock-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
