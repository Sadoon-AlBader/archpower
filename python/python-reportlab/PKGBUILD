# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer:
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: William Rea <sillywilly@gmail.com>

_origname=reportlab
pkgbase=python-reportlab
pkgname=(python2-reportlab python-reportlab)
pkgver=3.5.28
pkgrel=1
pkgdesc="A proven industry-strength PDF generating solution"
arch=(x86_64 powerpc64le)
url="https://bitbucket.org/rptlab/reportlab"
depends=('freetype2')
makedepends+=('python' 'python2' 'freetype2' 'python2-pillow' 'python-pillow' 'mercurial')
license=('BSD')
source=(https://files.pythonhosted.org/packages/source/${_origname::1}/${_origname}/${_origname}-${pkgver}.tar.gz)
sha256sums=('7195c6ea096d10c91cc470f9f0ced3ad74470d9c0fd97923b5e764597dd13671')

build() {
    cp -rf ${_origname}-${pkgver} "${_origname}2"
}

package_python2-reportlab() {
    depends+=('python2' 'python2-pip' 'python2-pillow')

    cd ${_origname}2
    python2 setup.py install --root="$pkgdir" --optimize=1
    install -D -m644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}

package_python-reportlab() {
    depends+=('python3' 'python-pip' 'python-pillow')

    cd ${_origname}-${pkgver}
    python3 setup.py install --root="$pkgdir" --optimize=1
    install -D -m644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}

check() {
    cd ${_origname}-${pkgver}
    python2 setup.py tests
    python setup.py tests
}
