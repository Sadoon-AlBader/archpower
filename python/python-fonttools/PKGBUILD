# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Firmicus <firmicus āt gmx dōt net>
# Contributor: flying sheep <flying-sheep@web.de>

pkgbase=python-fonttools
pkgname=('python2-fonttools' 'python-fonttools')
pkgver=3.43.1
pkgrel=1
pkgdesc="Converts OpenType and TrueType fonts to and from XML"
url="https://github.com/fonttools/fonttools"
license=("MIT")
arch=('any')
makedepends=("python2-setuptools" "python-setuptools" "python-numpy" "python2-numpy")
source=("https://github.com/fonttools/fonttools/releases/download/${pkgver}/fonttools-${pkgver}.zip")
sha256sums=('217176a154341c05c91a1b2f78f5534fb7ca14b84671ac73893e14578527888e')

prepare() {
  cd "$srcdir"
  cp -a fonttools-${pkgver} fonttools-py2-${pkgver}
  cd fonttools-py2-${pkgver}

  sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
      -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
      -i $(find . -name '*.py')
}

build() {
  # "Building Python2"
  cd "$srcdir"/fonttools-py2-${pkgver}
  python2 setup.py build

  # "Building Python3"
  cd "$srcdir"/fonttools-${pkgver}
  python setup.py build
}

package_python2-fonttools() {
  depends=("python2-numpy")

  cd "$srcdir"/fonttools-py2-${pkgver}

  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1

  # fix conflicts with python-fonttools
  mv "$pkgdir"/usr/bin/pyftmerge{,2}
  mv "$pkgdir"/usr/bin/pyftsubset{,2}
  mv "$pkgdir"/usr/bin/ttx{,2}
  mv "$pkgdir"/usr/share/man/man1/ttx{,2}.1
  mv "$pkgdir"/usr/bin/fonttools{,2}

  install -D -m755 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  chmod oga+r "$pkgdir"/usr/share/man/man1/ttx2.1
}

package_python-fonttools() {
  depends=("python-numpy")

  cd "$srcdir"/fonttools-${pkgver}

  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -D -m755 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  chmod oga+r "$pkgdir"/usr/share/man/man1/ttx.1
}
