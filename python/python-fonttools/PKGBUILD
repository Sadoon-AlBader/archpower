# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Firmicus <firmicus āt gmx dōt net>
# Contributor: flying sheep <flying-sheep@web.de>

pkgname='python-fonttools'
pkgver=4.0.0
pkgrel=1
pkgdesc="A library to manipulate font files from Python"
url="https://github.com/fonttools/fonttools"
license=("MIT")
arch=('any')
depends=("python")
makedepends=("python-setuptools")
optdepends=("python-lxml: faster backend for XML files reading/writing"
	    "python-fs: to read/write UFO source files"
	    "python-brotli: to compress/decompress WOFF 2.0 web fonts"
	    "python-zopfli: faster backend fom WOFF 1.0 web fonts compression"
	    "python-scipy: for finding wrong contour/component order between different masters"
	    "python-matplotlib: for visualizing DesignSpaceDocument and resulting VariationModel"
	    "python-sympy: for symbolic font statistics analysis"
	    "python-pyqt5: for drawing glyphs with Qt's QPainterPath"
	    "python-reportlab: to drawing glyphs as PNG images")
source=("https://github.com/fonttools/fonttools/releases/download/${pkgver}/fonttools-${pkgver}.zip")
sha256sums=('9415fda795f4ff8e89d41ab907388ec5a4c236f4774ea65a746d8c1e4e839d3d')

build() {
  cd "$srcdir"/fonttools-${pkgver}
  python setup.py build
}

package() {
  cd "$srcdir"/fonttools-${pkgver}
  python setup.py install --skip-build --root="$pkgdir" --optimize=1
  install -D -m755 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
