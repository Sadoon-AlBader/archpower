# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: David Campbell <davekong@archlinux.us>
# Contributor: Cilyan Olowen <gaknar@gmail.com>

pkgbase=python-paste
pkgname=(python-paste python2-paste)
pkgver=3.2.6
pkgrel=1
pkgdesc="Tools for using a Web Server Gateway Interface stack"
arch=('any')
url="http://pythonpaste.org/index.html"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-six' 'python2-six')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-flup' 'python2-flup'
              'python2-openid')
source=("https://files.pythonhosted.org/packages/source/P/Paste/Paste-${pkgver}.tar.gz")
sha512sums=('036571772d5b26c4f6dc2283de9ae404438e745a1bf327ef602aea136d06926de7fa6cebbf7a5125ef17c865d68915711c035cb03deb3590be0418caf04afad3')

prepare() {
  cp -a Paste-${pkgver}{,-py2}

  cd Paste-${pkgver}-py2
  sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
         -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      $(find . -name '*.py')
}

build() {
  cd "$srcdir"/Paste-$pkgver
  python setup.py build

  cd "$srcdir"/Paste-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/Paste-$pkgver
  python setup.py pytest

  cd "$srcdir"/Paste-$pkgver-py2
  python2 setup.py pytest
}

package_python-paste() {
  depends=('python-six')
  optdepends=('python-flup: WSGI utilities')

  cd Paste-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 docs/license.txt "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}

package_python2-paste() {
  depends=('python2-six')
  optdepends=('python2-flup: WSGI utilities'
              'python2-openid: Support for OpenID')

  cd Paste-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 docs/license.txt "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}
