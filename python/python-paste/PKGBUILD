# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: David Campbell <davekong@archlinux.us>
# Contributor: Cilyan Olowen <gaknar@gmail.com>

pkgname=python-paste
pkgver=3.4.0
pkgrel=2
pkgdesc="Tools for using a Web Server Gateway Interface stack"
arch=('any')
url="http://pythonpaste.org/index.html"
license=('MIT')
depends=('python-six')
optdepends=('python-flup: WSGI utilities')
makedepends=('python-setuptools' 'python-six')
checkdepends=('python-pytest-runner' 'python-flup')
source=("https://files.pythonhosted.org/packages/source/P/Paste/Paste-${pkgver}.tar.gz")
sha512sums=('fbe0d1f39d8147846b3a30f18e6776c31eb5bcd8f461c0bf7afc35533c65c2ddf74263807be1b73b76c49cd6cc73c210a37ca651ef1f34e727ccf0687478085a')

build() {
  cd "$srcdir"/Paste-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/Paste-$pkgver
  python setup.py pytest
}

package() {
  cd Paste-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 docs/license.txt "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}
