# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Dan Serban
# Contributor: Richard Murri

pkgname=('python-werkzeug' 'python2-werkzeug')
pkgver=1.0.1
pkgrel=1
pkgdesc='Swiss Army knife of Python web development'
url='http://werkzeug.pocoo.org/'
arch=('any')
license=('custom:BSD')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-timeout' 'python2-pytest-timeout' 'python-requests' 'python2-requests')
source=("https://github.com/pallets/werkzeug/archive/${pkgver}.tar.gz")
sha512sums=('ba82120fb3e96e3a1334c6452d5ba1950fc016d2d147d643bbc2168ccec496f1dea799982ae8c578167155082c0bdbda5d56a23f64b4b6865f97e90e22df022e')

prepare() {
  cp -r "werkzeug-$pkgver" "python-werkzeug-$pkgver"
  cp -r "werkzeug-$pkgver" "python2-werkzeug-$pkgver"
}

build() {
  cd "$srcdir/python-werkzeug-$pkgver"

  python setup.py build

  cd "$srcdir/python2-werkzeug-$pkgver"

  python2 setup.py build
}

check() {
  cd "$srcdir/python-werkzeug-$pkgver"
  PYTHONPATH="$PWD/build/lib" pytest

  cd "$srcdir/python2-werkzeug-$pkgver"
  PYTHONPATH="$PWD/build/lib" pytest2
}

package_python-werkzeug() {
  depends=('python')
  cd "python-werkzeug-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.rst "$pkgdir/usr/share/licenses/$pkgname/LICENSE.rst"
}

package_python2-werkzeug() {
  depends=('python2')
  cd "python2-werkzeug-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.rst "$pkgdir/usr/share/licenses/$pkgname/LICENSE.rst"
}

# vim:set ts=2 sw=2 et:
