# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Dan Serban
# Contributor: Richard Murri

pkgname=('python-werkzeug' 'python2-werkzeug')
pkgver=0.16.0
pkgrel=1
pkgdesc='Swiss Army knife of Python web development'
url='http://werkzeug.pocoo.org/'
arch=('any')
license=('custom:BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pytest' 'python2-pytest' 'python-requests' 'python2-requests' 'python-hypothesis' 'python2-hypothesis')
source=("https://github.com/pallets/werkzeug/archive/${pkgver}.tar.gz")
sha512sums=('78e7d2ccceb8764f92a3252b2ff670065a42fe2ad1d2b4631e05c107d2128f8f6f4090e32bd975526e4b5985dbbdbac22e9ad1514d14e88ecfb064483107d0ed')

prepare() {
  cp -r "werkzeug-$pkgver" "python-werkzeug-$pkgver"
  cp -r "werkzeug-$pkgver" "python2-werkzeug-$pkgver"
}

build() {
  cd "$srcdir/python-werkzeug-$pkgver"

  python setup.py build

  cd "$srcdir/python2-werkzeug-$pkgver"

  python2 setup.py build
}

check() {
  cd "$srcdir/python-werkzeug-$pkgver"
  python setup.py test

  cd "$srcdir/python2-werkzeug-$pkgver"
  python2 setup.py test
}

package_python-werkzeug() {
  depends=('python')
  cd "python-werkzeug-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.rst "$pkgdir/usr/share/licenses/$pkgname/LICENSE.rst"
}

package_python2-werkzeug() {
  depends=('python2')
  cd "python2-werkzeug-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.rst "$pkgdir/usr/share/licenses/$pkgname/LICENSE.rst"
}

# vim:set ts=2 sw=2 et:
