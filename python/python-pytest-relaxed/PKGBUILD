# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pytest-relaxed
pkgname=('python-pytest-relaxed' 'python2-pytest-relaxed')
pkgver=1.1.5
pkgrel=1
pkgdesc='Relaxed test discovery for pytest'
arch=('any')
license=('BSD')
url='https://github.com/bitprophet/pytest-relaxed'
makedepends=('python-pytest32' 'python2-pytest32' 'python-decorator' 'python2-decorator'
             'python-six' 'python2-six')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/bitprophet/pytest-relaxed/archive/$pkgver.tar.gz")
sha512sums=('a5484bc92be29e5228afb7d9911c1d3a7d6e493bec44af541780de24b91dfa8e8c9e1245acd8ae8670805294eb52b20f7c4abefad1afb8373109249a2df1cdaa')

prepare() {
  cp -a pytest-relaxed-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pytest-relaxed-$pkgver
  python setup.py build

  cd "$srcdir"/pytest-relaxed-$pkgver-py2
  python2 setup.py build
}

check() {
  # Hack entry points by installing it

  cd "$srcdir"/pytest-relaxed-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.7/site-packages:$PYTHONPATH" py.test

  cd "$srcdir"/pytest-relaxed-$pkgver-py2
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages:$PYTHONPATH" py.test2
}

package_python-pytest-relaxed() {
  depends=('python-pytest32' 'python-decorator' 'python-six')

  cd pytest-relaxed-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pytest-relaxed() {
  depends=('python2-pytest32' 'python2-decorator' 'python2-six')

  cd pytest-relaxed-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
