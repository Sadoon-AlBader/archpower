# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-oslo-db
pkgver=8.1.0
pkgrel=1
pkgdesc="OpenStack Database Pattern Library"
arch=('any')
url="https://pypi.python.org/pypi/oslo.db/$pkgver"
license=('Apache')
depends=('python-six' 'python-pbr' 'python-alembic' 'python-debtcollector' 'python-oslo-i18n'
         'python-oslo-config' 'python-oslo-utils' 'python-sqlalchemy' 'python-sqlalchemy-migrate'
         'python-stevedore')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-testresources' 'python-oslotest' 'python-oslo-context')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/oslo.db/archive/$pkgver.tar.gz")
sha512sums=('1a5bde4020cd374bcb122d5f2360ae9be3989b39ae8657422f603128846491698bb88afb1ea085aa35be9f2207adc7a621344f8aebfcd3086970ebb494de698d')

export PBR_VERSION=$pkgver

prepare() {
  sed -i 's/import unittest2/import unittest as unittest2/' oslo.db-$pkgver/oslo_db/tests/sqlalchemy/test_async_eventlet.py
}

build() {
  cd oslo.db-$pkgver
  python setup.py build
}

check() {
  cd oslo.db-$pkgver
  stestr run
}

package() {
  cd oslo.db-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
