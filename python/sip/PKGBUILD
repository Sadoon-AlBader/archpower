# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=sip
pkgver=6.0.0
pkgrel=2
arch=(x86_64 powerpc64le)
pkgdesc="A tool that makes it easy to create Python bindings for C and C++ libraries"
url='https://www.riverbankcomputing.com/software/sip/intro'
license=('custom:"sip"')
depends=(python-toml)
makedepends=(python-setuptools)
conflicts=(sip5)
replaces=(sip5)
source=("https://pypi.python.org/packages/source/s/sip/sip-$pkgver.tar.gz")
sha256sums=('6185160e1aa8e167b5919c4761cbbf68cc7478d34fb655b3a28b3eb9acb5943a')

build() {
  cd sip-$pkgver
  python setup.py build
}

package() {
  cd sip-$pkgver
  python setup.py install --skip-build --root="$pkgdir" --optimize=1
}
