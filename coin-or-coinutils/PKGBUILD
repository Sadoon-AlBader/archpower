# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: tikysal <tikysal@gmail.com>
# Derived from Daniel Ehlers'

pkgname=coin-or-coinutils
pkgver=2.11.2
pkgrel=1
pkgdesc="COIN-OR collection of utility classes"
arch=(x86_64 powerpc64le)
url="https://projects.coin-or.org/CoinUtils"
license=(EPL)
depends=(zlib bzip2 glpk lapack)
makedepends=(gcc-fortran doxygen)
groups=(coin-or)
source=("https://www.coin-or.org/download/source/CoinUtils/CoinUtils-$pkgver.tgz")
sha256sums=('23e955e5c02f2ccc9db9561f4f803ef76e976cf4aed54cea2973eedbffc79ad8')

build() {
  cd CoinUtils-$pkgver

  ./configure --prefix=/usr --with-blas-lib='-lblas' --with-lapack-lib='-llapack' --with-glpk-lib='-lglpk' --enable-dependency-linking \
              --build=${CHOST}
  make
}

check() {
  cd CoinUtils-$pkgver
  make test
}

package() {
  cd CoinUtils-$pkgver
  PKG_CONFIG_LIBDIR="$pkgdir"/usr/lib/pkgconfig/ \
  make DESTDIR="$pkgdir" install
}
