# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt5-webkit
_pkgver=5.212.0-alpha4
pkgver=${_pkgver/-/}
pkgrel=8
arch=(x86_64 powerpc64le powerpc)
url='https://www.qt.io'
license=(GPL3 LGPL3 FDL custom)
pkgdesc='Classes for a WebKit2 based implementation and a new QML API'
source=("https://github.com/qtwebkit/qtwebkit/releases/download/qtwebkit-$_pkgver/qtwebkit-$_pkgver.tar.xz"
         qt5-webkit-python-3.9.patch::"https://github.com/qtwebkit/qtwebkit/commit/78360c01.patch"
         qt5-webkit-bison-3.7.patch::"https://github.com/qtwebkit/qtwebkit/commit/d92b11fe.patch"
         qtwebkit-icu68.patch)
depends=(qt5-location qt5-sensors qt5-webchannel libwebp libxslt libxcomposite gst-plugins-base hyphen woff2)
makedepends=(cmake ruby gperf python qt5-doc qt5-tools)
optdepends=('gst-plugins-good: Webm codec support')
sha256sums=('9ca126da9273664dd23a3ccd0c9bebceb7bb534bddd743db31caf6a5a6d4a9e6'
            '9db5480dfbd3c440489e8e4131e3555b2af61fa4b6c19f35227588c5996be128'
            'cc5a2b762426e9cda5a3ae056bba266b5f775ee75c0590913839c255d5f10db0'
            '161f7f93c1176dc285101e80784df0470c9be94faaac1e410904f01d81b3955b')

prepare() {
  patch -d qtwebkit-$_pkgver -p1 -i ../qtwebkit-icu68.patch
  patch -d qtwebkit-$_pkgver -p1 -i ../qt5-webkit-python-3.9.patch # Fix build with python 3.9
  patch -d qtwebkit-$_pkgver -p1 -i ../qt5-webkit-bison-3.7.patch # Fix build with bison 3.7
}

build() {
  case "${CARCH}" in
    powerpc*)
      warning "Detected ${CARCH} and disabling JIT..."
      EXTRA_CMAKE_FLAGS="-DENABLE_JIT=OFF"
    ;;
  esac

  cmake -B build -S qtwebkit-$_pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS} -DNDEBUG" \
    -DPORT=Qt \
    -DENABLE_TOOLS=OFF ${EXTRA_CMAKE_FLAGS}
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}
