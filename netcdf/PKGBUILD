# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=netcdf
pkgver=4.7.1
pkgrel=1
pkgdesc="network Common Data Form interface for array-oriented data access and corresponding library"
arch=(x86_64 powerpc64le)
url="https://www.unidata.ucar.edu/software/netcdf/"
depends=(hdf5 curl)
makedepends=(cmake)
optdepends=('netcdf-fortran: fortran bindings' 'netcdf-cxx: c++ bindings')
options=(!makeflags)
license=(custom)
source=(https://github.com/Unidata/netcdf-c/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('583e6b89c57037293fc3878c9181bb89151da8c6015ecea404dd426fea219b2c')

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../${pkgname}-c-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_BUILD_TYPE=Release \
        -DENABLE_CDF5=ON \
        -DENABLE_DAP_LONG_TESTS=ON \
        -DENABLE_EXAMPLE_TESTS=ON \
        -DENABLE_EXTRA_TESTS=ON \
        -DENABLE_FAILING_TESTS=ON \
        -DENABLE_FILTER_TESTING=ON \
        -DENABLE_LARGE_FILE_TESTS=ON
    make
}

check() {
    cd build
    make test
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install

    cd "${srcdir}"/${pkgname}-c-${pkgver}
    install -Dm644 COPYRIGHT -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
