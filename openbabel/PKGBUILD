# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgbase=openbabel
pkgname=(openbabel python-openbabel)
pkgver=3.0.0
pkgrel=1
pkgdesc="A library designed to interconvert between many file formats used in molecular modeling and computational chemistry"
arch=(x86_64 powerpc64le)
url="https://openbabel.org/wiki/Main_Page"
license=('GPL')
makedepends=('cmake' 'eigen' 'wxgtk' 'boost' 'python' 'swig')
source=("https://github.com/openbabel/openbabel/releases/download/openbabel-${pkgver//./-}/openbabel-$pkgver-source.tar.bz2")
sha256sums=('aad58ef8deaea9f58faeecb333f87bb18e0bdf4854e3a3b188a814a8c4517259')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../openbabel-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DwxWidgets_CONFIG_EXECUTABLE=/usr/bin/wx-config \
    -DRUN_SWIG=ON \
    -DPYTHON_BINDINGS=ON
  make

  # To split python bindings
  sed -i '/scripts.cmake_install.cmake/d' cmake_install.cmake
}

package_openbabel() {
  depends=('libxml2' 'libsm')
  optdepends=('wxgtk: GUI interface')

  cd build
  make DESTDIR="$pkgdir" install
}

package_python-openbabel() {
  depends=('python' 'openbabel')

  cd build/scripts
  make DESTDIR="$pkgdir" install
}
