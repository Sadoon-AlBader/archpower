# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Mihai Militaru <mihai militaru at xmpp dot ro>
# Contributor: carstene1ns <arch carsten-teibes.de>

pkgname=mbedtls
pkgver=2.16.3
pkgrel=1
pkgdesc="Portable cryptographic and SSL/TLS library, aka polarssl"
arch=(x86_64 powerpc64le)
url="https://tls.mbed.org"
license=('Apache')
provides=('polarssl')
replaces=('polarssl')
conflicts=('polarssl')
source=("https://tls.mbed.org/download/mbedtls-$pkgver-gpl.tgz")
#source=("https://tls.mbed.org/download/mbedtls-$pkgver-apache.tgz")
sha1sums=('03220368f76e358986cc00a842ccc824f8407f83')
depends=('glibc')
checkdepends=('python')
options=('staticlibs')

# http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86/net-libs/polarssl/files/polarssl-1.3.8-ssl_pthread_server.patch
# https://github.com/alucryd/aur-alucryd/blob/master/personal/polarssl/PKGBUILD
# https://aur.archlinux.org/packages/po/polarssl-git/PKGBUILD

prepare() {
  cd "$pkgname-$pkgver"
  # enable flags for non-embedded systems
  sed -i 's|//\(#define MBEDTLS_THREADING_C\)|\1|' include/mbedtls/config.h
  sed -i 's|//\(#define MBEDTLS_THREADING_PTHREAD\)|\1|' include/mbedtls/config.h

  # FS#49914 (2.3.0-2)
  sed -i 's|<time.h>|"platform.h"|' include/mbedtls/ssl.h
}

build() {
  cd "$pkgname-$pkgver"
  LDFLAGS+=" -I../include " make SHARED=1 no_test
}

check() {
  cd "$pkgname-$pkgver"
  make SHARED=1 PYTHON=python check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/usr" install
}
