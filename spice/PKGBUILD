# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Patryk Kowalczyk < patryk at kowalczyk dot ws>

pkgname=spice
pkgver=0.14.1
pkgrel=1
pkgdesc="SPICE server"
arch=(x86_64 powerpc64le)
url="https://www.spice-space.org"
license=('LGPL2.1')
depends=(celt0.5.1 libjpeg-turbo libsasl pixman glib2 opus lz4 gstreamer)
makedepends=(python2-pyparsing python2-six qemu spice-protocol git libcacard)
source=(https://www.spice-space.org/download/releases/spice-server/spice-$pkgver.tar.bz2
        https://www.spice-space.org/download/releases/spice-server/spice-$pkgver.tar.bz2.sign)
sha256sums=('1ead5de63d06eededed4017db37240f07bef0abffbaf621899647e7e685a1519'
            'SKIP')
validpgpkeys=(94A9F75661F77A6168649B23A9D8C21429AC6C82) # Christophe Fergeau (teuf) <christophe@fergeau.eu>

build() {
  cd spice-$pkgver
  PYTHON=python2 ./configure --prefix=/usr --disable-static --enable-smartcard --enable-client --disable-werror --enable-celt051
  make
}

check() {
  cd spice-$pkgver
  make check
}

package() {
  cd spice-$pkgver
  make DESTDIR="$pkgdir/" install
}
