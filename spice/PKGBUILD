# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Patryk Kowalczyk < patryk at kowalczyk dot ws>

pkgname=spice
pkgver=0.14.0
pkgrel=3
pkgdesc="SPICE server"
arch=('x86_64')
url="https://www.spice-space.org"
license=('LGPL2.1')
depends=(celt0.5.1 libjpeg-turbo libsasl pixman glib2 opus lz4)
makedepends=(python2-pyparsing python2-six qemu spice-protocol git libcacard)
source=(https://www.spice-space.org/download/releases/spice-$pkgver.tar.bz2
        https://www.spice-space.org/download/releases/spice-$pkgver.tar.bz2.sign
        CVE-2019-3813.patch)
sha256sums=('3adb9495b51650e5eab53c74dd6a74919af4b339ff21721d9ab2a45b2e3bb848'
            'SKIP'
            '35c4f83f0b5933be2589327bfe203085289180217514d61dba2977b0ec6a6d39')
validpgpkeys=(94A9F75661F77A6168649B23A9D8C21429AC6C82) # Christophe Fergeau (teuf) <christophe@fergeau.eu>

prepare() {
  cd spice-$pkgver
  # based on upstream change a4a16ac42d2f19a17e36556546aa94d5cd83745f
  patch -p1 < ../CVE-2019-3813.patch
}

build() {
  cd spice-$pkgver
  PYTHON=python2 ./configure --prefix=/usr --disable-static --enable-smartcard --enable-client --disable-werror
  make
}

check() {
  cd spice-$pkgver
  make check
}

package() {
  cd spice-$pkgver
  make DESTDIR="$pkgdir/" install
}
