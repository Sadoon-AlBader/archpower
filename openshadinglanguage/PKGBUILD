# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
pkgname=openshadinglanguage
pkgver=1.11.4.1
_tag=${pkgver}-dev
pkgrel=1
pkgdesc="Advanced shading language for production GI renderers"
arch=(x86_64 powerpc64le)
url="https://github.com/imageworks/OpenShadingLanguage"
license=('custom')
depends=('boost-libs' 'openimageio' 'openexr' 'intel-tbb' 'freetype2' 'libpng'
         'libtiff' 'zlib' 'ncurses')
makedepends=('boost' 'cmake' 'python' 'llvm' 'clang')
source=("git+https://github.com/imageworks/OpenShadingLanguage.git#tag=Release-${_tag}")
sha512sums=('SKIP')

prepare() {
  mkdir -p OpenShadingLanguage/src/build
}

build() {
  cd OpenShadingLanguage/src/build
  
  cmake ../.. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DLLVM_STATIC=0 \
    -DENABLERTTI=ON \
    -DSTOP_ON_WARNING=OFF \
    -DCMAKE_CXX_STANDARD=14
  make
}

package() {
  cd OpenShadingLanguage/src/build

  make DESTDIR="$pkgdir/" install

  install -Dm644 "$pkgdir"/usr/share/doc/OSL/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
