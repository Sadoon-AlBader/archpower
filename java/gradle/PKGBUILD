# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Simon Legner <Simon.Legner@gmail.com>
# Contributor: Chris Molozian (novabyte) <chris DOT molozian AT gmail DOT com>
# Contributor: Sanjuro Makabe (itti) <vuck AT gmx DOT de>

pkgbase=gradle
pkgname=('gradle' 'gradle-doc' 'gradle-src')
pkgver=6.5.1
pkgrel=1
pkgdesc='Powerful build system for the JVM'
url='https://gradle.org/'
arch=('any')
license=('Apache')
depends=('java-environment' 'bash')
makedepends=('git' 'gradle' 'asciidoc' 'xmlto' 'kotlin' 'groovy' 'java-environment=11')
source=(https://services.gradle.org/distributions/${pkgbase}-${pkgver}-src.zip
        ${pkgbase}.sh
        gradle-ppc64.patch)
sha256sums=('02eca24ccdc272978569bdd0f7dc039bd1badbfed0efc828994c2d7e09a3a767'
            '6f3472486278252417af49196847ba465b56819d286658fcdf918687f89ee032'
            '6484ef499a8857750ea7cde95e9c97c1694161de3057a3dfeadc5245b1a3caf6')
sha512sums=('e3b30837891fa18e430fd252eff1606c154b97c8dbe296c7ecacb977c9af6f1d1b3578a602d949407c482d2600a836aada6f340c79547edce7f78a354621a2e5'
            'a50b6cf8281b56b80f55a20ac9316e1eed6887da1d191ad575dec140c9819711644d7077c4dc693b8cb0f1b08ceba0033ba88b5ad138d33ffb73b786c0d4bf81'
            '0de3cecd3482588a195029a4ec46050f6b0a947e9e9f07cc63362d48937b0f1edb448b3a5b6d1649dec9f49aacb698f8d9f99cd4eefa5ad7944533699cec4fdb')

prepare() {
  cd ${pkgbase}-${pkgver}
  patch -Np0 -i ${srcdir}/gradle-ppc64.patch
}

build() {
  cd ${pkgbase}-${pkgver}
  # requires java language level 6, which >=13 has dropped
  export PATH="/usr/lib/jvm/java-11-openjdk/bin:${PATH}"
  ./gradlew installAll \
    -PfinalRelease=true \
    -Pgradle_installPath="$(pwd)/dist"
}

package_gradle() {
  cd ${pkgbase}-${pkgver}/dist

  # install profile.d script
  install -Dm 755 "${srcdir}/${pkgbase}.sh" "${pkgdir}/etc/profile.d/${pkgbase}.sh"

  # create the necessary directory structure
  install -d "${pkgdir}/usr/share/java/${pkgname}/bin"
  install -d "${pkgdir}/usr/share/java/${pkgname}/lib/plugins"
  install -d "${pkgdir}/usr/share/java/${pkgname}/init.d"
  install -d "${pkgdir}/usr/bin"

  # copy across jar files
  install -Dm 644 lib/*.jar "${pkgdir}/usr/share/java/${pkgname}/lib"
  install -Dm 644 lib/plugins/*.jar "${pkgdir}/usr/share/java/${pkgname}/lib/plugins"

  # copy across supporting text documentation and scripts
  install -m 644 NOTICE "${pkgdir}/usr/share/java/${pkgname}"
  install -m 755 bin/gradle "${pkgdir}/usr/share/java/${pkgname}/bin"
  install -m 644 init.d/*.* "${pkgdir}/usr/share/java/${pkgname}/init.d"

  # link gradle script to /usr/bin
  ln -s /usr/share/java/${pkgname}/bin/${pkgname} "${pkgdir}/usr/bin"
}

package_gradle-doc() {
  pkgdesc='Powerful build system for the JVM (documentation)'
  options=('!strip')
  optdepends=('gradle')

  cd ${pkgbase}-${pkgver}/dist

  # copy across documentation
  install -d "${pkgdir}/usr/share/java/gradle/docs"
  cp -r docs/* "${pkgdir}/usr/share/java/gradle/docs"
}

package_gradle-src() {
  pkgdesc='Powerful build system for the JVM (sources)'
  options=('!strip')
  optdepends=('gradle')

  cd ${pkgbase}-${pkgver}/dist
  install -d "${pkgdir}/usr/share/java/gradle/src"
  cp -r src/* "${pkgdir}/usr/share/java/gradle/src"
}

# vim: ts=2 sw=2 et:
