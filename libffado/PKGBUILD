# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: galiyosha@gmail.com
# Contributor: Jon Kristian Nilsen <jokr.nilsen@gmail.com>

pkgname=libffado
pkgver=2.4.1
pkgrel=3
pkgdesc="Driver for FireWire audio devices"
arch=('x86_64' 'powerpc64le')
url="http://www.ffado.org/"
license=('GPL')
depends=('dbus-c++' 'jack' 'libavc1394' 'libconfig' 'libiec61883' 'libxml++' 'python-dbus')
makedepends=('python-dbus' 'scons' 'python-pyqt5')
optdepends=('python-pyqt5: ffado-mixer')
provides=('ffado')
source=("http://www.ffado.org/files/${pkgname}-${pkgver}.tgz"
        "${pkgname}-fix-python3-pyqt5.diff"
        "${pkgname}-simple-power-detect.patch"
)
sha512sums=('0859f17b87c68108bbde726d1cf8ab0b99f6f7f02cbe36acd8b451e112688a4d1edc21134eb4a2edc8c92dcf5f5da52efeabb2ad0dc07804ed1aa664a3b38d74'
            '39beb2399377d7ee13a43ddd7a80907eb94a2f8f146c5a94a4ba0e1471b3369d2b1519cb8b15c3e2352d9854048882cc037058a1992e6e87270fe9e17fdba91e'
            '97df392401f32e346fbe137f14a884abd9ec16b22930d8fff8c76eeda93f3f3ae404ec459d71a02e621937e619dcec377f5e54e8520140584686bec68a9c6c22')

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/${pkgname}-fix-python3-pyqt5.diff"

  # check if we're running in user-land emulation and if so
  # patch SConstruct to properly detect the build type
  UNAMEM=$(uname -m)
  LSPCU=$(lscpu | grep 'Model name' | grep POWER || true)
  if [ "${UNAMEM}" = "ppc64le" -a -z "${LSCPU}" ]; then
    warning "Detected powerpc64le userland emulation, patching SConstruct for POWER"
    patch -Np1 -i "${srcdir}/${pkgname}-simple-power-detect.patch"
  fi
}

build() {
  cd "${pkgname}-${pkgver}"
  scons PREFIX=/usr \
        DEBUG=no \
        MANDIR='/usr/share/man' \
        PYTHON_INTERPRETER='/usr/bin/python3' \
        PYPKGDIR='/usr/lib/python3.7/site-packages' \
        UDEVDIR='/usr/lib/udev/rules.d'
}

package() {
  cd "${pkgname}-${pkgver}"
  scons DESTDIR="${pkgdir}" WILL_DEAL_WITH_XDG_MYSELF="True" install

  # XDG
  install -vDm644 support/xdg/ffado.org-ffadomixer.desktop \
    "${pkgdir}/usr/share/applications/ffadomixer.desktop"
  install -vDm644 support/xdg/hi64-apps-ffado.png \
    "${pkgdir}/usr/share/pixmaps/hi64-apps-ffado.png"

  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" -vDm644 {AUTHORS,README}
}

# vim:set ts=2 sw=2 et:
