# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: spider-mario <spidermario@free.fr>
# Contributor: Daniel Ehlers <danielehlers@mindeye.net>

pkgname=coin-or-cbc
pkgver=2.10.3
pkgrel=1
pkgdesc="COIN-OR branch-and-cut mixed integer programming solver"
arch=(x86_64 powerpc64le)
url="https://projects.coin-or.org/Cbc"
license=(EPL)
groups=(coin-or)
depends=(coin-or-cgl)
source=("https://www.coin-or.org/download/source/Cbc/Cbc-${pkgver}.tgz")
sha256sums=('ad388357129497c1cc3be50c3707b1995fddf0a4188abc8e3669173f0179ecff')

build() {
  cd Cbc-$pkgver/Cbc
  COIN_SKIP_PROJECTS="Sample" \
  ./configure --prefix=/usr --build=${CHOST} \
              --with-osi-lib="$(pkg-config --libs osi)" \
              --with-osi-incdir="/usr/include/coin/" \
              --with-clp-lib="$(pkg-config --libs clp)" \
              --with-clp-incdir="/usr/include/coin/" \
              --with-cgl-lib="$(pkg-config --libs cgl)" \
              --with-cgl-incdir="/usr/include/coin/" \
              --with-coinutils-lib="$(pkg-config --libs coinutils)" \
              --with-coinutils-incdir="/usr/include/coin/" \
              --enable-dependency-linking \
              --enable-cbc-parallel
  make
}

check() {
  cd Cbc-$pkgver/Cbc
#  make test
}

package() {
  cd Cbc-$pkgver/Cbc
  PKG_CONFIG_LIBDIR="$pkgdir"/usr/lib/pkgconfig/ \
  make DESTDIR="$pkgdir" install
}
