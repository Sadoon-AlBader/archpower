# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Juergen Hoetzel <juergen@archlinux.org>
pkgname=clisp
pkgver=2.49.4
pkgrel=1
pkgdesc="ANSI Common Lisp interpreter, compiler and debugger"
arch=(x86_64 powerpc64le)
license=('GPL')
url="https://clisp.sourceforge.io/"
depends=('readline' 'libsigsegv')
provides=('common-lisp')
makedepends=('ffcall')
options=('!makeflags' '!emptydirs')
_commit=df3b9f6fdcff22832898e89a989eb499c0f842ed
source=(git+https://gitlab.com/gnu-clisp/clisp.git#commit=$_commit)
md5sums=('SKIP')

build() {
  cd $pkgname
  ./configure --prefix=/usr --with-readline --with-ffcall src

  cd src
  ./makemake --prefix=/usr --with-readline --with-ffcall --with-dynamic-ffi > Makefile
  make
}

check() {
  cd $pkgname
  make check || :
}

package() {
  cd $pkgname/src
  make DESTDIR=$pkgdir install
}
