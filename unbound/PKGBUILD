# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Hisato Tatekura <hisato_tatekura@excentrics.net>
# Contributor: Massimiliano Torromeo <massimiliano DOT torromeo AT google mail service>

pkgname=unbound
pkgver=1.9.3
pkgrel=3
pkgdesc='Validating, recursive, and caching DNS resolver'
url='https://unbound.net/'
license=('custom:BSD')
arch=(x86_64 powerpc64le)
makedepends=('expat')
optdepends=('expat: unbound-anchor')
depends=('fstrm' 'openssl' 'libsodium' 'protobuf-c' 'libevent' 'ldns' 'dnssec-anchors')
backup=('etc/unbound/unbound.conf')
validpgpkeys=('EDFAA3F2CA4E6EB05681AF8E9F6F1C2D7E045F8D')
source=("https://unbound.net/downloads/${pkgname}-${pkgver}.tar.gz"{,.asc}
        'https://github.com/NLnetLabs/unbound/commit/ff8fd0be5c529e7a1b84e8c74426e9c531c0a8f8.patch'
        'sysusers.d'
        'tmpfiles.d'
        'hook'
        'conf')
sha256sums=('1b55dd9170e4bfb327fb644de7bbf7f0541701149dff3adf1b63ffa785f16dfa'
            'SKIP'
            'e44b1e87940f5a6c08c74cfd2c9b8556d211070d6170ea1cd4b5efc650516d33'
            '85b8f00881fb93bcce83bdfe3246463a396eb5b352c0d7f5fca32fcb839d22fa'
            '31a573f43287dd7e3678be1680388bfc7d8dee8280eb2443f521a4b349aaa6b6'
            '2746aede36b1f57efdcc370b7643ce31ff9e6acb9a1f62705987b07eaed866a3'
            'ddc0ead682c5035cdd3a29d4ab12936a73573c82c27f14026998a378de56f418')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -p1 -i ../ff8fd0be5c529e7a1b84e8c74426e9c531c0a8f8.patch
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--sbindir=/usr/bin \
		--disable-rpath \
		--enable-dnscrypt \
		--enable-dnstap \
		--enable-pie \
		--enable-relro-now \
		--enable-subnet \
		--enable-systemd \
		--enable-tfo-client \
		--enable-tfo-server \
		--with-conf-file=/etc/unbound/unbound.conf \
		--with-pidfile=/run/unbound.pid \
		--with-rootkey-file=/etc/trusted-key.key \
		--with-libevent \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -Dm644 contrib/unbound.service "${pkgdir}/usr/lib/systemd/system/unbound.service"
	install -Dm644 doc/example.conf.in "${pkgdir}/etc/unbound/unbound.conf.example"
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 ../sysusers.d "${pkgdir}/usr/lib/sysusers.d/unbound.conf"
	install -Dm644 ../conf "${pkgdir}/etc/unbound/unbound.conf"

	# Trust anchor file available from within unbound's chroot.
	install -Dm644 ../tmpfiles.d "${pkgdir}/usr/lib/tmpfiles.d/unbound.conf"
	install -Dm644 ../hook "${pkgdir}/usr/share/libalpm/hooks/unbound-key.hook"
}
