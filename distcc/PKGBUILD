# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Judd Vinet <jvinet@zeroflux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=distcc
pkgver=3.3.3
pkgrel=1
pkgdesc='Distributed compilation of C, C++ and Objective-C'
arch=(x86_64 powerpc64le)
url='http://distcc.org'
license=(GPL)
depends=(avahi popt)
makedepends=(git gtk2 python setconf)
optdepends=('gtk2: for distccmon-gnome' python)
backup=('etc/conf.d/distccd'
        'etc/distcc/hosts')
source=("git+https://github.com/distcc/distcc#tag=v$pkgver"
        'distccd.conf.d'
        'distccd.service')
sha256sums=('SKIP'
            '43e02b461841ca2976816c244a0eca8b24820ca143f73cc0924403d75a8c012f'
            '540f0b866bb168535858e86b3f8c07531f4c1695cd70891ff572885e5f660ead')
prepare() {
  cd "$pkgname"

  ./autogen.sh
  setconf gnome/distccmon-gnome.desktop Name 'DistCC Monitor'
  sed -i 's/ install-gnome-data//g' Makefile.in
}

build() {
  cd "$pkgname"

  ./configure \
    --prefix=/usr \
    --enable-rfc2553 \
    --mandir=/usr/share/man \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --with-gtk
  make WERROR_CFLAGS= INCLUDESERVER_PYTHON=/usr/bin/python
}

package() {
  make -C "$pkgname" \
    DESTDIR="$pkgdir" \
    INCLUDESERVER_PYTHON=/usr/bin/python \
    install

  # Configuration
  install -Dm644 distccd.conf.d "$pkgdir/etc/conf.d/distccd"
  install -Dm644 distccd.service \
    "$pkgdir/usr/lib/systemd/system/distccd.service"

  # Desktop shortcut
  install -Dm644 "$pkgname/gnome/distccmon-gnome-icon.png" \
    "$pkgdir/usr/share/pixmaps/distccmon-gnome-icon.png"
  install -Dm644 "$pkgname/gnome/distccmon-gnome.desktop" \
    "$pkgdir/usr/share/applications/distccmon-gnome.desktop"

  # Symlinks
  install -d "$pkgdir/usr/lib/$pkgname/bin"
  for bin in c++ cc cpp g++ gcc ${CHOST}-c++ ${CHOST}-cc ${CHOST}-cpp ${CHOST}-g++ ${CHOST}-gcc; do
    ln -sf "/usr/bin/$pkgname" "$pkgdir/usr/lib/$pkgname/bin/$bin"
    # Additional symlinks are needed, see FS#57978
    ln -sf "/usr/bin/$pkgname" "$pkgdir/usr/lib/$pkgname/$bin"
  done
}

# getver: distcc.org
# vim: ts=2 sw=2 et:
