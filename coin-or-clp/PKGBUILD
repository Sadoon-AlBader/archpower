# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: spider-mario <spidermario@free.fr>
# Contributor: Daniel Ehlers <danielehlers@mindeye.net>

pkgname=coin-or-clp
pkgver=1.17.3
pkgrel=1
pkgdesc="COIN-OR linear programming solver"
arch=(x86_64 powerpc64le)
url="https://projects.coin-or.org/Clp"
license=(EPL)
groups=(coin-or)
depends=(coin-or-osi suitesparse)
source=("https://www.coin-or.org/download/source/Clp/Clp-${pkgver}.tgz"
        nuke-avx.patch)
sha256sums=('a13bf54291ad503cf76f5f93f2643d2add4faa5d0e60ff2db902ef715c094573'
            '324d559716917d055bf1448995e172a8717ed4e512639cbff1eab41f7b7311e4')

prepare() {
  cd Clp-$pkgver
  patch -Np1 -i ${srcdir}/nuke-avx.patch
}

build() {
  cd Clp-$pkgver/Clp
  COIN_SKIP_PROJECTS="Sample" AVX2=0 \
  ./configure --prefix=/usr --build=${CHOST} \
              --with-osi-lib="$(pkg-config --libs osi)" \
              --with-osi-incdir="/usr/include/coin/" \
              --with-coinutils-lib="$(pkg-config --libs coinutils)" \
              --with-coinutils-incdir="/usr/include/coin/" \
              --with-cholmod-lib='-lcholmod' --with-cholmod-incdir=/usr/include
  make
}

check() {
  cd Clp-$pkgver/Clp
# make test
}

package() {
  cd Clp-$pkgver/Clp
  PKG_CONFIG_LIBDIR="$pkgdir"/usr/lib/pkgconfig/ \
  make DESTDIR="$pkgdir" install
}
