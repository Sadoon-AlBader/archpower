# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: spider-mario <spidermario@free.fr>
# Contributor: Daniel Ehlers <danielehlers@mindeye.net>

pkgname=coin-or-clp
pkgver=1.17.5
pkgrel=1
pkgdesc="COIN-OR linear programming solver"
arch=(x86_64 powerpc64le)
url="https://projects.coin-or.org/Clp"
license=(EPL)
groups=(coin-or)
depends=(coin-or-osi suitesparse)
source=("https://www.coin-or.org/download/source/Clp/Clp-${pkgver}.tgz")
sha256sums=('49a9f917002b8db9df4df549119b0d44ec5a2f321b08f610dcc53be72879bc78')

build() {
  cd Clp-$pkgver/Clp
  COIN_SKIP_PROJECTS="Sample" \
  ./configure --prefix=/usr \
              --with-osi-lib="$(pkg-config --libs osi)" \
              --with-osi-incdir="/usr/include/coin/" \
              --with-coinutils-lib="$(pkg-config --libs coinutils)" \
              --with-coinutils-incdir="/usr/include/coin/" \
              --with-cholmod-lib='-lcholmod' --with-cholmod-incdir=/usr/include \
	      --build=${CHOST}
  make
}

check() {
  cd Clp-$pkgver/Clp
# make test
}

package() {
  cd Clp-$pkgver/Clp
  PKG_CONFIG_LIBDIR="$pkgdir"/usr/lib/pkgconfig/ \
  make DESTDIR="$pkgdir" install
}
