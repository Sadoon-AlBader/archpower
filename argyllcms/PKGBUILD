# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Frank Ickstadt (frank dot ickstadt at gmail dot com)

pkgname=argyllcms
_pkgname=Argyll
pkgver=2.1.2
_pkgver="V${pkgver}"
pkgrel=1
pkgdesc="An ICC compatible color management system with support for different colorimeter hardware"
arch=(x86_64 powerpc64le)
url="https://www.argyllcms.com/"
license=(GPL AGPL)
depends=(libpng libtiff libxss libxinerama libxxf86vm libxrandr zlib openssl)
makedepends=(ftjam zip unzip)
source=("https://www.argyllcms.com/${_pkgname}_${_pkgver}_src.zip")
sha256sums=('be378ca836b17b8684db05e9feaab138d711835ef00a04a76ac0ceacd386a3e3')

prepare() {
    cd ${_pkgname}_${_pkgver}
    # Use hwdb builtin, instead of the obsolete usb-db in the udev rules.
    sed -i 's:^TEST=="/lib/udev/usb-db", IMPORT{program}="usb-db %p":IMPORT{builtin}="hwdb --subsystem=usb":' usb/55-Argyll.rules

    # Use LDFLAGS
    echo "LINKFLAGS += ${LDFLAGS} ;" >> Jamtop
}

build() {
    cd ${_pkgname}_${_pkgver}
    CCOPTFLAG="$CFLAGS"
    jam -q -fJambase ${MAKEFLAGS} -sPREFIX=/usr -sDESTDIR="${pkgdir}" -sREFSUBDIR=share/argyllcms/ref all
}

package() {
    cd ${_pkgname}_${_pkgver}
    jam -q -fJambase ${MAKEFLAGS} -sPREFIX=/usr -sDESTDIR="${pkgdir}" -sREFSUBDIR=share/argyllcms/ref install

    rm "${pkgdir}"/usr/bin/License.txt

    rm {doc,ref}/afiles
    install -Dm644 doc/*.* -t "${pkgdir}"/usr/share/${pkgname}/doc/
    install -Dm644 doc/ccmxs/*.ccmx -t "${pkgdir}"/usr/share/${pkgname}/doc/ccmxs/

    install -Dm644 usb/55-Argyll.rules -t "${pkgdir}"/usr/lib/udev/rules.d/
}
