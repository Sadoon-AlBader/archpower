# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Isenmann <daniel@archlinux.org>

pkgname=libgdiplus
pkgver=6.0.1
pkgrel=1
url='https://www.mono-project.com/docs/gui/libgdiplus/'
pkgdesc='Open Source Implementation of the GDI+ API'
arch=(x86_64 powerpc64le)
license=('MPL' 'LGPL')
depends=('libtiff' 'libjpeg' 'libpng' 'cairo' 'giflib' 'libgif.so' 'glib2'
         'libexif' 'zlib' 'freetype2')
makedepends=('mesa')
source=(https://github.com/mono/libgdiplus/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('169f920ec461801f7f7eb51543d679e71b2452e88932592d5aadef3a14987c23')
sha512sums=('2cd5a055ca694d1214d237aa24145a19dedc07369c6b1d07d7d90bb61a0d99a28524400f194e1bcbf0203688dae79ea31630289378854e31689a439a464232df')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --with-cairo=system
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 NEWS README.md TODO AUTHORS ChangeLog -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
