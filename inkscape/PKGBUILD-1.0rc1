# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>
# Contributor: tobias <tobias@archlinux.org>

pkgname=inkscape
pkgver=1.0rc1
_magic=18046
_fullver=inkscape-1.0rc1_2020-04-09_09960d6f05
pkgrel=1
pkgdesc='Professional vector graphics editor'
url='https://inkscape.org/'
license=('GPL' 'LGPL')
arch=('x86_64')
makedepends=('cmake' 'boost')

depends=(
	'dbus-glib'
	'double-conversion'
	'gc'
	'gdl'
	'gsl'
	'gtkmm3'
	'gtkspell3'
	'libcdr'
	'libjpeg-turbo'
	'libmagick6'
	'libvisio'
	'libxslt'
	'poppler-glib'
	'potrace'
	'python'
	'ttf-dejavu'
)

optdepends=(
	'gvfs: import clip art'
	'pstoedit: latex formulas'
	'python-lxml: some extensions'
	'python-numpy: some extensions'
	'scour: optimized SVG output, some extensions'
	'texlive-core: latex formulas'
)

#source=("https://media.inkscape.org/dl/resources/file/${pkgname}-${pkgver}.tar.bz2")
source=("https://inkscape.org/gallery/item/${_magic}/${_fullver}.tar.xz")
sha256sums=('e0ddad9c7281744318f4ff02f98c62b400a6a96dad17885bd211dec1ca732432')

build() {
	mkdir -p build
	export PKG_CONFIG_PATH='/usr/lib/imagemagick6/pkgconfig'
	cmake -H${_fullver} -Bbuild \
		-DCMAKE_BUILD_TYPE=None \
		-DCMAKE_INSTALL_PREFIX:PATH=/usr \
		-DSHARE_INSTALL_PREFIX:PATH=/usr/share \
		-DINCLUDE_INSTALL_DIR:PATH=/usr/include \
		-DSYSCONF_INSTALL_DIR:PATH=/etc \
		-DLIB_INSTALL_DIR:PATH=/usr/lib \
		-DWITH_DBUS=ON \

	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --build build --target install
}
