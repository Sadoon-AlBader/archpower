# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Firmicus <firmicus āt gmx dōt net>
# Maintainer: Rémy Oudompheng <remy@archlinux.org>

pkgname=texlive-langgreek
pkgver=2019.51628
_revnr=${pkgver#2019.}
pkgrel=1
pkgdesc="TeX Live - Fonts and support for typesetting Greek"
license=('GPL')
arch=(any)
depends=('texlive-core')
groups=('texlive-lang')
url='https://tug.org/texlive/'
source=("https://sources.archlinux.org/other/texlive/$pkgname-$pkgver-src.zip" "$pkgname.maps")
options=('!emptydirs' '!strip')
sha256sums=('7fb23275664a04f98e734f6cbe8a043748b500bd10e81c8c04619e69ad9e3ac3'
            '1d808532a77ad46260660e87b74de23403ffdd51ca76d14a406d5719e4e02f26')

build() {
   cd "$srcdir"
   for p in *.tar.xz; do
	   bsdtar -xf $p
   done
   rm -rf {tlpkg,doc,source} || true
}

package() {
   install -m755 -d $pkgdir/var/lib/texmf/arch/installedpkgs
   sed -i '/^#/d' CONTENTS
   install -m644 CONTENTS $pkgdir/var/lib/texmf/arch/installedpkgs/${pkgname}_${_revnr}.pkgs
   install -m644 $pkgname.maps $pkgdir/var/lib/texmf/arch/installedpkgs/
   install -m755 -d $pkgdir/usr/share
   wanteddirs=$(for d in *; do test -d $d && [[ $d != texmf* ]] && echo $d; done) || true
   for dir in $wanteddirs; do
     find $dir -type d -exec install -d -m755 $pkgdir/usr/share/texmf-dist/'{}' \;
     find $dir -type f -exec install -m644 '{}' $pkgdir/usr/share/texmf-dist/'{}' \;
   done
   if [[ -d texmf-dist ]]; then
     find texmf-dist -type d -exec install -d -m755 $pkgdir/usr/share/'{}' \;
     find texmf-dist -type f -exec install -m644 '{}' $pkgdir/usr/share/'{}' \;
   fi
   if [[ -d $pkgdir/usr/share/texmf-dist/scripts ]]; then
     find $pkgdir/usr/share/texmf-dist/scripts -type f -exec chmod a+x '{}' \;
   fi
   install -m755 -d $pkgdir/usr/bin
   ln -s /usr/share/texmf-dist/scripts/mkgrkindex/mkgrkindex $pkgdir/usr/bin/mkgrkindex
}
