# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=maeparser
pkgver=1.2.2
pkgrel=3
pkgdesc="Maestro file parser"
arch=(x86_64 powerpc64le)
url="https://github.com/schrodinger/maeparser"
license=(MIT)
depends=(boost-libs)
makedepends=(cmake boost)
source=($pkgname-$pkgver.tar.gz::"https://github.com/schrodinger/maeparser/archive/v$pkgver.tar.gz"
        maeparser-missing-header.patch::"https://github.com/schrodinger/maeparser/commit/a6539942.patch")
sha256sums=('e65dec4dd8b2c633d9c43711eccd6dfa3c4df6383ec32aeffd21a96d465f5fe7'
            '6c676399dd0dfd00699761065788cd9e1f853e965812fbf85e2c5fd967748e8a')

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../maeparser-missing-header.patch # Fix missing header
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/$pkgname-$pkgver/LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname
}
