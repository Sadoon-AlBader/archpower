# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=camlp4
pkgver=4.08
pkgrel=3
pkgdesc='Caml preprocessor and pretty-printer'
arch=(x86_64 powerpc64le)
license=('GPL2')
url='https://github.com/ocaml/camlp4/releases'
depends=('ocaml>=4.09.0')
makedepends=('git' 'ocamlbuild' 'ocaml-compiler-libs')
options=('!makeflags' '!emptydirs' 'staticlibs')
source=("https://github.com/ocaml/camlp4/archive/4.08+1.tar.gz" "configure.patch")
sha256sums=('655cd3bdcafbf8435877f60f4b47dd2eb69feef5afd8881291ef01ba12bd9d88'
            '4043b51c0a7d88133d7272fb78ec6cc25f48c59e7ab6e287855721e7279c41c9')

prepare() {
  cd "${srcdir}/$pkgname-${pkgver}-1"
  patch -i  "${srcdir}/configure.patch" -p0
}

build() {
  cd "${srcdir}/$pkgname-${pkgver}-1"
  ./configure
  make all camlp4/META
}

package() {
  cd "${srcdir}/$pkgname-${pkgver}-1"
  make \
    BINDIR="$pkgdir/usr/bin" \
    LIBDIR="$pkgdir/usr/lib/ocaml" \
    PKGDIR="$pkgdir/usr/lib/ocaml" \
    install install-META
}

# vim:set ts=2 sw=2 et:
