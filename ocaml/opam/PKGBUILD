# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Vincent B. <vb@luminar.eu.org>

pkgname=opam
pkgver=2.1.0beta2
pkgrel=1
pkgdesc='OCaml package manager'
arch=(x86_64 powerpc64le powerpc)
url='https://opam.ocaml.org/'
license=(GPL)
depends=(bubblewrap ocaml-compiler-libs unzip)
makedepends=(dune)
optdepends=('darcs: For downloading packages with darcs'
            'git: For downloading packages with git'
            'mercurial: For downloading packages with mercurial'
            'rsync: For downloading packages with rsync')
source=("https://github.com/ocaml/opam/releases/download/${pkgver/beta/-beta}/opam-full-${pkgver/beta/-beta}.tar.gz")
sha256sums=('a2ac31ee3dfad3f5c2151e0b40f81ede13fdf0bf26cf0ef702d4082ba34be37b')

build() {
  cd "opam-full-${pkgver/beta/-beta}"

  ./configure --prefix=/usr
  make lib-ext all -j1 \
    OCAMLC='ocamlc -unsafe-string' \
    OCAMLOPT='ocamlopt -unsafe-string'
}

package() {
  make -C "opam-full-${pkgver/beta/-beta}" DESTDIR="$pkgdir" install
}

# getver: github.com/ocaml/opam/releases
# vim: ts=2 sw=2 et:
