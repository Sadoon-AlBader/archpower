# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: SpepS <dreamspepser at yahoo dot it>

pkgname=openimageio
pkgver=2.0.11
pkgrel=1
pkgdesc="A library for reading and writing images, including classes, utilities, and applications"
arch=(x86_64 powerpc64le)
url="http://www.openimageio.org/"
license=('custom')
depends=('openexr' 'boost-libs' 'jasper' 'glew' 'libtiff' 'opencolorio' 'intel-tbb' 'openssl' 'libpng' 'libraw' 'libwebp')
makedepends=('cmake' 'qt5-base' 'python' 'boost' 'mesa' 'git' 'freetype2' 'fontconfig' 'libxrender')
optdepends=('qt5-base: iv image viewer'
            'python: bindings support')
source=(git+git://github.com/OpenImageIO/oiio.git#tag=Release-${pkgver})
md5sums=('SKIP')

prepare() {
  mkdir oiio/build
}

build() {
  cd oiio/build

  cmake .. \
      -DUSE_PYTHON=ON \
      -DPYTHON_VERSION=3.7 \
      -DUSE_OPENSSL=ON \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DOIIO_BUILD_TESTS=ON \
      -DOIIO_BUILD_TOOLS=ON \
      -DSTOP_ON_WARNING=OFF
  make
}

package() {
  cd oiio/build

  make DESTDIR="$pkgdir" install

  install -Dm644 ../LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE".md
}
