# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Pierre Schmitz <pierre@archlinux.de>
# Maintainer: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=archlinux-keyring
pkgver=20201028
pkgrel=2
pkgdesc='Arch Linux PGP keyring'
arch=(any)
url='https://projects.archlinux.org/archlinux-keyring.git/'
license=(GPL)
install=$pkgname.install
source=(https://sources.archlinux.org/other/$pkgname/${pkgname}-${pkgver}.tar.gz{,.sig}
	archpower.patch)
sha256sums=('d9797e64da8446c74f3c1a69a52d35ef1d0b9ec09b9930e9f7e902bb5eac5d49'
            'SKIP'
            'd6a73ed88f6b36006a23f698ae9155a537fa44fc17cb9d81caf8fe0db7e763dd')
validpgpkeys=('4AA4767BBC9C4B1D18AE28B77F2D434B9741E8AC'  # Pierre Schmitz <pierre@archlinux.de>
              'A314827C4E4250A204CE6E13284FC34C8E4B1A25'  # Thomas Bächler <thomas@bchlr.de>
              '86CFFCA918CF3AF47147588051E8B148A9999C34'  # Evangelos Foutras <evangelos@foutrelis.com>
              'F3691687D867B81B51CE07D9BBE43771487328A9'  # Bartlomiej Piotrowski <bpiotrowski@archlinux.org>
              'BD84DE71F493DF6814B0167254EDC91609BC9183'  # Christian Hesse <Christi@n-Hes.se>
              'CFA6AF15E5C74149FC1D8C086D1655C14CE1C13E'  # Florian Pritz <bluewind@xinu.at>
              'E499C79F53C96A54E572FEE1C06086337C50773E')  # Jelle van der Waa <jelle@archlinux.org>

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ${srcdir}/archpower.patch
}

package() {
  cd $pkgname-$pkgver
  make PREFIX=/usr DESTDIR="$pkgdir" install
}
