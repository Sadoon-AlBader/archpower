# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=alembic
pkgver=1.7.11
pkgrel=1
pkgdesc="A open framework for storing and sharing scene data"
arch=(x86_64 powerpc64le)
url="http://www.alembic.io/"
license=('BSD')
depends=('openexr' 'boost-libs' 'hdf5')
makedepends=('cmake' 'boost')
source=(https://github.com/alembic/alembic/archive/${pkgver}.tar.gz)
sha512sums=('94b9c218a2fe6e2e24205aff4a2f6bab784851c2aa15592fb60ea91f0e8038b0c0656a118f3a5cba0d3de8917dd90b74d0e2d1c4ac034b9ee3f5d0741d9f6b70')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  mkdir build && cd build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_HDF5=ON

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

  cd build
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
