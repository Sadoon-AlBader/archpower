# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Charles Mauch <cmauch@gmail.com>

pkgname=perl-yaml
pkgver=1.29
pkgrel=2
pkgdesc="Perl/CPAN Module YAML : YAML Aint Markup Language"
arch=('any')
url="https://search.cpan.org/dist/YAML/"
license=('GPL' 'PerlArtistic')
depends=('perl')
checkdepends=('perl-test-deep' 'perl-test-yaml')
options=('!emptydirs')
source=(https://www.cpan.org/authors/id/T/TI/TINITA/YAML-${pkgver}.tar.gz)
sha512sums=('a3bcec325360694826781f78611b7855bcf9ddd58a009d836d222d8b63f42e2c6565304df70e6b13af415e45761ac16a4cef9dcf0ed05ff9dec8cf2d94c33572')

build() {
  cd YAML-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd YAML-${pkgver}
  make test
}

package() {
  cd YAML-${pkgver}
  make DESTDIR="${pkgdir}" install
}
