# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=perl-xml-parser
pkgver=2.44
pkgrel=9
pkgdesc="Expat-based XML parser module for perl"
arch=(x86_64 powerpc64le)
license=('GPL' 'PerlArtistic')
url="https://search.cpan.org/dist/XML-Parser/"
depends=('perl' 'expat')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/T/TO/TODDR/XML-Parser-${pkgver}.tar.gz")
sha1sums=('0ab6b932713ec1f9927a1b1c619b6889a5c12849')

build() {
  cd XML-Parser-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd XML-Parser-${pkgver}
  make test
}

package() {
  cd XML-Parser-${pkgver}
  make DESTDIR="${pkgdir}" install
  # fix install weirdness on power
  cp -av ${pkgdir}${CARCH}/* ${pkgdir}/
}
