# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Andrew Simmons <andrew.simmons@gmail.com>

pkgname=perl-archive-zip
pkgver=1.66
pkgrel=1
pkgdesc="Provide a perl interface to ZIP archive files"
arch=('any')
license=('GPL' 'PerlArtistic')
url="https://search.cpan.org/dist/Archive-Zip/"
depends=('perl')
checkdepends=('perl-test-mockmodule')
options=('!emptydirs')
source=(https://search.cpan.org/CPAN/authors/id/P/PH/PHRED/Archive-Zip-${pkgver}.tar.gz)
sha512sums=('abeefcd87510c83374856c958178f94ce9c6bd5710adc4b3725d8bdcfa5a6a5441876f3bc0575eeba3eec233565276c2d314b24ce0875c0dd8a3beb98e9ac5c7')

build() {
  cd Archive-Zip-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Archive-Zip-${pkgver}
  make test
}


package() {
  cd Archive-Zip-${pkgver}
  make DESTDIR="${pkgdir}" install
}
