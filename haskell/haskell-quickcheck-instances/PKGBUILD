# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=quickcheck-instances
pkgname=haskell-quickcheck-instances
pkgver=0.3.19
pkgrel=17
pkgdesc="Common quickcheck instances"
url="https://github.com/phadej/qc-instances"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-quickcheck' 'haskell-base-compat' 'haskell-case-insensitive'
         'haskell-hashable' 'haskell-old-time' 'haskell-scientific' 'haskell-tagged'
         'haskell-transformers-compat' 'haskell-unordered-containers' 'haskell-uuid-types'
         'haskell-vector')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('8665606b4e42ae5db940db80ab278722ae3a8e7c497559db66943da556cbca3ea0106d74542bd4a51e1661206f877b20af3c4fe2de773aa60c0e30d7782bed4a')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/<.*0.10/<1/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-bytestring-builder
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
