# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=splitmix
pkgname=haskell-splitmix
pkgver=0.0.4
pkgrel=18
pkgdesc="Fast Splittable PRNG"
url="https://github.com/phadej/splitmix"
license=('BSD')
arch=(x86_64 powerpc64le)
depends=('ghc-libs' 'haskell-random')
makedepends=('ghc' 'haskell-async' 'haskell-base-compat-batteries' 'haskell-hunit'
             'haskell-tf-random' 'haskell-vector')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('9b8c137067497b94a460d024e1f7c3269e27536f505649face85748a0aaea273d32893a806d9ecf551bd29532ed820004ed2dde47e1099afc3c31519ea5cdbac')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/< *0.11/<1/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    if (( CHECKFUNC )); then
        _opts=('--enable-tests')
    else
        _opts=('--disable-tests')
    fi
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname "${_opts[@]}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
