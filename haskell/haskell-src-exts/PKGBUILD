# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=haskell-src-exts
pkgname=haskell-src-exts
pkgver=1.23.0
pkgrel=6
pkgdesc="Manipulating Haskell source: abstract syntax, lexer, parser, and pretty-printer"
url="https://github.com/haskell-suite/haskell-src-exts"
license=("custom:BSD")
arch=(x86_64 powerpc64le)
depends=('ghc-libs')
makedepends=('ghc' 'happy' 'haskell-pretty-show' 'haskell-smallcheck' 'haskell-tasty'
             'haskell-tasty-golden' 'haskell-tasty-smallcheck')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('bbcde248c265a72ea4c1a6643cc36ffee8ebb592971ebd628b2b14a6d64c3e5d91e430883245464b29b55ab6614ace00b2d5b125458f275e8791f7f2e29c9081')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    # https://github.com/haskell-suite/haskell-src-exts/issues/448
    runhaskell Setup test || :
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
