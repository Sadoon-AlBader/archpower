# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Nagy <danielnagy at gmx de>
# Contributor: Daniel Micay <danielmicay@gmail.com>

pkgname=haskell-aeson
_hkgname=aeson
pkgver=1.4.7.1
pkgrel=9
pkgdesc="A JSON parsing and encoding library optimized for ease of use and high performance."
url="https://github.com/bos/aeson"
license=("GPL")
arch=(x86_64 powerpc64le)
depends=('ghc-libs' 'haskell-attoparsec' 'haskell-base-compat-batteries' 'haskell-dlist'
         'haskell-hashable' 'haskell-primitive' 'haskell-scientific' 'haskell-tagged'
         'haskell-th-abstraction' 'haskell-time-compat' 'haskell-unordered-containers'
         'haskell-uuid-types' 'haskell-vector')
makedepends=('ghc' 'haskell-quickcheck' 'haskell-integer-logarithms'
             'haskell-base-orphans' 'haskell-base16-bytestring' 'haskell-diff'
             'haskell-generic-deriving' 'haskell-hashable-time' 'haskell-tasty'
             'haskell-tasty-golden' 'haskell-tasty-hunit' 'haskell-tasty-quickcheck'
             'haskell-quickcheck-instances')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-${pkgver}.tar.gz")
sha512sums=('236ed1b2cc4b39d49277cea5b787b6107c7efbe1c0d73de2005d400746bacf44c6b8ef5d26619395e24844f8948115685963ec7d03a998d0b728ab992e32e705')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/< *2.14/<3/' $_hkgname.cabal
}

build() {
  cd $_hkgname-$pkgver
  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-developer -f-fast -f-cffi
  runhaskell Setup build
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
