# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=conduit
pkgname=haskell-conduit
pkgver=1.3.1.1
pkgrel=6
pkgdesc="Streaming data processing library."
url="https://github.com/snoyberg/conduit"
license=("MIT")
arch=(x86_64 powerpc64le)
depends=('ghc-libs' 'haskell-exceptions' 'haskell-mono-traversable' 'haskell-primitive'
         'haskell-resourcet' 'haskell-unliftio-core' 'haskell-vector')
makedepends=('ghc' 'haskell-hspec' 'haskell-quickcheck' 'haskell-safe' 'haskell-silently'
             'haskell-split' 'haskell-unliftio')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('a41a12b480f9fa16c10318c76936cca074ddefabb4cf4e1be98f80d951e255932858e49bdd10777e2b716433e7ffe8ea7252b0e4fdcf5e8c47e895e04467b807')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
