# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=hedgehog
pkgname=haskell-hedgehog
pkgver=1.0.1
pkgrel=10
pkgdesc="A modern property-based testing system"
url="https://hedgehog.qa"
license=("BSD")
arch=(x86_64 powerpc64le)
depends=('ghc-libs' 'haskell-ansi-terminal' 'haskell-async' 'haskell-concurrent-output'
         'haskell-exceptions' 'haskell-lifted-async' 'haskell-mmorph' 'haskell-monad-control'
         'haskell-pretty-show' 'haskell-primitive' 'haskell-random' 'haskell-resourcet'
         'haskell-transformers-base' 'haskell-wl-pprint-annotated')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('093d40be85dfdd5d063d994f62c5a32169b5d3accdc6dfb85411d3671cf84ea3deafaa92cfeda58251385ca58efd91b92a0ff48c265d9dd361f8ad75e039cef0')

prepare() {
    cd $_hkgname-$pkgver
    sed -i -e 's/< *0.10/<1/' -e '/semigroups/d' -e '/fail/d' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
