# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=hedgehog
pkgname=haskell-hedgehog
pkgver=1.0
pkgrel=1
pkgdesc="A modern property-based testing system"
url="https://hedgehog.qa"
license=("BSD")
arch=(x86_64 powerpc64le)
depends=('ghc-libs' 'haskell-ansi-terminal' 'haskell-async' 'haskell-concurrent-output'
         'haskell-exceptions' 'haskell-lifted-async' 'haskell-mmorph' 'haskell-monad-control'
         'haskell-pretty-show' 'haskell-primitive' 'haskell-random' 'haskell-resourcet'
         'haskell-th-lift' 'haskell-transformers-base' 'haskell-wl-pprint-annotated'
         'haskell-monad-fail')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('93b214744b262feb436019dc14715e314e6b95b98f370f75352ba4cd3a37970ec10ff6f0b29a43c204f75e835a8bd88df3c17df8e898153cb56ebb3566c17231')

prepare() {
    cd $_hkgname-$pkgver
    sed -i -e 's/<.*1.8/<2/' -e 's/< *2.5/<3/' -e 's/< *2.14/<3/' $_hkgname.cabal
    sed -i '/semigroups/d' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
