# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Jameson Pugh <imntreal@gmail.com>
# Contributor: Tim Jester-Pfadt <t.jp<at>gmx.de>

pkgbase=appstream
pkgname=(appstream appstream-qt)
pkgver=0.14.1
pkgrel=2
pkgdesc="Provides a standard for creating app stores across distributions"
arch=(x86_64 powerpc64le powerpc)
url="https://distributions.freedesktop.org/wiki/AppStream"
license=(GPL)
depends=(libyaml libxml2 libstemmer glib2 lmdb libsoup)
makedepends=(meson xmlto gobject-introspection gtk-doc qt5-tools itstool vala gperf)
source=("https://www.freedesktop.org/software/appstream/releases/AppStream-$pkgver.tar.xz"{,.asc}
        update-appstream-cache.hook
        appstream-fix-descriptions.patch::"https://github.com/ximion/appstream/commit/1a3637a8.patch")
sha256sums=('94ac2f32d58f79b68e65f723375a68490224e0f1c5a62207a9c6b98bb9ad77d7'
            'SKIP'
            '3a96a1479cfd18dad36c2ca3181aabe46af9bf772c00b965d86ec5f55cd0e0eb'
            '24ab38a8c6adf2833dbf03982f86c27b17591a8ba02a3f916755a1f280c56aa2')
validpgpkeys=(D33A3F0CA16B0ACC51A60738494C8A5FBF4DECEB) # Matthias Klumpp <matthias@tenstral.net>

prepare() {
  patch -d AppStream-$pkgver -p1 < appstream-fix-descriptions.patch # Fix missing descriptions
}

build() {
  meson build AppStream-$pkgver \
    --prefix=/usr \
    -Dqt=true \
    -Dvapi=true \
    -Dcompose=true
  ninja -C build
}

package_appstream() {
  DESTDIR="$pkgdir" ninja install -C build

# provided by -qt subpackage
  rm -r "$pkgdir"/usr/{include/AppStreamQt,lib/cmake,lib/libAppStreamQt.*}

  install -Dm644 update-appstream-cache.hook "$pkgdir"/usr/share/libalpm/hooks/update-appstream-cache.hook
}

package_appstream-qt() {
  pkgdesc='Qt5 interface for AppStream'
  depends=(appstream qt5-base)

  DESTDIR="$pkgdir" ninja install -C build

# provided by appstream
  rm -r "$pkgdir"{/etc,/usr/{bin,include/appstream{,-compose},lib/{girepository-1.0,libappstream*,pkgconfig},share}}
}
