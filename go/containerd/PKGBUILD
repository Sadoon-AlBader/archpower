# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Santiago Torres-Arias <santiago@archlinux.org>
# Contributor: SÃ©bastien "Seblu" Luttringer
# Contributor: Iwan Timmer <irtimmer@gmail.com>

pkgname=containerd
pkgver=1.4.3
pkgrel=2
pkgdesc='An open and reliable container runtime'
url='https://containerd.io/'
depends=('runc')
makedepends=('go' 'git' 'btrfs-progs' 'libseccomp' 'containers-common')
provides=('container-runtime')
arch=(x86_64 powerpc64le)
license=("Apache")
source=("git+https://github.com/containerd/containerd.git#tag=v${pkgver}?signed"
	"${pkgname}-${pkgver}-${pkgrel}.patch::https://github.com/containerd/containerd/compare/v1.4.3...release/1.4.patch")
validpgpkeys=("8C7A111C21105794B0E8A27BF58C5D0A4405ACDB") # Derek McGowan
sha256sums=('SKIP'
            'e9493152d238e14b2d0750560bb89662f40554c02c9e9f401151db5237442048')

prepare() {
  mkdir -p src/github.com/containerd
  ln -rTsf $pkgname src/github.com/containerd/containerd

  # fix paths in service
  sed -i 's,/sbin,/usr/bin,;s,/usr/local,/usr,' $pkgname/containerd.service

  cd ${srcdir}/${pkgname}
  patch -Np1 -i ${srcdir}/${pkgname}-${pkgver}-${pkgrel}.patch
}

build() {
  export GOPATH="$srcdir"
  cd src/github.com/containerd/containerd
  export GOFLAGS="-trimpath -mod=readonly -modcacherw"
  make VERSION=v$pkgver
}

check() {
  cd src/github.com/containerd/containerd
  GOFLAGS="" make test
}

package() {
  export GOPATH="$srcdir"
  cd src/github.com/containerd/containerd
  make install DESTDIR="$pkgdir/usr"
  install -Dm644 containerd.service "$pkgdir"/usr/lib/systemd/system/containerd.service
}
