# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=containers-common
pkgver=0.30.0
_image_pkgver=5.8.1
_podman_pkgver=2.1.1
_skopeo_pkgver=1.2.0
_storage_pkgver=1.24.1
pkgrel=1
pkgdesc="Configuration files and manpages for containers"
arch=('any')
url="https://github.com/containers"
license=('Apache')
makedepends=('go-md2man')
backup=(
  'etc/containers/containers.conf'
  'etc/containers/mounts.conf'
  'etc/containers/policy.json'
  'etc/containers/registries.conf'
  'etc/containers/seccomp.json'
  'etc/containers/storage.conf'
)
# configuration override directories need to exist
options=('emptydirs')
source=("common-${pkgver}.tar.gz::https://github.com/containers/common/archive/v${pkgver}.tar.gz"
        "image-${_image_pkgver}.tar.gz::https://github.com/containers/image/archive/v${_image_pkgver}.tar.gz"
        "podman-${_podman_pkgver}.tar.gz::https://github.com/containers/podman/archive/v${_podman_pkgver}.tar.gz"
        "skopeo-${_skopeo_pkgver}.tar.gz::https://github.com/containers/skopeo/archive/v${_skopeo_pkgver}.tar.gz"
        "storage-${_storage_pkgver}.tar.gz::https://github.com/containers/storage/archive/v${_storage_pkgver}.tar.gz"
        'mounts.conf'
        "${pkgname}-0.29.0-comment_defaults.patch"
)
sha512sums=('39f15274041a7022a939dec5427c1aa717b7a4137407eaea5e19a2f6a3975166818ced0a5ec51759b57e8b4a76449aa661bf6aae55c13d8d9bb5928988c139d5'
            '5eb77cd2270eb90d77ba5057e57d704f5a4b7d8a13dc1621cc761029a55fbe32a9db2d68252ad04ca6966560d5ad2e64857cd1cf714f4808cbed5798b796e42e'
            '94096aca1816bb1726b74c71a3c3cd50c4c783110c79dd00e851e6292438ccae7b933b6028177039a7d7d96373afcb783f4bc0449a62c18ae97d73767ee3fa26'
            '630f4209eb6e45c7e99731f98e32705cc7e8f476a5b288f74d7596dd449e0e30e7cebb64c1ecd2562fb1516d91b8ce0e8a41a479a7b1e337d16f8b1fa5b29f9e'
            '2f1244a270c756f7a135e6e5cda25029d6e487fc5b6b4315c3961e2be2e6a96e4c297da101bf7386c087d67af8f8c30938881dcc15d08e8adcffd3780b161fe7'
            '11fa515bbb0686d2b49c4fd2ab35348cb19f9c6780d6eb951a33b07ed7b7c72a676627f36e8c74e1a2d15e306d4537178f0e127fd3490f6131d078e56b46d5e1'
            'e3079aff83f6f0e881d488d69eeceba248d8a6e8e682ce7ad6d2368d14088e2251c0acdf0f4d504242b9ba2d961415d0fe26e4ee22d0a205cadc1250e52f0955')
b2sums=('035e7d66bdd1c52ed1d88e6c6c6c3c17e3e23b86537fcfe2796625cc29cb62c27170195e496bd0ae3fac16379b3b63997895b40e0d0ce3942771b01e5ab77d50'
        'c9351576bfd8a3e82479b1f567685b71379be64f8475cdaafc0fb70f990ee3cf217e575e1a01e38f9d02ab23de5d5fc9224b9464b83f6dfe38972455c91af41e'
        '48e72cc436395b33bef04bf7a95106c5e13cb4fc57e1f7ee04576afc9c86b418373286f368193026206e0f74b39e3fce1b62d7bc794d6e504119db8b0726203d'
        '0c5c0b5e35e278913efef316835c105f49fc0552a1a21159591cc11ec201a306d392c2e8af81c2636b86c7cc52b60f986658516034836128f17265b0f0f2edc6'
        '7caacf5d2e0b952c27c4b82a9e824ce5f79e714ce227a375e06686c0744b63a6416b04639b1d903e89aac494c03f7490d2f91934f9bd534355175718c92a89c8'
        '2f4b0af3271103362a898e7fcc3ec05f06755902ad664ac3107bb8debb8b2ac0d50de311d5fc651279a817a56e3ff05864a7e77c0d8fc628ff7411bfb98c9b69'
        '716f66af2bc76b08ed767732618bb5e74dd8d2c518cd3d1a94f78f493df00a4a0ee1c83d54fb92756e10649a0a5c1d51eb702a43b205b20debbcd0ebebed05c3')

prepare() {
  (
    cd "storage-${_storage_pkgver}"
    sed -r 's/(GOMD2MAN = ).*/\1 go-md2man/' -i docs/Makefile
  )
  (
    cd "common-${pkgver}"
    # comment default_capabilities and default_sysctls
    # https://bugs.archlinux.org/task/68742
    # https://bugs.archlinux.org/task/68743
    patch -Np1 -i ../"${pkgname}-0.29.0-comment_defaults.patch"
  )

}

build() {
  (
    cd "common-${pkgver}/docs"
    for _man_page in *.md; do
      go-md2man -in "$_man_page" -out "${_man_page//.md}"
    done
  )
  (
    cd "image-${_image_pkgver}/docs"
    mkdir -vp man5
    mv -v *.5.md man5/
    for _man_page in *.md; do
      go-md2man -in "$_man_page" -out "${_man_page//.md}.1"
    done
    for _man_page in man5/*.md; do
      go-md2man -in "$_man_page" -out "${_man_page//.md}"
    done
  )
  (
    cd "podman-${_podman_pkgver}"
    go-md2man -in docs/source/markdown/containers-mounts.conf.5.md \
      -out containers-mounts.conf.5
    go-md2man -in pkg/hooks/docs/oci-hooks.5.md \
      -out oci-hooks.5
  )
  (
    cd "storage-${_storage_pkgver}"
    make -C docs
  )
}

package() {
  # directories
  install -vdm 755 "${pkgdir}/etc/containers/oci/hooks.d/"
  install -vdm 755 "${pkgdir}/etc/containers/registries.conf.d/"
  install -vdm 755 "${pkgdir}/usr/share/containers/oci/hooks.d/"
  install -vdm 755 "${pkgdir}/var/lib/containers/"

  # configs
  install -vDm 644 mounts.conf -t "${pkgdir}/etc/containers/"

  (
    cd "common-${pkgver}"
    # configs
    install -vDm 644 pkg/config/containers.conf -t "${pkgdir}/etc/containers/"
    install -vDm 644 pkg/config/containers.conf -t "${pkgdir}/usr/share/containers/"
    install -vDm 644 pkg/seccomp/seccomp.json -t "${pkgdir}/etc/containers/"
    install -vDm 644 pkg/seccomp/seccomp.json -t "${pkgdir}/usr/share/containers/"
    # man pages
    install -vDm 644 docs/*.5 -t "${pkgdir}/usr/share/man/man5/"
  )
  (
    cd "image-${_image_pkgver}"
    # configs
    install -vDm 644 registries.conf -t "${pkgdir}/etc/containers/"
    # man pages
    install -vDm 644 docs/*.1 -t "${pkgdir}/usr/share/man/man1/"
    install -vDm 644 docs/man5/*.5 -t "${pkgdir}/usr/share/man/man5/"
  )
  (
    cd "podman-${_podman_pkgver}"
    install -vDm 644 *.5 -t "${pkgdir}/usr/share/man/man5/"
  )
  (
    cd "skopeo-${_skopeo_pkgver}"
    # configs
    install -vDm 644 default-policy.json "${pkgdir}/etc/containers/policy.json"
    install -vDm 644 default.yaml -t "${pkgdir}/etc/containers/registries.d/"
  )
  (
    cd "storage-${_storage_pkgver}"
    # configs
    install -vDm 644 storage.conf -t "${pkgdir}/etc/containers/"
    install -vDm 644 storage.conf -t "${pkgdir}/usr/share/containers/"
    # man pages
    install -vDm 644 docs/*.1 -t "${pkgdir}/usr/share/man/man1/"
    install -vDm 644 docs/*.5 -t "${pkgdir}/usr/share/man/man5/"
  )
}
