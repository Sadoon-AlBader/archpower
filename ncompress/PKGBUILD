# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

pkgname=ncompress
pkgver=4.2.4.6
pkgrel=1
pkgdesc="a fast, simple LZW file compressor"
arch=(x86_64 powerpc64le)
url="https://github.com/vapier/${pkgname}"
license=('custom:PublicDomain')
provides=('compress')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('112acfc76382e7b631d6cfc8e6ff9c8fd5b3677e5d49d3d9f1657bc15ad13d13')
b2sums=('28a1c0df117f1dbf45a1d584d4c49b6178e19efd90c215b110abbf698aac3faed0c707d71554e384615a0c75cb52e09ffb39cca4809065675f7296be40313eaa')

build() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    make
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    make DESTDIR="${pkgdir}" PREFIX=/usr install_core
    # an uncompress implementation which handles gzipped files is provided by gzip
    mv "${pkgdir}"/usr/bin/uncompress{,.real}

    install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}/" LZW.INFO UNLICENSE
}

