# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Alexander 'dma147' Mieland <dma147@linux-stats.org>
# Contributor: Simon Morgan <simon@16hz.net>

pkgname=chmlib
pkgver=0.40
pkgrel=6
pkgdesc="Library for dealing with Microsoft ITSS/CHM format files"
arch=(x86_64 powerpc64le)
url="http://www.jedrea.com/chmlib/"
license=('LGPL')
depends=('glibc')
source=("http://www.jedrea.com/chmlib/chmlib-0.40.tar.bz2"
        chmlib-0.40-ppc64.patch)
md5sums=('7ea49ed8c335215c1edc6fae83e6b912'
         'faa80ea24fd4a4ac6b0c94ac280defa8')

prepare() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/chmlib-0.40-ppc64.patch
}

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  ./configure --prefix=/usr \
              --enable-examples=yes \
              --build=${CHOST}
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
}
