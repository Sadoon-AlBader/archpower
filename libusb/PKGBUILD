# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libusb
pkgver=1.0.22
pkgrel=2
pkgdesc='Library that provides generic access to USB devices'
arch=(x86_64 powerpc64le)
url='https://libusb.info/'
license=(LGPL)
depends=(glibc systemd-libs)
makedepends=(systemd)
replaces=(libusb1 libusbx)
provides=("libusbx=$pkgver" libusb-1.0.so)
conflicts=(libusbx)
source=(https://github.com/libusb/libusb/releases/download/v$pkgver/libusb-$pkgver.tar.bz2)
md5sums=('466267889daead47674df933cea9cacb')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  make -C $pkgname-$pkgver check
}

package () {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install
}
