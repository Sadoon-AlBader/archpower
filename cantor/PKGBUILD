# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=cantor
pkgver=18.12.3
pkgrel=2
pkgdesc="KDE Frontend to Mathematical Software"
url="https://kde.org/applications/education/cantor/"
arch=(x86_64 powerpc64le)
license=(GPL LGPL FDL)
groups=(kde-applications kdeedu)
depends=(analitza libspectre kpty ktexteditor knewstuff libqalculate hicolor-icon-theme discount qt5-xmlpatterns)
makedepends=(extra-cmake-modules python python2 kdoctools lua r julia)
optdepends=('maxima: Maxima backend'
            'octave: Octave backend'
            'r: R backend'
            'lua: LUA backend'
            'python: Python 3 backend'
	    'python2: Python 2 backend'
            'sagemath: SageMath backend'
            'julia: Julia backend')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('2537b8e8a9e5b72a2b3bf2b08d24c4978f52ef18ced61cdcfd2a09069f670398'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DPYTHON_INCLUDE_DIR=/usr/include/python2.7 \
    -DPYTHON_LIBRARY=/usr/lib/libpython2.7.so
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
