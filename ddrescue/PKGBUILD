# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Paul Mattal <paul@archlinux.org>

pkgname=ddrescue
pkgver=1.24
pkgrel=2
pkgdesc="GNU data recovery tool"
arch=(x86_64 powerpc64le)
url="https://www.gnu.org/software/ddrescue/ddrescue.html"
license=('GPL3')
depends=('gcc-libs')
source=("https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.lz"
        "https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.lz.sig")
sha256sums=('4b5d3feede70e3657ca6b3c7844f23131851cbb6af0cecc9721500f7d7021087'
            'SKIP')
validpgpkeys=('1D41C14B272A2219A739FA4F8FE99503132D7742') # Antonio Diaz Diaz

prepare() {
  cd $pkgname-$pkgver
  # remove hardcoded makeflag overrides
  sed -e '/CPPFLAGS=/d' \
      -e '/CXXFLAGS=/d' \
      -e '/LDFLAGS=/d' \
      -i configure
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install install-man
  install -vDm 644 {AUTHORS,ChangeLog,NEWS,README} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
#  rm "$pkgdir"/usr/share/info/dir
}
