# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=lilv
pkgver=0.24.8
pkgrel=2
pkgdesc="A C library interface to the LV2 plug-in standard"
arch=(x86_64 powerpc64le)
url="https://drobilla.net/software/lilv/"
license=('custom:ISC')
depends=('glibc')
makedepends=('libsndfile' 'lv2' 'python' 'serd' 'sord' 'sratom' 'swig' 'waf')
optdepends=('bash-completion: completion for bash'
            'libsndfile: for lv2apply'
            'python: for Python bindings')
provides=('liblilv-0.so')
source=("https://download.drobilla.net/${pkgname}-${pkgver}.tar.bz2"{,.sig}
        "${pkgname}-0.24.8-fix_translations.patch")
sha512sums=('f2b2638372a8280041f6e88e2077f2109f89dad6feedb9d07a88c6b5a05c37b62112f5d03282da4335ddd0d14339cfb49ad96a3df3de310265a40c24718be192'
            'SKIP'
            '71a38be9d33a21e2d2a9500e37efee9825124bab817c8e459790c00c28b9b27a1c9a3b611955790542b55253ae656dad58be934199a854cd7a5e16daed485b68')
validpgpkeys=('907D226E7E13FA337F014A083672782A9BF368F3')

prepare() {
  cd "${pkgname}-${pkgver}"
  # patch translation issues
  patch -Np1 -i "../${pkgname}-0.24.8-fix_translations.patch"
  # let wscript(s) find the custom waf scripts
  mkdir -pv tools
  touch __init__.py
  cp -v waflib/extras/{autoship,autowaf,lv2}.py tools/
  mkdir -pv plugins/tools/
  cp -v waflib/extras/{autoship,autowaf,lv2}.py plugins/tools/
  rm -rv waflib
  sed -e 's/waflib.extras/tools/g' \
      -e "s/load('autowaf'/load('autowaf', tooldir='tools'/g" \
      -e "s/load('lv2'/load('lv2', tooldir='tools'/g" \
      -i wscript
}

build() {
  cd "${pkgname}-${pkgver}"
  waf configure --prefix=/usr \
                --no-bash-completion \
                --dyn-manifest \
                --test
  waf -v build
}

check() {
  cd "${pkgname}-${pkgver}"
  waf test
}

package() {
  depends+=('libserd-0.so' 'libsord-0.so' 'libsratom-0.so')
  cd "${pkgname}-${pkgver}"
  waf install --destdir="${pkgdir}"
  # license
  install -vDm 644 COPYING \
    -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # bash completion
  install -vDm 644 utils/lilv.bash_completion\
    "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
}
# vim:set ts=2 sw=2 et:
