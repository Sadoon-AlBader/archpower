# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=libimagequant
pkgver=2.12.3
pkgrel=1
pkgdesc="Library for high-quality conversion of RGBA images to 8-bit indexed-color (palette) images"
arch=(x86_64 powerpc64le)
url="https://pngquant.org/lib/"
license=('BSD')
depends=('openmp')
makedepends=('llvm' 'clang')
source=("https://github.com/ImageOptim/libimagequant/archive/$pkgver/libimagequant-$pkgver.tar.gz")
sha256sums=('2bd7854657a7e52416cd9dbfc68f10a843b003950f06e74a0e51fc6de48ff13d')

prepare() {
    cd "$srcdir/$pkgname-$pkgver"
    sed -r 's/^install:.*/install:/;/install.*STATICLIB/d' -i Makefile
}

build() {
    cd "$srcdir/$pkgname-$pkgver"
    CC=clang \
    ./configure --prefix=/usr --with-openmp
    make shared imagequant.pc
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" PREFIX=/usr install
    install -Dm644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
