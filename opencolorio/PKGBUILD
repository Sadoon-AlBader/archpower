# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Dumitru Ursu <dima@ceata.org>

pkgname=opencolorio
pkgver=1.1.1
pkgrel=4
pkgdesc="A color management framework for visual effects and animation"
arch=(x86_64 powerpc64le)
url="https://opencolorio.org"
license=('BSD')
depends=('lcms2' 'tinyxml' 'yaml-cpp')
makedepends=('boost' 'cmake' 'python')
optdepends=('python: python bindings')
source=($pkgname-$pkgver.tar.gz::https://github.com/AcademySoftwareFoundation/OpenColorIO/archive/v$pkgver.tar.gz
        8d48ee8da42de2d878db7b42586db8b3c67f83e1.patch)
sha512sums=('aac3581e6d7cdb420d48dbe14487f6adc4ffb33455c569f4651e4545ac87e9039eea8e28c1a720bc544a9e27e095b12298359e8bb2cb2740fd9e2d071cf447e6'
            'fd1c6e134ef898a8715333b1e85a606736685ec0fce07188c0fba68ae048edb61dc4f107eff8de79a53dbbf226a1f84bb0a7d046a15fafb981ce02568350fd49')

prepare() {
  mv *OpenColorIO-* $pkgname-$pkgver

  cd $pkgname-$pkgver
  sed -i "s/-Werror//g" src/core/CMakeLists.txt
  sed -i "s/-Werror//g" src/pyglue/CMakeLists.txt
  sed -i "s/push(hidden)/push(default)/g" src/core/OCIOYaml.cpp

  patch -Np1 -i "$srcdir"/8d48ee8da42de2d878db7b42586db8b3c67f83e1.patch
}

build() {
  cd $pkgname-$pkgver

  mkdir -p build && cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DUSE_EXTERNAL_YAML=ON \
    -DUSE_EXTERNAL_TINYXML=ON \
    -DUSE_EXTERNAL_LCMS=ON \
    -DOCIO_USE_SSE=OFF
  make
}

package() {
  cd "$pkgname-$pkgver"/build

  make DESTDIR="$pkgdir" install

  mv "$pkgdir"/usr/lib/static/libOpenColorIO.a "$pkgdir"/usr/lib/libOpenColorIO.a
  rmdir "$pkgdir"/usr/lib/static

  install -Dm644 ../LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
