# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: carstene1ns <arch carsten-teibes de> - http://git.io/ctPKG
# Contributors: Arkham, Christoph Zeiler, JD Steffen

pkgname=eduke32
_pkgdate=20200827
_pkgrev=9251
_hash='2609d8cbf'
pkgver=$_pkgdate.$_pkgrev
pkgrel=1
pkgdesc="An advanced Duke Nukem 3D source port"
arch=(x86_64 powerpc64le)
url="http://eduke32.com/"
license=('GPL' 'custom: BUILD')
depends=('sdl2_mixer' 'libgl' 'libvpx' 'flac' 'gtk2')
makedepends=('mesa' 'glu')
optdepends=('freepats-legacy: a free soundfont for music'
            'fluidsynth: alternative midi sequencer'
            'eduke32-shareware-episode: Duke Nukem 3D Demo')
install=$pkgname.install
source=("http://dukeworld.com/$pkgname/synthesis/latest/eduke32_src_${_pkgdate}-${_pkgrev}-${_hash}.tar.xz"
        "$pkgname.png"
        "$pkgname.desktop")
sha256sums=('65b60dde5a9ee01dc393906f4138968c07abdfdb15245db6e762ba19230c4f0c'
            'b55a264b4682afabd49587d700d4c85ce6c2e7af7ce2764dd217ebe167549863'
            'cf2ea2c6a0d85ab15e30171ed5b921f1fcf5644de7af9638c0ef60de21c44f24')
	    
build() {
  cd eduke32_${_pkgdate}-${_pkgrev}-${_hash}

  # add cxxflags, set revision
  make PACKAGE_REPOSITORY=1 VC_REV=$_pkgrev #PRETTY_OUTPUT=0
}

package() {
  cd eduke32_${_pkgdate}-${_pkgrev}-${_hash}

  # install binaries, buildlicense, icon and desktop files
  install -d "$pkgdir"/usr/bin
  install -m755 eduke32 mapster32 "$pkgdir"/usr/bin
  install -Dm644 package/common/buildlic.txt "$pkgdir"/usr/share/licenses/$pkgname/buildlic.txt
  install -Dm644 ../$pkgname.png "$pkgdir"/usr/share/pixmaps/$pkgname.png
  install -Dm644 ../$pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop
}
