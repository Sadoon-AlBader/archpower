# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
pkgname=swig3
pkgver=3.0.12
pkgrel=1
pkgdesc="Generate scripting interfaces to C/C++ code"
arch=(x86_64 powerpc64le)
url="http://www.swig.org/"
license=('custom')
depends=('pcre' 'gcc-libs')
conflicts=('swig')
checkdepends=('ruby' 'python' 'java-environment' 'tcl' 'php' 'lua' 'ocaml' 'r' 'go' 'python-cffi' 'camlp4')
source=(https://downloads.sourceforge.net/swig/swig-${pkgver}.tar.gz)
sha512sums=('5eaa2e06d8e4197fd02194051db1e518325dbb074a4c55a91099ad9c55193874f577764afc9029409a41bd520a95154095f26e33ef5add5c102bb2c1d98d33eb')

build() {
  cd swig-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd swig-${pkgver}
  make check || warning "Tests failed"
}

package() {
  cd swig-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
  install -D -m644 LICENSE-UNIVERSITIES "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE-UNIVERSITIES
}
