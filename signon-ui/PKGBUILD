# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=signon-ui
pkgver=0.17+20171022
pkgrel=2
pkgdesc='UI component responsible for handling the user interactions which can happen during the login process of an online account'
arch=( x86_64 powerpc64le powerpc )
url="https://launchpad.net/online-accounts-signon-ui"
license=(GPL)
depends=(libaccounts-qt signond libnotify)
if [ ! "${CARCH}" == 'powerpc' ]; then
  depends+=(qt5-webengine)
 _commit=4368bb77d9d1abc2978af514225ba4a42c29a646
else
  depends+=(qt5-webkit)
  _commit=79d6d672564012af31cf20f3515ffd64edc787a2
fi
source=($pkgname-$pkgver.tar.gz::"https://gitlab.com/accounts-sso/signon-ui/repository/archive.tar.gz?ref=$_commit")
sha256sums=('335b1ce78fe4e2c27d318d818afc69fc9d9385751570448dfdead54f6be7ab04')

prepare() {
  mv signon-ui-$_commit-$_commit $pkgname-$pkgver
  cd $pkgname-$pkgver

  # Do not install tests
  sed -e 's|src \\|src|' -e '/tests/d' -i signon-ui.pro
}

build() {
  cd $pkgname-$pkgver

  qmake-qt5 PREFIX=/usr LIBDIR=/usr/lib
  make
}

package() {
  cd $pkgname-$pkgver

  make INSTALL_ROOT="$pkgdir" install
}
