# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
pkgname=yaboot
pkgver=1.3.17
pkgrel=1
pkgdesc="A PowerPC bootloader for Open Firmware based machines"
arch=(powerpc)
url="http://yaboot.ozlabs.org/"
license=('GPL')
depends=('powerpc-utils' 'hfsprogs')
makedepends=('git')
_commont='dc5421b84b9633b00e4d9ea53fc97e1cf36cf3a9'
_e2fsprogs_ver=1.41.14
source=("git://ozlabs.org/srv/projects/yaboot/yaboot.git#commit=dc5421b84b9633b00e4d9ea53fc97e1cf36cf3a9"
	yaboot-modern.patch
	http://ftp.be.debian.org/pub/linux/kernel/people/tytso/e2fsprogs/e2fsprogs-${_e2fsprogs_ver}.tar.gz)
sha256sums=('SKIP'
            '9b4add400d98835a0d872e5864f4fa18b46670de5e01c21fec1703f69b283067'
            '3f8ac1fedd7c4bec480afcbe4acabdd4ac59ec0446a0fd50c8975cd0aad7b176')
validpgpkeys=('4774128D015CF860AA791D625E442088ED632E87'  # Tony Breeds (Signing Key) <tony@bakeyournoodle.com>
	      '3AB057B7E78D945C8C5591FBD36F769BC11804F0') # Theodore Ts'o <tytso@mit.edu>

prepare() {
	cd "${srcdir}/e2fsprogs-${_e2fsprogs_ver}"
	CFLAGS="-Os -fno-stack-protector -no-pie -fgnu89-inline -fno-builtin-malloc -DNDEBUG" \
	./configure --disable-testio-debug --disable-debugfs --disable-tls
	make -C lib/et
	make -C lib/ext2fs
	cp lib/libcom_err.a lib/libext2fs.a ${srcdir}/${pkgname}/

	cd "${srcdir}/${pkgname}"
	patch -Np1 -i ${srcdir}/yaboot-modern.patch
}

build() {
	cd "${srcdir}/${pkgname}"
	make -j1
}

package() {
	cd "${srcdir}/${pkgname}"
	make ROOT="${pkgdir}" PREFIX="/usr" install
}
