# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=gssdp
pkgver=1.2.2
pkgrel=2
pkgdesc="A GObject-based API for handling resource discovery and announcement over SSDP"
url="https://wiki.gnome.org/Projects/GUPnP"
arch=(x86_64 powerpc64le)
license=(LGPL)
depends=(libsoup)
makedepends=(gobject-introspection gtk-doc vala meson git gtk3)
optdepends=('gtk3: gssdp-device-sniffer')
_commit=0e87f5aecb661516983767c996d1834909f6901f  # tags/gssdp-1.2.2^0
source=("git+https://gitlab.gnome.org/GNOME/gssdp.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^gssdp-//;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build -D gtk_doc=true
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
