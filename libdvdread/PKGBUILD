# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Thomas Baechler <thomas@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=libdvdread
pkgver=6.1.0
pkgrel=1
pkgdesc="Provides a simple foundation for reading DVD video disks"
arch=(x86_64 powerpc64le)
url="https://www.videolan.org/developers/libdvdnav.html"
license=('GPL')
depends=('glibc')
makedepends=('libdvdcss' 'git')
optdepends=('libdvdcss: for decoding encrypted DVDs')
_commit=d413571ce39acd404523b6742ba361215f6ada68  # tags/6.1.0^0
source=("git+https://code.videolan.org/videolan/libdvdread.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed -e 's/-/+/g'
}

prepare() {
  cd $pkgname
  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" install
}
