# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libqmi
pkgver=1.24.2
pkgrel=1
pkgdesc="QMI modem protocol helper library"
arch=(x86_64 powerpc64le)
url="https://www.freedesktop.org/wiki/Software/libqmi/"
license=(GPL2)
depends=(libmbim libgudev)
makedepends=(gtk-doc python git help2man)
_commit=4c2337275950ad1cd5caaf7f6e6dda5bfdb45045  # tags/1.24.2
source=("git+https://gitlab.freedesktop.org/mobile-broadband/libqmi.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib --disable-static --enable-gtk-doc
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
