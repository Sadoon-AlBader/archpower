# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: David Herrmann <dh.herrmann@gmail.com>

pkgname=dbus-broker
pkgver=21
pkgrel=2
pkgdesc="Linux D-Bus Message Broker"
url="https://groups.google.com/forum/#!forum/bus1-devel"
arch=(x86_64 powerpc64le)
license=(Apache)
depends=(systemd-libs expat audit)
makedepends=(git meson systemd python-docutils)
_commit=d03c640cf52c74688881c46f171c526ba707016f  # tags/v21^0
source=("git+https://github.com/bus1/dbus-broker#commit=$_commit"
        git+https://github.com/c-util/c-{rbtree,list,dvar,utf8,shquote,ini,stdaux})
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname

  git submodule init

  local sm
  for sm in c-{rbtree,list,dvar,utf8,shquote,ini,stdaux}; do
    git config --local submodule.subprojects/$sm.url "$srcdir/$sm"
  done

  git submodule update
}

build() {
  arch-meson $pkgname build \
    -D audit=true \
    -D docs=true \
    -D system-console-users=gdm,sddm,lightdm,lxdm
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set sw=2 et:
