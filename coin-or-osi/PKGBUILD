# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=coin-or-osi
pkgver=0.108.4
pkgrel=1
pkgdesc="COIN-OR Open Solver Interface"
arch=(x86_64 powerpc64le)
url="https://projects.coin-or.org/Osi"
license=(EPL)
groups=(coin-or)
depends=(coin-or-coinutils)
makedepends=(gcc-fortran)
source=("https://www.coin-or.org/download/source/Osi/Osi-${pkgver}.tgz")
sha256sums=('45a72e7019d33b9cf9c3f9fde30a9cb1e822e3b6897239c7dd67bc0060837c8d')

build() {
  cd Osi-$pkgver
  COIN_SKIP_PROJECTS="Sample" \
  ./configure --prefix=/usr --enable-dependency-linking \
              --with-coinutils-lib="$(pkg-config --libs coinutils)" \
              --with-coinutils-incdir="/usr/include/coin/" \
              --build=${CHOST}
  make
}

package() {
  cd Osi-$pkgver
  PKG_CONFIG_LIBDIR="${pkgdir}/usr/lib/pkgconfig/" \
  make DESTDIR="$pkgdir" install
}
