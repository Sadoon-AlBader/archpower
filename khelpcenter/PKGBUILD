# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=khelpcenter
pkgver=18.12.3
pkgrel=1
pkgdesc="Application to show KDE Applications' documentation"
arch=(x86_64 powerpc64le)
url='https://userbase.kde.org/KHelpCenter'
license=(LGPL)
groups=(kde-applications kdebase)
depends=(khtml kdoctools xapian-core grantlee kinit)
makedepends=(extra-cmake-modules)
conflicts=(khelpcenter4 kdebase-runtime-doc)
replaces=(khelpcenter4 kdebase-runtime-doc)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('5b4a9ed17d0898c74cf7fd1612e2d055086d5e04148b3b17df5977255fc240b8'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
