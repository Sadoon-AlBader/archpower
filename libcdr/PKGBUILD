# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: megadriver <megadriver at gmx dot com>
# Contributor: Luca Bennati <lucak3 AT gmail DOT com>

pkgname=libcdr
pkgver=0.1.5
pkgrel=3
pkgdesc="CorelDraw file format importer library for LibreOffice"
arch=(x86_64 powerpc64le)
url="https://wiki.documentfoundation.org/DLP/Libraries/libcdr"
license=('GPL2' 'LGPL2.1' 'MPL')
depends=('libwpd' 'lcms2' 'icu' 'librevenge')
makedepends=('libwpg' 'boost' 'doxygen' 'cppunit')
source=(https://dev-www.libreoffice.org/src/$pkgname/$pkgname-$pkgver.tar.xz)
sha256sums=('6ace5c499a8be34ad871e825442ce388614ae2d8675c4381756a7319429e3a48')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --build=${CHOST}
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
